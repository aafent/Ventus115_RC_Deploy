! ***********************************************************************
! ***** REPORT      : uhmer.sqr                                      ****
! ***** DATE        : 28/04/93          - 29/03/95                   ****
! ***** APPLICATION : ÃÅÍÉÊÇ ËÏÃÉÓÔÉÊÇ                               ****
! ***** NOTE        : --- ÇÌÅÑÏËÏÃÉÏ ÏÌÁÄÁÓ    ÃÅÍ.ËÏÃ.  ---         ****
! ***********************************************************************

#DEFINE   PAGE-Y                 62
#DEFINE   PAGE-X                132 
#DEFINE   HEAD-SIZE               4
#DEFINE   FOOT-SIZE               5
#DEFINE   HEAD-LINE               4

#DEFINE   DATEMASK         DD/MM/YY
#DEFINE   MASK1              999999
#DEFINE   MASK2     9999,999,999.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

 create-array name=   hmer_arr size=600 field=total_xre:number
                                       field=total_pis:number
                                       field=perigrafh:char
                                       field=hmerologio:char
                                       field=pre_xre:number
                                       field=pre_pis:number
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Áğü Çìåñïìçíßá  : '
 input $evshmnia  'Eùò Çìåñïìçíßá  : '
 input $omhm      'Ïìáäá Çìåñïëïã. : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 unstring $omhm by '"' into $filler1 $omada $filler1

 move 0 to #maxarr
 move $L_CID to $cid

 move '1 jan 1992' to $datestart


 do init
 do loadall
 do main

End-Report

! ****************************************************************************
begin-procedure init
begin-select
xrhsh						() NOP
convert(datetime, xrhsh_arxh) &xrhsh_arxh 	() NOP
	move &xrhsh      to #xrhsh
        move &xrhsh_arxh to $datestart
from ap_xrhsh
where cid = $cid
  and $apohm between xrhsh_arxh and xrhsh_telos
end-select
end-procedure
! ****************************************************************************

begin-procedure main

  move 'F' to $pageflag
  move 'T' to $firstpage

  let #poso_x =  0
  let #poso_p =  0

begin-select
  if  #current-line = {HEAD-SIZE} + 1
  and $firstpage = 'F'
      print '_ '                     (,59,35) fill
      print 'ÁĞÏ ÌÅÔÁÖÏÑÁ : '        (,59,35)
      print #poso_x                  (,+1,15) edit {MASK2}
      print #poso_p                  (,+1,15) edit {MASK2}
      next-listing
  else 
      let $firstpage = 'F'
  end-if
convert(datetime,a.hmer_e) &a.hmer_e (,10,08) on-break 
                                              print=change/top-page 
                                              before=hmerbreak 
                                              save=$hmer 
                                              level=1
                                              after=totalbreak 
a.mhxar                              (,01,06) edit {MASK1}
b.perigrafh                          (,+1,25) 
a.logar                              ()       NOP
  do  makelogform($cid, #xrhsh, &a.logar, $logarpr)
  print $logarpr                     (,+1,15)
a.parid                              (,+1,08)
a.aitia                              (,+1,35)
a.xrevsh                             (,+1,15) edit {MASK2}
a.pistvsh 			     (,+1,15) edit {MASK2}
a.hmerologio    &hmer                ()       NOP

      let #poso_x = #poso_x + &a.xrevsh
      let #poso_p = #poso_p + &a.pistvsh

   do addtotals(&hmer, &a.xrevsh, &a.pistvsh)
   next-listing

  if  #current-line = {PAGE-Y} - 2
      print '_ '                     (,59,35) fill
      print 'ÓÅ  ÌÅÔÁÖÏÑÁ : '        (,59,35)
      print #poso_x                  (,+1,15) edit {MASK2}
      print #poso_p                  (,+1,15) edit {MASK2}
      next-listing
  end-if

!                                                  (v) 1.9.94 Nick
from  gl_kin a, gl_logar b, gl_hmerandomada ss
where a.cid        = $cid
and   a.cid        = b.cid
and   a.logar      = b.logar
and   b.xrhsh      = #xrhsh
and   a.hmer_e     between $apohm and $evshm
!and   a.mhxar      is not null
and   a.hmerologio = ss.hmerologio
and   ss.omada     = $omada
[$SQLCMD]
order by a.mhxar
end-select
next-listing 
end-procedure

! ****************************************************************************

begin-procedure totalbreak
  next-listing
  move 0 to #pro_xre
  move 0 to #pro_pis
  move 0 to #gen_xre
  move 0 to #gen_pis
  move 1 to #inx
  while #maxarr>=#inx
     get $hmerologio      from hmer_arr(#inx) hmerologio
     do getdioru($hmerologio,$hmer,$hmer,#dio_xre,#dio_pis)
     get $perigrafh       from hmer_arr(#inx) perigrafh
     get #pre_xrevsh      from hmer_arr(#inx) pre_xre
     get #pre_pistvsh     from hmer_arr(#inx) pre_pis
     get #total_xrevsh    from hmer_arr(#inx) total_xre
     get #total_pistvsh   from hmer_arr(#inx) total_pis
     print 'ĞÑÏÇÃÏÕÌÅÍÁ ÓÕÍÏËÁ '  (,30)
     print $perigrafh             (,+1,20)
     print ' : '                  (,+1,03)
     print #pre_xrevsh            (,+1,15) edit {MASK2}
     print #pre_pistvsh           (,+1,15) edit {MASK2}
     next-listing
     let #total_xrevsh =#total_xrevsh+ #dio_xre
     let #total_pistvsh=#total_pistvsh+#dio_pis
     print 'ÓÕÍÏËÁ ÇÌÅÑÁÓ      '  (,30)
     print $perigrafh             (,+1,20)
     print ' : '                  (,+1,03)
     print #total_xrevsh          (,+1,15) edit {MASK2}
     print #total_pistvsh         (,+1,15) edit {MASK2}
     next-listing

     let #pro_xre=#pre_xrevsh +#total_xrevsh
     let #pro_pis=#pre_pistvsh+#total_pistvsh
     print 'ĞÑÏÏÄÅÕÔÉÊÁ ÓÕÍÏËÁ '  (,30)
     print $perigrafh             (,+1,20)
     print ' : '                  (,+1,03)
     print #pro_xre               (,+1,15) edit {MASK2}
     print #pro_pis               (,+1,15) edit {MASK2}
     let #gen_xre=#gen_xre+#pro_xre
     let #gen_pis=#gen_pis+#pro_pis
     next-listing
     put #pro_xre into hmer_arr(#inx) pre_xre
     put #pro_pis into hmer_arr(#inx) pre_pis
     move 0 to #tmp
     put #tmp   into hmer_arr(#inx) total_xre
     put #tmp   into hmer_arr(#inx) total_pis
     let #inx=#inx+1
  end-while
  next-listing
     print 'ÃÅÍÉÊÁ ĞÑÏÏÄÅÕÔÉÊÁ ÓÕÍÏËÁ' (,30)
     print ' '                    (,+1,14)
     print ' : '                  (,+1,03)
     print #gen_xre               (,+1,15) edit {MASK2}
     print #gen_pis               (,+1,15) edit {MASK2}
   next-listing
end-procedure

! ****************************************************************************

begin-procedure getdioru($hmerologio,$hmer1,$hmer2,:#xrevsh,:#pistvsh)
  move 0 to #xrevsh
  move 0 to #pistvsh
begin-select
sum(xrevsh)    &dio_xre    () NOP
sum(pistvsh)   &dio_pis    () NOP
  move &dio_xre  to #xrevsh
  move &dio_pis  to #pistvsh
from  gl_dioru a, ap_user b
where a.cid        = b.company
and   b.suname     = suser_name()
and   a.hmerologio = $hmerologio
and   a.hmeromhnia  between $hmer1 and $hmer2
end-select
end-procedure

! ****************************************************************************

begin-procedure hmerbreak
  if $pageflag='T' 
     new-page
  else 
     next-listing
  end-if
  next-listing
  print 'Çìåñïìçíßá : '  (1) center
  print &a.hmer_e        (,+2) edit {DATEMASK}
  next-listing
  next-listing
  move 'T' to $pageflag

  move 'T' to $firstpage
  let #poso_x =  0
  let #poso_p =  0
end-procedure

! ****************************************************************************

begin-procedure makelogform($cid, #xrhsh, $inlogar, :$outlogar)
  execute glp_logfrm @cid       = $cid,
                     @xrhsh     = #xrhsh,
                     @in_logar  = $inlogar, 
                     @out_logar = $outlogar output
end-procedure

! ****************************************************************************

begin-procedure  addtotals($hmerologio, #xrevsh, #pistvsh)
  move 0 to #inx
  while #inx<200
    let #inx=#inx+1
    get $tmphmer from hmer_arr(#inx) hmerologio
    if $tmphmer=$hmerologio
       break
    end-if
  end-while

     get #tmp from hmer_arr(#inx) total_xre
     let #tmp = #tmp + #xrevsh
     put #tmp into hmer_arr(#inx) total_xre
 
     get #tmp from hmer_arr(#inx) total_pis
     let #tmp = #tmp + #pistvsh
     put #tmp into hmer_arr(#inx) total_pis
 

end-procedure


! ****************************************************************************

begin-procedure loadall

begin-select
dateadd(dd,-1,$apohm) &evshmeromhnia
end-select

begin-select 
a.hmerologio          () NOP
a.seira               () NOP
c.perigrafh           () NOP

  let #inx=&a.seira
  do getdioru(&a.hmerologio,$datestart,&evshmeromhnia,#dio_xre,#dio_pis)
  put 0        into hmer_arr(#inx) total_xre
  put 0        into hmer_arr(#inx) total_pis
  put #dio_xre into hmer_arr(#inx) pre_xre
  put #dio_pis into hmer_arr(#inx) pre_pis
  put &c.perigrafh  into hmer_arr(#inx) perigrafh
  put &a.hmerologio into hmer_arr(#inx) hmerologio
  if &a.seira>#maxarr
     move &a.seira to #maxarr
  end-if

from gl_hmerandomada a, ap_user b, gl_hmerologia c
where a.cid=b.company
and   b.suname=suser_name()
and   a.hmerologio=c.kvdikos
and   a.cid=c.cid

! (v) 1.9.94 Nick
and   a.omada = $omada
end-select

  execute do=loadall2 glp_hmomadatot 
          @hmer_apo=$datestart, @hmer_evs=&evshmeromhnia, 
          @omada=$omada
     into &hmerologio    char(03),
          &perigrafh     char(30),
          &xrevsh        float,
          &pistvsh       float,
          &seira         int

end-procedure

! ------------------------------------------------------------------------------

begin-procedure loadall2

   move &seira to #inx

   get #tmp from hmer_arr(#inx) pre_xre
   let #tmp=#tmp+&xrevsh
   put #tmp into hmer_arr(#inx) pre_xre

   get #tmp from hmer_arr(#inx) pre_pis
   let #tmp=#tmp+&pistvsh
   put #tmp into hmer_arr(#inx) pre_pis

end-procedure 

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD03.inc"

!  print 'ÇÌÅÑÏËÏÃÉÏ ÅÃÃÑÁÖÙÍ ÃÅÍÉÊÇÓ ËÏÃÉÓÔÉÊÇÓ' (2,+1) center

 let $msg='ĞÅÑÉÏÄÏÕ '||$apohmpr||' - '||$evshmpr

 ! print $msg (3,+1) center

   print 'Ìç÷.Áñ'            (5,01,06)
   print 'Ğ å ñ é ã ñ á ö Ş'  (,+1,25) 
   print 'Ëïãáñéáóìüò'        (,+1,15)
   print 'Ğáñ/êü  '           (,+1,08)
   print 'Á é ô é ï ë ï ã ß á'(,+1,35)
   print '         ×ñİùóç'    (,+1,15)
   print '        Ğßóôùóç'    (,+1,15)

End-Heading

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	3	1	21	uhmer	rs	X	01},TITLE{Èåùñçìİíï Çìåñïëüãéï ÃË}
!	PARCOUNT{2}
!	PAR1{Çì/êŞ Ğåñßïäïò	DUMP	2	ÁĞÏ-ÅÙÓ	datetime		X}
!	PAR2{Ïìáäá çìåñïëïãéùí	DUMP2	1	=	varchar(3)		X}
!REPORTHELP{F}
!</pcalib>
