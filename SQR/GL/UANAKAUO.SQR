! ***********************************************************************
! ***** PROLOGIC S.A.                                                ****
! ***** REPORT      : uanakauol.sqr                                  ****
! ***** DATE        : 22/05/95                                       ****
! ***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
! ***** AUTHOR      : Mal Nick                                       ****
! ***** NOTE        : ---  ΑΝΑΛΥΤΙΚΟ ΚΑΘΟΛΙΚΟ                ---     ****
! ***********************************************************************
 
#DEFINE   PAGE-Y     62
#DEFINE   PAGE-X    132 
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   MASK1     999,999,999.99c
#DEFINE   MASK2                999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 move 1 to #apolevel
 move 1 to #evslevel

!************** move '1 jan 2005' to $datestart

 move $L_CID to $cid
 move $L_XRHSH to #xrhsh

 do find_datestart($cid,#xrhsh,$datestart)
 do main($cid,#xrhsh,$SQLCMD,$datestart,$apohm,$evshm,#apolevel,#evslevel)

End-Report

! ****************************************************************************

begin-procedure find_datestart($cid, #xrhsh, :$datestart)

begin-select -C1
xrhsh_arxh    () NOP
   move &xrhsh_arxh  to $datestart
from ap_xrhsh
where cid = $cid
  and xrhsh = #xrhsh
end-select


end-procedure


! ****************************************************************************

begin-procedure main($cid,#xrhsh,$SQLCMD,$datestart,$apohm,$evshm,#apolevel,#evslevel)


begin-select
a.baumos                       (,1) on-break print=change/top-page 
                                            after=levelbreak 
a.logar                       ()   NOP
a.perigrafh       &perigrafh  ()   NOP

      do makelogform(&a.logar,$logarpr)
      print ' '             (  , 1,{PAGE-X})
      next-listing
      next-listing 
      print $logarpr        (  , 1,10)             
      print &perigrafh      (  ,+0,40)              

      do ypol($logarpr,&perigrafh,$cid,&a.logar,$datestart,$apohm,#p_xrevsh,#p_pistvsh)
      print '--- ΕΚ ΜΕΤΑΦΟΡΑΣ :' (  ,+2,18) 
      print #p_xrevsh                (  ,+1,14) edit {MASK1}
      print #p_pistvsh               (  ,+1,14) edit {MASK1}

      let #px  = #p_xrevsh
      let #pp  = #p_pistvsh

      do printrest($logarpr,&perigrafh,$cid,&a.logar,$apohm,$evshm,#xrevsh,#pistvsh,#px,#pp,#diff_x,#diff_p)

    if #px !=0 or #pp !=0
      let #p_xrevsh  =  #px
      let #p_pistvsh =  #pp
    end-if

      let #newdiff = #p_xrevsh - #p_pistvsh
       if #newdiff > 0  
          let #diff_x = #newdiff
          move 0 to #diff_p
       else
          let #diff_p = (#newdiff * -1)
          move 0 to #diff_x
       end-if

   next-listing

  let $msg = '                                         -------------- -------------- -------------- -------------- -------------- --------------'

         print $msg     (  , 1,{PAGE-X})

   next-listing

         print '          Σ Υ Ν Ο Λ Α  ΛΟΓΑΡΙΑΣΜΟΥ :' (  , 1,40)              
         print #xrevsh                      (  ,+1,14) edit {MASK1}  
         print #pistvsh                     (  ,+1,14) edit {MASK1}   

         print #p_xrevsh                (  ,+1,14) edit {MASK1}
           let #s_px = #s_px + #p_xrevsh
         print #p_pistvsh               (  ,+1,14) edit {MASK1}
           let #s_pp = #s_pp + #p_pistvsh
         
         print #diff_x                  (  ,+1,14) edit {MASK1}
         print #diff_p                  (  ,+1,14) edit {MASK1}
           let #s_x = #s_x + #xrevsh
           let #s_p = #s_p + #pistvsh
           let #s_diffx = #s_diffx + #diff_x
           let #s_diffp = #s_diffp + #diff_p


 next-listing

from  gl_logar a
where a.cid = $cid
and   a.baumos between #apolevel and #evslevel
and xrhsh=#xrhsh
!group by a.logar
order by a.logar, a.baumos

end-select


  let $msg = '                                         ============== ============== ============== ============== ============== =============='

         next-listing 
         next-listing 
         print $msg     (  , 1,{PAGE-X}) 

   next-listing

         print 'Γ Ε Ν Ι Κ Α    Σ Υ Ν Ο Λ Α   : '  (  , 1,40)              
         print #s_x                               (  ,+1,14) edit {MASK1}  
         print #s_p                               (  ,+1,14) edit {MASK1}   
         print #s_px                              (  ,+1,14) edit {MASK1}
         print #s_pp                              (  ,+1,14) edit {MASK1}
         print #s_diffx                           (  ,+1,14) edit {MASK1}
         print #s_diffp                           (  ,+1,14) edit {MASK1}

end-procedure

! ****************************************************************************

begin-procedure levelbreak
end-procedure

! ****************************************************************************

begin-procedure makelogform($inlogar,:$outlogar)
  execute glp_logfrm @in_logar=$inlogar, @out_logar=$outlogar output
end-procedure

! ****************************************************************************

begin-procedure printrest ($logarpr,$perigrafh,$cid,$logar,$apohm,$evshm,:#xrevsh,:#pistvsh,:#px,:#pp,:#diff_x,:#diff_p)

  move 0 to #s_x
  move 0 to #s_p 
  move 0 to #xrevsh
  move 0 to #pistvsh
  move 0 to #diff_x
  move 0 to #diff_p

begin-select
convert(datetime,b.hmeromhnia)         &hm    () NOP
sum(b.xrevsh)                          &xre   () NOP
sum(b.pistvsh)                         &pis   () NOP

  let #xrevsh    = &xre
  let #pistvsh   = &pis
  let #px        = #px  + &xre
  let #pp        = #pp  + &pis
  let #s_x       = #s_x + #xrevsh
  let #s_p       = #s_p + #pistvsh
  let #diff      = #px  - #pp 

      next-listing
      print &hm               (,33, 8) edit {DATEMASK}
      print #xrevsh           (,+1,14) edit {MASK1}  
      print #pistvsh          (,+1,14) edit {MASK1}   
      print #px               (,+1,14) edit {MASK1}  
      print #pp               (,+1,14) edit {MASK1}   
          
      if #diff > 0 
         let #diff_x = #diff_x + #diff
         print #diff           ( ,+1,14) edit {MASK1} 
      else
         let #diff   = (#diff  * -1)   
         let #diff_p = #diff_p + #diff
         print #diff           ( ,+14,14) edit {MASK1}  
      end-if

from gl_ypol b
where b.logar = $logar
and   b.cid   = $cid
! and   b.isoz  = 1
and   b.hmeromhnia between $apohm and $evshm
group by b.hmeromhnia
order by b.hmeromhnia

end-select

  move #s_x to #xrevsh
  move #s_p to #pistvsh

end-procedure

! ****************************************************************************

begin-procedure ypol($logarpr,$perigrafh,$cid,$a.logar,$datestart,$evs,:#p_xrevsh,:#p_pistvsh)

    move 0 to #p_xrevsh
    move 0 to #p_pistvsh

begin-select 
sum(b.xrevsh)        &xrep   () NOP
sum(b.pistvsh)       &pisp   () NOP

   let #p_xrevsh  = &xrep
   let #p_pistvsh = &pisp

from  gl_ypol b
where b.logar = $a.logar
and   b.cid   = $cid
! and   b.isoz  = 1
and   b.hmeromhnia >=   $datestart 
and   b.hmeromhnia <   $evs 

end-select
end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD03.inc"

  print 'ΓΕΝΙΚΟ ΚΑΘΟΛΙΚΟ ΓΕΝΙΚΗΣ ΛΟΓΙΣΤΙΚΗΣ' (2,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print ' '                        (5,1,15)
   print ' '                        (,+0,21)
   print ' '                        (,+0,13)
   print '-----Περιόδου-----'       (,+3,21)
   print ' '                        (,+0,07)
   print '---Προοδευτικά----'       (,+1,21)
   print ' '                        (,+0,07)
   print '-----Υπόλοιπα-----'       (,+1,21)

   print 'Λογαριασμός'              (6,1,15)
   print 'Περιγραφή Λογαριασμού'    (,+0,21)
   print ' '                        (,+0,13)
   print 'Χρέωση  -  Πίστωση'       (,+3,21)
   print ' '                        (,+0,07)
   print 'Χρέωση  -  Πίστωση'       (,+1,21)
   print ' '                        (,+0,07)
   print 'Χρέωση  -  Πίστωση'       (,+1,21)

End-Heading
!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	3	10	5008	uanakauo	rs	X	01},TITLE{Θεωρημένο Γενικό Καθολικό  Κ.Β.Σ.}
!	PARCOUNT{1}
!	PAR1{Ημ/κή Περίοδος	DUMP	2	ΑΠΟ-ΕΩΣ	datetime		X}
!REPORTHELP{T},RNHELP{Ισοζύγιο (1) Εναρξης, (2) Εγγραφών Ισολογισμού, (3) Γενικό},
!</pcalib>
