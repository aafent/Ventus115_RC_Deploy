!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : isoz_doy2.sqr                                  ****
!***** DATE        : 21/04/97                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** AUTHOR      : ΘΕΟΤΟΚΑΣ ΙΩΑΝΝΗΣ - ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ           ****
!***** NOTE        : --- ΙΣΟΖΥΓΙΟ ΓΙΑ ΤΗΝ ΕΦΟΡΙΑ (FILE)      ---    ****
!***********************************************************************

#DEFINE   PAGE-Y    60
#DEFINE   PAGE-X   132 
#DEFINE   HEAD-SIZE  6
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK  MMYY
#DEFINE   MASK1     999999999999.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

 CREATE-ARRAY NAME=totals
  	      SIZE=25 
              FIELD=the:number=0
	      FIELD=arn:number=0

begin-sql -C1
 create table ##tmplogar
  ( logar      varchar(25) null,
    perigrafh  varchar(70) null,
    baumos     int         null
  )
end-sql

 no-formfeed

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID   to $cid
 move $L_XRHSH to #xrhsh
 input $enop_flag  'Ενοποιημένο    : '
 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '
 input $logkeis 'Ιδιαίτεροι Λογαριασμοί Κλεισίματος : '

 display $apohmnia
 unstring $enop_flag by '"' into $filler $enop_flag $filler
 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output
 unstring $logkeis by '"' into $filler $logkeis $filler


 let $CIDCMD='and a.cid = "'||$cid||'"' 
!
! TO ENOP FLAG EXEI XRHSH MONO STHN ELAIOYRGIKH
!
 
 if $enop_flag = 'ν' or $enop_flag='Ν' or $enop_flag='n' or $enop_flag = 'N'
    move 'T' to $enop_flag
 else
    move 'F' to $enop_flag
 end-if
 
 display 'ENOP FLAG : ' noline
 display $enop_flag 
 if $enop_flag = 'T'
    move '1' to $cid
    move 'and a.cid in (select cid1 from cu_cid) ' to $CIDCMD
 end-if

if $logkeis = 'ν' or $logkeis='Ν' or $logkeis='n' or $logkeis = 'N'
    move '1' to $isoztype
 else
    move '2' to $isoztype
 end-if
 

 
 do init($cid, $apohm, #xrhsh)
 do header
 
 do colllogar($CIDCMD,#xrhsh)
 do main
       
End-Report

! ****************************************************************************
begin-procedure init($cid, $apohm, :#xrhsh)
begin-select 
xrhsh						() NOP
   	move &xrhsh      to #xrhsh
from ap_xrhsh
where cid = $cid
and $apohm between xrhsh_arxh and xrhsh_telos
end-select
end-procedure
! ****************************************************************************

begin-procedure main

 let #countth = 0
 let #countar = 0

 move 0 to #maxbaumos
 move 0 to #i
 while #i < 24
    let #i = #i + 1
    put 0 0 into totals(#i) the arn
 end-while

begin-select -C1
a.logar 				() NOP
a.perigrafh                       	() NOP
a.baumos                           	() NOP

      do makelogform($cid, #xrhsh, &a.logar, $logarpr)
      do getypol($isoztype,$CIDCMD, &a.logar, $apohm, $evshm, #xrevsh, #pistvsh)

      let #ypol = #xrevsh - #pistvsh

      if #ypol <> 0 or (#ypol=0 and &a.baumos <> 3 and (substr($logarpr,1,1) = '6' or substr($logarpr,1,1) = '7' or substr($logarpr,1,1) = '8'))
         print 'TRB'                    (,01,03)
         print $logarpr			(,04,25)
         print &a.perigrafh             (,29,75)

         move &a.baumos to #baumos
         if #baumos > #maxbaumos
            move #baumos to #maxbaumos
         end-if
 
         let #aypol = abs(#ypol)
         
         if #ypol < 0
            print '-'                   (,104,1)
            array-add #aypol to totals(#baumos) arn
         else
            print '+'                   (,104,1)
            let #totthe  = #totthe  + abs(#ypol)
	    array-add #aypol to totals(#baumos) the
         end-if
 
 	! voula (v)
	if &a.baumos = 1		
         if #ypol > 0 
           let #countth = #countth + #aypol
	 else
	   let #countar = #countar + #aypol
	 end-if   	
	end-if
	! voula (^)

         let #pr_ypol = #aypol
         print #pr_ypol                 (,105,15)  edit {MASK1}

         next-listing

      end-if

from ##tmplogar a
order by a.logar, a.baumos
end-select

 display '========================================================'

 move 0 to #i
 move 0 to #totthe
 move 0 to #totarn
 while #i < #maxbaumos
    let #i = #i + 1
    get #the #arn from totals(#i) the arn
    add #the to #totthe
    add #arn to #totarn
    move #i to $i 999
    display 'Σύνολα για τον βαθμό : ' noline
    display $i             noline
    display ' Θετικά   : ' noline
    display #the           noline
    display ' Αρνητικά : ' noline
    display #arn           
 end-while
 display ' '
 display '*** Σύνολο Θετικών   : ' noline
 display #totthe
 display '*** Σύνολο Αρνητικων : ' noline
 display #totarn
 display '========================================================'

end-procedure


begin-procedure colllogar($CIDCMD,#xrhsh)

display $CIDCMD

begin-select -C2
a.cid
   display 'WCID :' noline
   display &a.cid
   do filllogar(&a.cid,#xrhsh)
from ap_comp a
where 1=1
[$CIDCMD]
end-select

end-procedure


begin-procedure filllogar($cid,#xrhsh)

begin-sql -C1

insert into ##tmplogar (logar,perigrafh,baumos)
select a.logar, a.perigrafh, a.baumos
from gl_logar a
where a.xrhsh = #xrhsh 
 and a.cid = $cid
 and (
 (a.logar like '[01345]%' and a.baumos < 3) or
 (a.logar like '[2678]%') or
 (a.logar like '54%')
     )
and not exists(select 1 from ##tmplogar b where b.logar = a.logar)
end-sql

end-procedure

! ****************************************************************************

begin-procedure getypol($isoztype, $CIDCMD, $logar, $apohm, $evshm, :#xrevsh, :#pistvsh)

move 0 to #xrevsh
move 0 to #pistvsh

if $isoztype='1'
   move 'and a.isoz IN (1,2)' to $SQLISOZ
else
   move '' to $SQLISOZ
end-if   

begin-select -C2
sum(a.xrevsh)       &a.xrevsh        () NOP
sum(a.pistvsh)      &a.pistvsh       () NOP
	move &a.xrevsh  to #xrevsh
        move &a.pistvsh to #pistvsh
from gl_ypol a  
where a.logar  = $logar
[$CIDCMD]
and a.hmeromhnia between $apohm and $evshm
[$SQLISOZ]
end-select


end-procedure

! ****************************************************************************
 
! ****************************************************************************

begin-procedure makelogform($cid, #xrhsh, $inlogar,:$outlogar)
  execute glp_logfrm @cid = $cid, @xrhsh = #xrhsh, 
                     @in_logar=$inlogar, @out_logar=$outlogar output
end-procedure

! ****************************************************************************
begin-procedure header

  do get_cid_info($cid, #xrhsh, #tmp, $tmp, $afm, $tmp, $tmp, $tmp, $tmp, $tmp, $tmp, $tmp, $tmp)

  do hmeromhnia($apohmpr, $evshmpr, $hmer)

  print 'HDR'			(,01,03)
  print $afm			(,04,09)
  print $hmer			(,13,09)
  next-listing

end-procedure

! ****************************************************************************
begin-procedure hmeromhnia($apohmpr, $evshmpr, :$hmer)
display 'apohmpr: ' noline
display $apohmpr

display 'evshmpr: ' noline
display $evshmpr

display 'hmer: ' noline
display $hmer


! 2002-09-16 ΚΑΤΕΡΙΝΑ ΣΑΛΑΓΙΑΝΝΗ
let $apomhnas = substr($apohmpr,4,2)
let $apoetos = substr($apohmpr,7,2)

let $evsmhnas = substr($evshmpr,4,2)
let $evsetos = substr($evshmpr,7,2)

let $hmer = $apomhnas || $apoetos || '-' || $evsmhnas || $evsetos

end-procedure

! ****************************************************************************


! ****************************************** VENTUS LIBRARY ********
#INCLUDE "{$L_GDATA}/sqr/ventus01.inc"
! ******************************************************************

!<pcalib help>
!ΘΕΩΡΗΜΕΝΟ ΙΣΟΖΥΓΙΟ ΓΙΑ ΤΗΝ ΕΦΟΡΙΑ (FILE)
!Μετά τη συγκέντρωση της κατάστασης θα πρέπει το αρχείο
!@(LDIR)/isozdoy2.lis να αντιγραφεί σε μιά άδεια δισκέτα 
!η οποία θα υποβληθεί στην Δ.Ο.Υ.
!το όνομα του αρχείου στη δισκέτα θα πρέπει να είναι <το_ΑΦΜ>.trb
!παράδειγμα εαν το ΑΦΜ της εταιρείας είναι το : 094191278 το όνομα του
!αρχείου θα πρέπει να είναι το 094191278.trb
!</pcalib help>

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	3	16	5149	isozdoy2	rs	X	01},TITLE{Ισοζύγιο για Εφορία (Δισκέτα)}
!	PARCOUNT{2}
!	PAR1{Ενοποιημένο (N/O)	ENOP	1	ΕΠΙΛΟΓΗ	varchar(1)		X}
!	PAR2{Ημερομηνίες	HMER	2	BETWEEN	DATETIME		X}
!REPORTHELP{F}
!</pcalib>

