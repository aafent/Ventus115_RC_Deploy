!***********************************************************************
!***** REPORT      : el_isoz.sqr                                    ****
!***** DATE        : 30/03/98                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : --- ΙΣΟΖΥΓΙΑ ΛΟΓΑΡΙΑΣΜΩΝ ΓΕΝ.ΛΟΓ.  ---         ****
!***********************************************************************

#DEFINE   PAGE-Y                62
#DEFINE   PAGE-X               131 
#DEFINE   HEAD-SIZE              7
#DEFINE   FOOT-SIZE              1
#DEFINE   HEAD-LINE              4

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   MASK1     99999999999.99c
! it was :   MASK1     999,999,999.99c
#DEFINE   MASK2                999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

 create-array name=total_arr size=20 field=pre_xrevsh:number
                                     field=pre_pistvsh:number
			 	     field=xrevsh:number
				     field=pistvsh:number
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $ibaumos     'Βαθμός          : '
 input $apohmnia    'Από Ημερομηνία  : '
 input $evshmnia    'Eως Ημερομηνία  : '
 input $from_logar  'Από Λογαριασμό  : '
 input $to_logar    'Eως Λογαριασμό  : '
 input $like_logar  'Λογαριασμος να μοιαζει με: '

 execute -C1 app_floatcnv  $ibaumos,  #baumos output
 execute -C1 app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute -C1 app_datecnv   $evshmnia, $evshm output, $evshmpr output

 
 unstring $from_logar by '"' into $filler $from_logar $filler
 unstring $to_logar   by '"' into $filler $to_logar   $filler
 unstring $like_logar by '"' into $filler $like_logar $filler

 move '1 jan 1992' to $datestart
 move $L_CID to $cid

 If #baumos = 1
    move 'ΠΡΩΤΟΒΑΘΜΙΩΝ'			to $baumos_pr
 else
    if #baumos = 2
       move 'ΔΕΥΤΕΡΟΒΑΘΜΙΩΝ'		to $baumos_pr
    else
       if #baumos = 3
          move 'ΤΡΙΤΟΒΑΘΜΙΩΝ'		to $baumos_pr
       else
          if #baumos = 4
             move 'ΤΕΤΑΡΤΟΒΑΘΜΙΩΝ'	to $baumos_pr
          else
             move ''			to $baumos_pr
          end-if
       end-if
    end-if
 end-if

 move 'F' to $f_new_page
   
 do init
 do main

End-Report

! ****************************************************************************
begin-procedure init
begin-select -C1
xrhsh				() NOP
convert(datetime, xrhsh_arxh)	&xrhsh_arxh 	() NOP
convert(datetime, xrhsh_telos)	&xrhsh_telos	() NOP
	move &xrhsh	    to #xrhsh
    	move &xrhsh_arxh    to $xrhsh_arxh  
    	move &xrhsh_telos   to $xrhsh_telos
from ap_xrhsh
where cid = $cid
and $apohm between xrhsh_arxh and xrhsh_telos
end-select

move 0 to #hdlr
execute -C1 glp_collogar @FE='SQL', @hdlr=#hdlr OUTPUT,
                         @cid=$cid, @to_level=#baumos,
                         @from_date=$apohm, @to_date=$evshm,
			 @from_logar=$from_logar, @to_logar=$to_logar
 display 'HDLR : '
 display #hdlr

end-procedure

begin-procedure pca_dummyprocedure
end-procedure

! ****************************************************************************

begin-procedure main

 move 0 to #s_gtot_xre
 move 0 to #s_gtot_pis

 move 0 to #maxinx
 move 1 to #old_baumos
 
begin-select -C1
a.baumos            		(,1) on-break print=never after=levelbreak 
a.logar             		() NOP
a.perigrafh         		() NOP
a.post_flag         		() NOP
a.sum_xrevsh 	   &xrevsh	() NOP
a.sum_pistvsh  	   &pistvsh	() NOP
a.flag_kin                      () NOP

      !
      ! ta pedia sum_xrevsh, sum_pistvsh otan to flag_kin den einai X exoun to synolo
      ! apo thn arxh ths xrhshs evs thn apo hmeromhnia
      ! se ayto to report mas endiaferoun mono ta synola ths periodoy dhladh otan
      ! to flag_kin exei thn timh X 
      !

      if &a.flag_kin = 'X'
	      move &xrevsh	to #xrevsh
	      move &pistvsh	to #pistvsh
      else 
	      move 0      	to #xrevsh
	      move 0    	to #pistvsh
      end-if

      do getprev($cid, &a.logar, $xrhsh_arxh, $evshm, #pre_xrevsh, #pre_pistvsh)

      if $f_new_page = 'T'
         print 'ΑΠΟ ΜΕΤΑΦΟΡΑ'			(,17,28)
         print #s_xrevsh         		(,42,14)  edit {MASK1}
         print #s_pistvsh        		(,+1,14)  edit {MASK1}
         print #s_pre_xrevsh 			(,+1,14)  edit {MASK1}
         print #s_pre_pistvsh    		(,+1,14)  edit {MASK1}
	 next-listing
         move 'F' to $f_new_page
      end-if

      move 'T' to $print_flag

      If &a.baumos < #baumos
         do makelogform(&a.logar,$logarpr)
         print $logarpr        (, 1,13)
         print &a.perigrafh    (,+0,27)
         next-listing
         move 'F' to $print_flag
      End-If
     
      if #xrevsh     = 0 and
         #pistvsh    = 0 and
         #pre_xrevsh = 0 and
         #pre_pistvsh= 0
         move 'F' to $print_flag
      end-if

      let #diff=#pre_xrevsh - #pre_pistvsh

      if $print_flag = 'T'
         do makelogform(&a.logar,$logarpr)
         print $logarpr        (, 1,13)
         print &a.perigrafh    (,+0,27)
         print #xrevsh         (,+1,14)  edit {MASK1}
         print #pistvsh        (,+1,14)  edit {MASK1}
         print #pre_xrevsh     (,+1,14)  edit {MASK1}
         print #pre_pistvsh    (,+1,14)  edit {MASK1}
 
         if #diff > 0
            print #diff        (,+1,14)  edit {MASK1}
	    add #diff to #s_gtot_xre
            position (,+14)
         else
            let #diff=#diff*(-1)
            position (,+14)
            print #diff        (,+1,14)  edit {MASK1}
	    add #diff to #s_gtot_pis
         end-if

         next-listing need = 3

         do add-table (&a.baumos, #xrevsh, #pistvsh, #pre_xrevsh, #pre_pistvsh)

	 let #s_xrevsh      = #s_xrevsh      + #xrevsh
	 let #s_pistvsh     = #s_pistvsh     + #pistvsh
	 let #s_pre_xrevsh  = #s_pre_xrevsh  + #pre_xrevsh
	 let #s_pre_pistvsh = #s_pre_pistvsh + #pre_pistvsh

     end-if

     if #current-line > 59
        do se_metafora
     end-if

     move &a.baumos to #old_baumos

from  gl_tmplogar a
where a.hdlr = #hdlr
  and a.logar like $like_logar
order by a.logar,a.baumos
end-select

 move #old_baumos to #inx
 move 1 to #inx2
 do print-totals

 next-listing	
 next-listing	
 print 'ΓΕΝΙΚΑ ΣΥΝΟΛΑ'			(,17,28)
 print #s_xrevsh         		(,42,14)  edit {MASK1}
 print #s_pistvsh        		(,+1,14)  edit {MASK1}
 print #s_pre_xrevsh 			(,+1,14)  edit {MASK1}
 print #s_pre_pistvsh    		(,+1,14)  edit {MASK1}

 print #s_gtot_xre	    		(,+1,14)  edit {MASK1}
 print #s_gtot_pis	    		(,+1,14)  edit {MASK1}

end-procedure




! ****************************************************************************

begin-procedure add-table (#inx, #xrevsh, #pistvsh, #pre_xrevsh, #pre_pistvsh)

  While #inx > 0
     array-add #xrevsh 		to total_arr(#inx) xrevsh
     array-add #pistvsh 	to total_arr(#inx) pistvsh
     array-add #pre_xrevsh 	to total_arr(#inx) pre_xrevsh
     array-add #pre_pistvsh 	to total_arr(#inx) pre_pistvsh
     let #inx = #inx - 1
  End-While

end-procedure

! ****************************************************************************

begin-procedure levelbreak

  move #old_baumos to #inx
  move &a.baumos to #inx2
  If #inx > &a.baumos

     do print-totals

  End-If
  next-listing	

end-procedure


! ****************************************************************************

begin-procedure print-totals

     let #inx = #inx - 1

     While #inx >= #inx2

        print '* ΣΥΝΟΛΑ'			(+1,17,08)
        print #inx       			(,+1,01)	edit 9
        print '-ΒΑΘΜΙΟΥ'                        (,  ,08)

        get #xrevsh  	 from total_arr(#inx) xrevsh
        get #pistvsh 	 from total_arr(#inx) pistvsh
        get #pre_xrevsh  from total_arr(#inx) pre_xrevsh
        get #pre_pistvsh from total_arr(#inx) pre_pistvsh

        print #xrevsh         (,42,14)  edit {MASK1}
        print #pistvsh        (,+1,14)  edit {MASK1}
        print #pre_xrevsh     (,+1,14)  edit {MASK1}
        print #pre_pistvsh    (,+1,14)  edit {MASK1}

        put 0	into total_arr(#inx) xrevsh
        put 0	into total_arr(#inx) pistvsh
        put 0 	into total_arr(#inx) pre_xrevsh
        put 0	into total_arr(#inx) pre_pistvsh

	let #diff = #pre_xrevsh - #pre_pistvsh

        if #diff > 0
           print #diff        (,+1,14)  edit {MASK1}
           position (,+14)
        else
           let #diff= #diff * (-1)
           position (,+14)
           print #diff        (,+1,14)  edit {MASK1}
        end-if

        next-listing need = 3

        let #inx = #inx - 1
     End-While

End-procedure

! ****************************************************************************

begin-procedure makelogform($inlogar,:$outlogar)
  execute glp_logfrm @in_logar=$inlogar, @out_logar=$outlogar output
end-procedure

! ****************************************************************************

begin-procedure getprev ($cid, $logar, $datestart, $evshm, :#pre_xrevsh, :#pre_pistvsh)


  execute glp_getypol @cid      = $cid,
                      @logar    = $logar,
                      @hmer_apo = $datestart,
                      @hmer_evs = $evshm,
                      @xrevsh   = #pre_xrevsh  output,
                      @pistvsh  = #pre_pistvsh output,
                      @diafora  = #pre_diafora output

end-procedure

! ****************************************************************************
begin-procedure se_metafora

        print 'ΣΕ ΜΕΤΑΦΟΡΑ'			(+1,17,28)

        print #s_xrevsh         (,42,14)  edit {MASK1}
        print #s_pistvsh        (,+1,14)  edit {MASK1}
        print #s_pre_xrevsh     (,+1,14)  edit {MASK1}
        print #s_pre_pistvsh    (,+1,14)  edit {MASK1}

	new-page

end-procedure
! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 move 'T' to $f_new_page

 display 'v'
 display $f_new_page
 display '^'

 let $msg = 'ΙΣΟΖΥΓΙΟ ' || $baumos_pr || ' ΛΟΓΑΡΙΑΣΜΩΝ ΓΕΝΙΚΗΣ ΛΟΓΙΣΤΙΚΗΣ'

 print $msg 				     (2,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print ' '                                  (5,1,15)
   print ' '                                  (,+0,25)
   print '--------- Περιόδου ---------'       (,+2,28)
   print '------- Προοδευτικά --------'       (,+2,28)
   print '--------- Υπόλοιπα ---------'       (,+2,28)

   print 'Λογαριασμός'                        (6,1,15)
   print 'Περιγραφή Λογαριασμού'              (,+0,25)
   print '   Χρέωση    -    Πίστωση'          (,+2,28)
   print '   Χρέωση    -    Πίστωση'          (,+2,28)
   print '   Χρέωση    -    Πίστωση'          (,+2,28)


End-Heading

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	2	99	5142	el_isoz	rs	X	01},TITLE{Ισοζύγιο Πρωτοβάθμιων Λογαριασμών}
!	PARCOUNT{4}
!	PAR1{Βαθμός	BAUMOS	1	ΕΠΙΛΟΓΗ	int		X}
!	PAR2{Ημερομηνία	HMER	2	ΑΠΟ-ΕΩΣ	datetime		X}
!	PAR3{Λογαριασμός	a.logar	2	ΑΠΟ-ΕΩΣ	char(25)		X}
!	PAR4{Λογαριασμός	a.logar	1	LIKE	char(25)		X}
!REPORTHELP{F}
!</pcalib>
