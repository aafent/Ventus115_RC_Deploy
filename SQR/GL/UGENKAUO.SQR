! ***********************************************************************
! ***** PROLOGIC S.A.                                                ****
! ***** REPORT      : ugenkauol.sqr                                  ****
! ***** DATE        : 12/04/93,11/9/98                               ****
! ***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
! ***** AUTHOR      : ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ                              ****
! ***** NOTE        : --- ΓΕΝΙΚΟ ΚΑΘΟΛΙΚΟ                ---         ****
!***********************************************************************

#DEFINE   PAGE-Y     58
#DEFINE   PAGE-X    160
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   MASK1   99,999,999,999.99c
#DEFINE   MASK2                999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

 create-array name=   total_arr size=20 field=total_xre:number
                                        field=total_pis:number
 create-array name=pretotal_arr size=20 field=total_xre:number
                                        field=total_pis:number
 create-array name=ypototal_arr size=20 field=total_xre:number
                                        field=total_pis:number
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $iisoz     'Ισοζύγιο        : '
 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 execute -C1 app_floatcnv  $iisoz,    #isoz output
 execute -C1 app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute -C1 app_datecnv   $evshmnia, $evshm output, $evshmpr output

 move 1 to #apolevel
 move 1 to #evslevel

! move '1 jan 1992' to $datestart
! move '1 jan 1999' to $datestart

 do ventus_findxrhsh_info($cid,$apohm,#xrhsh,$datestart,$filler)
 display $datestart
 
 do main($SQLCMD,$datestart,$apohm,$evshm,#apolevel,#evslevel,
                 $apologar, $evslogar, #isoz)
End-Report



! *************************************************************************
! *** Syntax : 
begin-procedure ventus_findxrhsh_info($cid,$hmer,:#xrhsh,:$xrhsh_arxh,:$xrhsh_telos)

move 0 to #xrhsh

begin-select
xrhsh           () NOP
xrhsh_arxh      () NOP
xrhsh_telos     () NOP
  move &xrhsh to #xrhsh
  move &xrhsh_arxh  to $xrhsh_arxh
  move &xrhsh_telos to $xrhsh_telos
from ap_xrhsh
 where cid = $cid
   and $hmer between xrhsh_arxh and xrhsh_telos
end-select

end-procedure


! ****************************************************************************

begin-procedure main($SQLCMD,$datestart,$apohm,$evshm,#apolevel,#evslevel,
                             $apologar, $evslogar, #isoz)

 move 0 to #maxinx

begin-select -C1
a.baumos           (,1) on-break print=never ! after=levelbreak 
a.logar             ()     NOP
a.perigrafh         ()     NOP
a.post_flag         ()     NOP

      do getrest(#isoz, &a.logar,$datestart,$apohm,$evshm,#xrevsh,#pistvsh,#pre_xrevsh,#pre_pistvsh)

      move 'T' to $print_flag

      if #xrevsh     = 0 and
         #pistvsh    = 0 and
         #pre_xrevsh = 0 and
         #pre_pistvsh= 0
         move 'F' to $print_flag
      end-if

      if $print_flag = 'T'
         if &a.post_flag != 'X'
            next-listing
         end-if 

         do makelogform(&a.logar,$logarpr)
	 
         print $logarpr        (, 1,13)
         print &a.perigrafh    (,+0,27)
         print #xrevsh         (,+1,17)  edit {MASK1}
         print #pistvsh        (,+1,17)  edit {MASK1}
         print #pre_xrevsh     (,+1,17)  edit {MASK1}
         print #pre_pistvsh    (,+1,17)  edit {MASK1}
      end-if

      move &a.baumos to #inx

      let #diff=#pre_xrevsh-#pre_pistvsh

      if #diff > 0
         if $print_flag = 'T'
            print #diff        (,+1,17)  edit {MASK1}
            position (,+14)
         end-if
         get #tmp from ypototal_arr(#inx) total_xre
         let #tmp=#tmp+#diff
         put #tmp into ypototal_arr(#inx) total_xre
      else
         let #diff=#diff*(-1)
         if $print_flag = 'T'
            position (,+14)
            print #diff        (,+1,17)  edit {MASK1}
         end-if
         get #tmp from ypototal_arr(#inx) total_pis
         let #tmp=#tmp+#diff
         put #tmp into ypototal_arr(#inx) total_pis
      end-if

      get #tmp from total_arr(#inx) total_xre
      let #tmp=#tmp+#xrevsh
      put #tmp into total_arr(#inx) total_xre

      get #tmp from total_arr(#inx) total_pis
      let #tmp=#tmp+#pistvsh
      put #tmp into total_arr(#inx) total_pis

      get #tmp from pretotal_arr(#inx) total_xre
      let #tmp=#tmp+#pre_xrevsh
      put #tmp into pretotal_arr(#inx) total_xre

      get #tmp from pretotal_arr(#inx) total_pis
      let #tmp=#tmp+#pre_pistvsh
      put #tmp into pretotal_arr(#inx) total_pis

      if #maxinx < #inx
         move #inx to #maxinx
      end-if

      if $print_flag = 'T'
         ! next-listing
      end-if

from  gl_logar a, ap_user b
where a.cid=b.company
and   b.suname=suser_name()
and   a.xrhsh = b.xrhsh
and   a.baumos between #apolevel and #evslevel
and   a.logar not like '9%'
[$SQLCMD]
order by a.logar, a.baumos
end-select

 next-listing need=4

 move 0 to #inx
 while #inx <= #maxinx
  let #inx=#inx+1

  get #xrevsh  from total_arr(#inx) total_xre
  get #pistvsh from total_arr(#inx) total_pis
  get #pre_xrevsh  from pretotal_arr(#inx) total_xre
  get #pre_pistvsh from pretotal_arr(#inx) total_pis
  get #ypo_xrevsh  from ypototal_arr(#inx) total_xre
  get #ypo_pistvsh from ypototal_arr(#inx) total_pis

  if #inx>#maxinx
     break
  end-if

   print 'ΣΥΝΟΛΑ ΒΑΘΜΟΥ : '  (, 14,24)
   print #inx            (,+0,25)  edit {MASK2}
   print #xrevsh         (,+1,17)  edit {MASK1}
   print #pistvsh        (,+1,17)  edit {MASK1}
   print #pre_xrevsh     (,+1,17)  edit {MASK1}
   print #pre_pistvsh    (,+1,17)  edit {MASK1}
   print #ypo_xrevsh     (,+1,17)  edit {MASK1}
   print #ypo_pistvsh    (,+1,17)  edit {MASK1}

   next-listing


 end-while

end-procedure

! ****************************************************************************

begin-procedure levelbreak

!  next-listing 

end-procedure

! ****************************************************************************

begin-procedure makelogform($inlogar,:$outlogar)
  execute -C2 glp_logfrm @in_logar=$inlogar, @out_logar=$outlogar output
end-procedure

! ****************************************************************************

begin-procedure getrest (#isoz, $logar,$datestart,$apohm,$evshm,:#xrevsh,:#pistvsh,:#pre_xrevsh,:#pre_pistvsh)

if #isoz = 1
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
		      @isoz    = 3

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
		      @isoz    = 3
end-if

if #isoz = 2
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
		      @isoz    = 2

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
		      @isoz    = 2
end-if

if #isoz = 3
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output
end-if

if #isoz = 4
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
		      @isoz    = 1

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
		      @isoz    = 1
end-if



! ---------------------------------
! DATA :
! ---------------------------------
!   1 ΓΕΝΙΚΟ ΙΣΟΖΥΓΙΟ                
!   2 ΙΣΟΖΥΓΙΟ ΑΠΟΓΡΑΦΗΣ             
!   3 ΙΣΟΖΥΓΙΟ ΕΓΓΡ. ΙΣΟΛΟΓΙΣΜΟΥ     
! ---------------------------------


end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
End-Footing

Begin-Heading {HEAD-SIZE}
print $select12cpi (,1) code
 #INCLUDE "{$L_GDATA}/sqr/HEAD03.inc"

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print ' '                                  (5,1,13)
   print ' '                                  (,+0,27)
   print '------------ Περιόδου ------------' (,+2,34)
   print '---------- Προοδευτικά -----------' (,+2,34)
   print '------------ Υπόλοιπα ------------' (,+2,34)

   print 'Λογαριασμός'                        (6,1,13)
   print 'Περιγραφή Λογαριασμού'              (,+0,27)
   print '      Χρέωση    -       Πίστωση'    (,+2,34)
   print '      Χρέωση    -       Πίστωση'    (,+2,34)
   print '      Χρέωση    -       Πίστωση'    (,+2,34)

End-Heading

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	3	1	22	ugenkauo	rs	X	01},TITLE{Θεωρημένo Γενικό Καθολικό}
!	PARCOUNT{2}
!	PAR1{Ισοζύγιο	DUMP1	1	=	int		X}
!	PAR2{Ημ/κή Περίοδος	DUMP2	2	ΑΠΟ-ΕΩΣ	datetime		X}
!REPORTHELP{T},RNHELP{Ισοζύγιο (1) Εναρξης, (2) Εγγραφών Ισολογισμού, (3) Γενικό},
!</pcalib>
