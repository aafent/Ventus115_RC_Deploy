!***********************************************************************
!***** REPORT      : isgaka.sqr                                     ****
!***** DATE        : 7/3/01                                         ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : --- ΙΣΟΖΥΓΙΑ ΛΟΓΑΡΙΑΣΜΩΝ ΓΕΝ.ΛΟΓ.  ---         ****
!***********************************************************************

#DEFINE   PAGE-Y                62
#DEFINE   PAGE-X               202    ! htan 180  me logar_perigrafh 24
#DEFINE   HEAD-SIZE              8
#DEFINE   FOOT-SIZE              1
#DEFINE   HEAD-LINE              5

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   MASK1     999,999,999.99c
#DEFINE   MASK11    99999999999.99c
#DEFINE   MASK2                999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

 create-array name=   total_arr size=20 field=total_xre:number
                                        field=total_pis:number
 create-array name=pretotal_arr size=20 field=total_xre:number
                                        field=total_pis:number

 create-array name=ypototal_arr size=20 field=total_xre:number
                                        field=total_pis:number
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $flag_ueor 'Θεωρημένο        : '
 input $iisoz     'Ισοζύγιο         : '
 input $apohmnia  'Από Ημερομηνία   : '
 input $evshmnia  'Eως Ημερομηνία   : '
 input $apolevel  'Απο Βαθμό        : '
 input $evslevel  'Εώς Βαθμό        : '
 input $logarlike 'Εύρος Λογ./μού   : '
 input $multy_selida 'Αρ.1ς Σελίδας : '

 execute app_floatcnv  $iisoz,    #isoz output
 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output
 execute app_floatcnv  $apolevel, #apolevel output
 execute app_floatcnv  $evslevel, #evslevel output
 execute app_floatcnv  $multy_selida, #multy_selida output
 
 unstring $logarlike by '"' into $filler $logarlike $filler
 unstring $flag_ueor by '"' into $filler $flag_ueor $filler

 move 'N' to $flag_multy
 if $flag_ueor = 'e' or $flag_ueor='E' or $flag_ueor='ε' or $flag_ueor='Ε' 
    move 'Y' to $flag_multy
    move 'N' to $flag_ueor
 end-if

 if $flag_ueor = 'n' or $flag_ueor='N' or $flag_ueor='ν' or $flag_ueor='Ν'
    move 'Y' to $flag_ueor
 else
    move 'N' to $flag_ueor
 end-if

 move '1 jan 1999' to $datestart
 move $L_CID to $cid

 if $logarlike = '' 
    move '%' to $logarlike
 end-if
 display 'LOGAR LIKE : ' noline
 display $logarlike


 do init
 move &xrhsh_arxh to $datestart
 do main

End-Report

! ****************************************************************************
begin-procedure init
begin-select -C1
xrhsh				() NOP
convert(datetime, xrhsh_arxh)	&xrhsh_arxh 	() NOP
convert(datetime, xrhsh_telos)	&xrhsh_telos	() NOP
	move &xrhsh	    to #xrhsh
    	move &xrhsh_arxh    to $xrhsh_arxh  
    	move &xrhsh_telos   to $xrhsh_telos
from ap_xrhsh
where cid = $cid
and $apohm between xrhsh_arxh and xrhsh_telos
end-select
end-procedure
! ****************************************************************************

begin-procedure main

 move 0 to #maxinx

begin-select -C1
a.baumos             (,1) on-break print=never ! after=levelbreak 
a.logar             ()     NOP
a.frm_logar         ()     NOP
a.perigrafh         ()     NOP
a.post_flag         ()     NOP

      display &a.logar
      do getrest(#isoz, &a.logar,$datestart,$apohm,$evshm,#xrevsh,#pistvsh,#pre_xrevsh,#pre_pistvsh)
      let #pre2_xrevsh  = #pre_xrevsh  - #xrevsh
      let #pre2_pistvsh = #pre_pistvsh - #pistvsh

      move 'T' to $print_flag

      if #xrevsh     = 0 and
         #pistvsh    = 0 and
         #pre_xrevsh = 0 and
         #pre_pistvsh= 0
         move 'F' to $print_flag
      end-if

      if $print_flag = 'T'
         if &a.post_flag != 'X'
            next-listing
         end-if 

         if &a.baumos < 6 
            move &a.frm_logar to $logarpr
         else
            move &a.logar to $logarpr
         end-if

         print $logarpr        (, 1,15)
         print &a.perigrafh    (,+1,50)

         position (,+1)
         do print14 (#pre2_xrevsh)
         !print #pre2_xrevsh    (,+1,14)  edit {MASK1}
         
         position (,+1)
         do print14 (#pre2_pistvsh)
         !print #pre2_pistvsh   (,+1,14)  edit {MASK1}

	       let #dif = #pre2_xrevsh - #pre2_pistvsh
	       position (,+1)
         do print14 (#dif)
         
         !move #dif to $tmp {MASK1}
         !if instr($tmp,'*',0) > 0
         !   print #dif            (,+1,14)  edit {MASK11}
         !else
	       !   print #dif            (,+1,14)  edit {MASK1}
         !end-if

         position (,+1)
         !print #xrevsh         (,+1,14)  edit {MASK1}
         do print14 (#xrevsh)
         
         position (,+1)
         !print #pistvsh        (,+1,14)  edit {MASK1}
         do print14 (#pistvsh)
         
         position (,+1)
         !print #pre_xrevsh     (,+1,14)  edit {MASK1}
         do print14 (#pre_xrevsh)
         
         position (,+1)
         !print #pre_pistvsh    (,+1,14)  edit {MASK1}
         do print14 (#pre_pistvsh)
         
      end-if

      move &a.baumos to #inx

      let #diff=#pre_xrevsh-#pre_pistvsh

      if #diff > 0
         if $print_flag = 'T'
            position (,+1)
            do print14 (#diff)
            !print #diff        (,+1,14)  edit {MASK1}
            position (,+14)
         end-if
         get #tmp from ypototal_arr(#inx) total_xre
         let #tmp=#tmp+#diff
         put #tmp into ypototal_arr(#inx) total_xre
      else
         let #diff=#diff * (-1)
         if $print_flag = 'T'
            position (,+15)
            do print14 (#diff)
            !print #diff        (,+1,14)  edit {MASK1}
         end-if
         get #tmp from ypototal_arr(#inx) total_pis
         let #tmp=#tmp+#diff
         put #tmp into ypototal_arr(#inx) total_pis
      end-if

      get #tmp from total_arr(#inx) total_xre
      let #tmp=#tmp+#xrevsh
      put #tmp into total_arr(#inx) total_xre

      get #tmp from total_arr(#inx) total_pis
      let #tmp=#tmp+#pistvsh
      put #tmp into total_arr(#inx) total_pis

      get #tmp from pretotal_arr(#inx) total_xre
      let #tmp=#tmp+#pre_xrevsh
      put #tmp into pretotal_arr(#inx) total_xre

      get #tmp from pretotal_arr(#inx) total_pis
      let #tmp=#tmp+#pre_pistvsh
      put #tmp into pretotal_arr(#inx) total_pis

      if #maxinx < #inx
         move #inx to #maxinx
      end-if

      if $print_flag = 'T'
         next-listing
      end-if

from  gl_logar a
where a.cid    = $cid
and   a.xrhsh  = #xrhsh
and   a.baumos between #apolevel and #evslevel
and   a.logar like $logarlike 
[$SQLCMD]
order by a.logar, a.baumos
end-select

 next-listing need=4

 move 0 to #inx
 while #inx <= #maxinx
  let #inx=#inx+1

  get #xrevsh  from total_arr(#inx) total_xre
  get #pistvsh from total_arr(#inx) total_pis
  get #pre_xrevsh  from pretotal_arr(#inx) total_xre
  get #pre_pistvsh from pretotal_arr(#inx) total_pis
  get #ypo_xrevsh  from ypototal_arr(#inx) total_xre
  get #ypo_pistvsh from ypototal_arr(#inx) total_pis

  if #inx>#maxinx
     break
  end-if

   let #pre2_xrevsh  = #pre_xrevsh  - #xrevsh
   let #pre2_pistvsh = #pre_pistvsh - #pistvsh

   print 'ΣΥΝΟΛΑ ΒΑΘΜΟΥ : '  (, 1,15)
   print #inx                (,+0, 3)  edit {MASK2}
   print ' '                 (,+0,48)

   print #pre2_xrevsh 	 (,+1,14)  edit {MASK11}
   print #pre2_pistvsh   (,+1,14)  edit {MASK11}

   let #dif = #pre2_xrevsh - #pre2_pistvsh
   print #dif             (,+1,14) edit {MASK11}

   print #xrevsh         (,+1,14)  edit {MASK11}
   print #pistvsh        (,+1,14)  edit {MASK11}
   print #pre_xrevsh     (,+1,14)  edit {MASK11}
   print #pre_pistvsh    (,+1,14)  edit {MASK11}
   print #ypo_xrevsh     (,+1,14)  edit {MASK11}
   print #ypo_pistvsh    (,+1,14)  edit {MASK11}

   next-listing


 end-while

end-procedure

! ****************************************************************************

begin-procedure print14 (#noumero)
         move #noumero to $tmp {MASK1}
         if instr($tmp,'*',0) > 0
            print #noumero            (,,14)  edit {MASK11}
         else
	          print #noumero            (,,14)  edit {MASK1}
         end-if
end-procedure

! ****************************************************************************

begin-procedure levelbreak

!  next-listing 

end-procedure


! ****************************************************************************

begin-procedure getrest (#isoz, $logar,$datestart,$apohm,$evshm,:#xrevsh,:#pistvsh,:#pre_xrevsh,:#pre_pistvsh)

! Σε αυτή τη μορφή ισοζυγίου υπάρχουν οι στήλες προοδευτικά που έχουν από έναρξη χρήσης μέχρι απο ημερομηνια-1
! και περιόδου που περιέχει την από έως την έωσ ημερομηνία.
! Τα ακόλουθα procedures μαζεύουν για τα προοδευτικά από την έναρξη χρήσης μέχρι την έως και στη συνέχεια
! το function που έχει καλέσει την getrest αφαιρεί από τα προοδευτικά την περίοδο.


if #isoz = 8 ! (8) ΙΣΟΖΥΓΙΟ ΠΡΟ ΕΓΓΡΑΦΩΝ ΚΛΕΙΣΙΜΑΤΟΣ
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
		      @isoz    = 1

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
		      @isoz    = 1

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#tmp_xrevsh  output,
                      @pistvsh =#tmp_pistvsh output,
                      @diafora =#tmp_diafora output,
		      @isoz    = 2

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#tmp_pre_xrevsh  output,
                      @pistvsh =#tmp_pre_pistvsh output,
                      @diafora =#tmp_pre_diafora output,
		      @isoz    = 2

  let #xrevsh = #xrevsh + #tmp_xrevsh
  let #pistvsh= #pistvsh+ #tmp_pistvsh
  let #diafora= #diafora+ #tmp_diafora

  let #pre_xrevsh = #pre_xrevsh + #tmp_pre_xrevsh
  let #pre_pistvsh= #pre_pistvsh+ #tmp_pre_pistvsh
  let #pre_diafora= #pre_diafora+ #tmp_pre_diafora


end-if







if #isoz = 1 ! (1) ΙΣΟΖΥΓΙΟ ΓΕΝΙΚΟΥ ΑΝΑΛΥΤΙΚΩΝ ΚΑΘΟΛΙΚΩΝ

  move 0 to #xrevsh
  move 0 to #pistvsh
  move 0 to #tmp_xrevsh
  move 0 to #tmp_pistvsh
  move 0 to #pre_xrevsh
  move 0 to #pre_pistvsh
  move 0 to #cur_xrevsh
  move 0 to #cur_pistvsh


  execute -C2 glp_getypol  @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
                      @isoz    = 1

  

                      
 execute -C2  glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output
                      @no_isoz = 9, ! ΔΛΠ
                      @no_isoz2= 8, ! KΛΠ
                      @no_isoz3= 11, ! ΡΡΦ


  execute -C2  glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#tmp_xrevsh  output,
                      @pistvsh =#tmp_pistvsh output,
                      @diafora =#tmp_diafora output,
		      @isoz    = 4 ! λήξη

  let #pre_xrevsh = #pre_xrevsh - #tmp_xrevsh
  let #pre_pistvsh= #pre_pistvsh- #tmp_pistvsh
  let #pre_diafora= #pre_diafora- #tmp_diafora



 execute -C2  glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#tmp_xrevsh  output,
                      @pistvsh =#tmp_pistvsh output,
                      @diafora =#tmp_diafora output,
                      @isoz    = 3 ! τακτοποίηση

  ! (v) η τακτοποίηση θα αφαιρεθεί από τα προοδευτικά από τον κώδικα που καλει αυτο το sqr-procedure
  let #xrevsh = #xrevsh  + #tmp_xrevsh 
  let #pistvsh= #pistvsh + #tmp_pistvsh  
  let #diafora= #diafora + #tmp_diafora  


 ! (v) RRF
 execute -C2  glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#tmp_xrevsh  output,
                      @pistvsh =#tmp_pistvsh output,
                      @diafora =#tmp_diafora output,
                      @isoz    = 11 ! RRF

  ! (v) RRF θα αφαιρεθεί από τα προοδευτικά από τον κώδικα που καλει αυτο το sqr-procedure
  !     οπότε εδώ θα το προσθέσουμε ώστε η επόμενη αφαίρεση να φέρει το σωστό προοδευτικό
  !     θα προστεθεί στη κίνηση περιόδου που έχει κατά βάση το ισοζύγιο=1 μέχρι τώρα
  !     με adjustemets που του έγιναν παραπάνω
  !
  let #pre_xrevsh = #pre_xrevsh  + #tmp_xrevsh
  let #pre_pistvsh= #pre_pistvsh + #tmp_pistvsh
  let #pre_diafora= #pre_diafora + #tmp_diafora


  let #xrevsh = #xrevsh  + #tmp_xrevsh 
  let #pistvsh= #pistvsh + #tmp_pistvsh  
  let #diafora= #diafora + #tmp_diafora  




end-if





if #isoz = 2 ! (2) ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΕΝΑΡΞΗΣ
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
                      @isoz    = 2

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
                      @isoz    = 2

end-if


if #isoz = 3 ! (3) ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΤΑΚΤΟΠΟΙΗΣΗΣ
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
                      @isoz    = 3

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
                      @no_isoz  = 9,! ΔΛΠ
                      @no_isoz2 = 8 ! KΛΠ


  execute -C2  glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#tmp_xrevsh  output,
                      @pistvsh =#tmp_pistvsh output,
                      @diafora =#tmp_diafora output,
                      @isoz    = 4 ! λήξη

  let #pre_xrevsh = #pre_xrevsh - #tmp_xrevsh
  let #pre_pistvsh= #pre_pistvsh- #tmp_pistvsh
  let #pre_diafora= #pre_diafora- #tmp_diafora

end-if




if #isoz = 4 ! (4) ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΛΗΞΗΣ
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
                      @isoz	= 4

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
                      @no_isoz  = 9, ! ΔΛΠ
                      @no_isoz2 = 8  ! KΛΠ

  ! (^) σημείωση δεν αφαιρώ τη λήξη από τα προοδευτικά γιατί οι εγγραφές λήξης είναι πάντα στην 31/12 (performance)

end-if


if #isoz = 9 ! (9) ΓΕΝΙΚΟ ΙΣΟΖΥΓΙΟ ΛΟΓΙΣΤΙΚΗΣ
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output,
                      @no_isoz  = 9,! ΔΛΠ
                      @no_isoz2 = 8 ! KΛΠ

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output,
                      @no_isoz  = 9,! ΔΛΠ
                      @no_isoz2 = 8 ! KΛΠ
end-if

if #isoz = 10 ! (10) ΓΕΝΙΚΟ ΙΣΟΖΥΓΙΟ ΛΟΓΙΣΤΙΚΗΣ ΜΕ ΔΛΠ
  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output

  execute -C2 glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output
end-if


end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 !#INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}

 if $flag_ueor = 'N'
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
 else
 #INCLUDE "{$L_GDATA}/sqr/HEAD03.inc"
 end-if


 
 if #isoz = 1
    move '(1) ΙΣΟΖΥΓΙΟ ΓΕΝΙΚΟΥ-ΑΝΑΛΥΤΙΚΩΝ ΚΑΘΟΛΙΚΩΝ' to $titlos
 else
 if #isoz = 2
    move '(2) ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΕΝΑΡΞΗΣ' to $titlos
 else
 if #isoz = 3
    move '(3) ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΤΑΚΤΟΠΟΙΗΣΗΣ' to $titlos
 else
 if #isoz = 4
    move '(4) ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΛΗΞΗΣ' to $titlos
 else
 if #isoz = 8
    move '(8) ΓΕΝΙΚΟ ΙΣΟΖΥΓΙΟ ΠΡΟ ΕΓΓΡΑΦΩΝ ΚΛΕΙΣΙΜΑΤΟΣ' to $titlos
 else 
 if #isoz = 9
    move '(9) ΓΕΝΙΚΟ ΙΣΟΖΥΓΙΟ ΛΟΓΙΣΤΙΚΗΣ' to $titlos
 else
 if #isoz = 10
    move '(10) ΙΣΟΖΥΓΙΟ ΔΙΕΘΝΩΝ ΛΟΓΙΣΤΙΚΩΝ ΠΡΟΤΥΠΩΝ' to $titlos
 else
 
    move 'ΑΚΥΡΟ ΑΚΥΡΟ ΑΚΥΡΟ ΑΚΥΡΟ' to $titlos
 end-if
 end-if
 end-if
 end-if
 end-if
 end-if
 end-if

 if $flag_ueor = 'N' or $flag_multy = 'Y'
    print $titlos (3,+1) center
 end-if
 if $flag_multy = 'Y'
    move {PAGE-X} to #col
    let #col = #col - 20
    print 'ΣΕΛΙΔΑ: ' (3, #col)
    print #multy_selida () edit 99999999
    let #multy_selida = #multy_selida + 1
 end-if

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr
 print $msg (4,+1) center

   print ' '                                  (6,1,15)
   print ' '                                  (,+1,50)
   print '---Προγενέστερα Αθροίσματα--'       (,+2,28)
   print ' '                                  (,+2,14)
    print '------Κίνηση Περιόδου-------'      (,+2,28)
   print '----Συνολικά  Αθροίσματα----'       (,+2,28)
   print '-------Τελικά Υπόλοιπα------'       (,+2,28)

   print 'Λογαριασμός'                        (7,1,13)
   print ' '        			      (,+0,3)
   print 'Περιγραφή Λογαριασμού'              (,+0,50)
   
   print '       Χρέωση   -    Πίστωση'       (,+2,28)
   print '      Υπόλοιπα'       	      (,+1,14)
   print '       Χρέωση   -    Πίστωση'       (,+2,28)
   print '       Χρέωση   -    Πίστωση'       (,+2,28)
   print '       Χρέωση   -    Πίστωση'       (,+2,28)

End-Heading

!<pcalib help>
!
! 17cpi
!
! Στην παράμετρο [Ισοζύγιο] θα πρέπει να δοθεί ένας από τους παρακάτω κωδικούς :
! Ισοζύγιο (1) Γενικό, (2) Απογραφής έναρξης, (3) Ισολογιμου τακτοποιήσεις, (4) λήξεως
!
! 1. Για το : ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΙΣΟΛΟΓΙΣΜΟΥ ΕΝΑΡΞΗΣ Γ.Λ.
!    περιέχει τις εγγραφές στη λογιστική με κωδικό ισοζυγίου : 3 (εγγραφών ισολογισμού)
!    τόσο στη περιόδο ή / και στα προδευτικά, ανάλογα με την περίοδο. 
!    Περιέχει μόνο εγγραφές έναρξης (Κωδικοί κίνησης Λογιστικής : 4)
!
! 1. Για το : ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΙΣΟΛΟΓΙΣΜΟΥ ΕΝΑΡΞΗΣ Γ.Λ.
!    περιέχει τις εγγραφές στη λογιστική με κωδικό ισοζυγίου : 3 (εγγραφών ισολογισμού)
!    τόσο στη περιόδο ή / και στα προδευτικά, ανάλογα με την περίοδο. 
!    Περιέχει μόνο εγγραφές έναρξης (Κωδικοί κίνησης Λογιστικής : 4)
!
! 2. Για το : ΙΣΟΖΥΓΙΟ ΕΓΓΡΑΦΩΝ ΙΣΟΛΟΓΙΣΜΟΥ ΕΝΑΡΞΗΣ/ΛΗΞΗΣ Γ.Λ.
!    περιέχει τις εγγραφές στη λογιστική με κωδικό ισοζυγίου : 3 (εγγραφών ισολογισμού)
!    τόσο στα σύνολα περιόδου
!    και
!    όλες τις εγγραφές στα προδευτικά.
!
! 3. Για το : ΙΣΟΖΥΓΙΟ ΛΟΓΑΡΙΑΣΜΩΝ ΓΕΝΙΚΗΣ ΛΟΓΙΣΤΙΚΗΣ (ΓΕΝΙΚΟ)
!    περιέχει όλες τις εγγραφές στη λογιστική 
!    τόσο στα σύνολα περιόδου, όσο και στα προδευτικά.
!
!</pcalib help>

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	3	1	6036	isgaka	rs	X	01},TITLE{Ισοζύγιο γενικού, αναλυτικού καθολικού}
!	PARCOUNT{4}
!	PAR1{Ισοζύγιο	DUMP1	1	=	int		X}
!	PAR2{Ημερομηνία	DUMP2	2	ΑΠΟ-ΕΩΣ	datetime		X}
!	PAR3{Βαθμός Λογαριασμών	DUMP3	2	ΑΠΟ-ΕΩΣ	int		X}
!	PAR4{Λογαριασμός	a.logar	2	BETWEEN	varchar(25)	X	}
!REPORTHELP{T},RNHELP{Επιλογές ισοζυγίου 1,2,3,4 (πάτα βοήθεια για περσ. πληρ.)},RNVER{1.0},RNCREDT{2001-03-08},RNMODDT{2001-03-08},
!</pcalib>


