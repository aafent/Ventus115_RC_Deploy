!***********************************************************************
!***** REPORT      : kart.sqr                                       ****
!***** DATE        : 28/04/93                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : --- ΕΛΕΓΧΟΣ ΜΗΧΑΝΟΓΡΑΦΙΚΟΥ ΑΡΙΘΜΟΥ ---         ****
!***********************************************************************

#DEFINE   PAGE-Y    60
#DEFINE   PAGE-X    75
#DEFINE   HEAD-SIZE  6
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1     9999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 move $L_CID to $cid
 input $apo_hm  'Από Ημερομηνία  : '
 input $evs_hm  'Eως Ημερομηνία  : '

 execute app_datecnv   $apo_hm, $apohm output, $apohmpr output
 execute app_datecnv   $evs_hm, $evshm output, $evshmpr output

 do main
End-Report

begin-procedure main

 next-listing
 print 'Λογαριασμοί της Γενικής Λογιστικής :' () center
 next-listing SKIPLINES=2
 move 'P' to $jobflag
 do dorep
 next-listing

 do printcnt

 next-listing SKIPLINES=2
 print 'Λογαριασμοί της Αναλυτικής Λογιστικής :' () center
 next-listing SKIPLINES=2
 move 'A' to $jobflag
 do dorep
 next-listing

  do printcnt

end-procedure


begin-procedure dorep
begin-select
getdate()  &difhm  () NOP
end-select

 move 0 to #cnt
 display $jobflag
 execute do=printlns
         glp_hmerdiaf @cid=$cid ,@hm_apo=$apohm, @hm_evs=$evshm, @hm_diaf=&difhm
                      @jobflag=$jobflag,   @ans=$ans output
                      into &hmer_e      datetime,
                           &plhuos      int,
                           &hmeres      int
 next-listing
end-procedure

! ***************************************************************** [printlns]

begin-procedure printcnt
 if #cnt=0 
    move 'Δέν υπάρχουν ανοικτές εγγραφές στα δοθέντα ημερομηνιακά όρια' to $msg
 else
    move #cnt to $cnt 99999999
    let $msg='Βρέθηκαν ανοικτές εγγραφές σε '||$cnt||' ημερομηνίες'
 end-if
 print $msg () center
 next-listing
end-procedure

begin-procedure printlns
 print  &hmer_e    (,20,08) edit {DATEMASK}
 print  &plhuos    (,+5,04) edit {MASK1}
 print  &hmeres    (,+5,04) edit {MASK1}
 add 1 to #cnt
 next-listing
end-procedure

! ****************************************************************************
Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΚΑΤΑΣΤΑΣΗ ΕΛΕΓΧΟΥ ΜΗΧΑΝΟΓΡΑΦΙΚΩΝ ΑΡΙΘΜΩΝ' (3) center

 print  'Ημ/νία  ' (5,20,08) 
 print  'Πλήθ'     ( ,+5,04)
 print  'Ημερ'     ( ,+5,04)

End-Heading
!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	1	4	20	hmdiaf	rs	X	01},TITLE{Ελέγχου Μηχανογραφικών Αριθμών}
!	PARCOUNT{1}
!	PAR1{Ημερομηνίες	DUMP	2	ΑΠΟ-ΕΩΣ	datetime		X}
!REPORTHELP{F}
!</pcalib>
