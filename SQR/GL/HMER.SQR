! ***********************************************************************
! ***** REPORT      : hmer.sqr                                       ****
! ***** DATE WRITTEN: 28/04/93                 LAST UPDATE: 29/09/94 ****
! ***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
! ***** NOTE        : --- ΗΜΕΡΟΛΟΓΙΟ ΟΜΑΔΑΣ    ΓΕΝ.ΛΟΓ.  ---         ****
! ***********************************************************************

#DEFINE   PAGE-Y                 58
#DEFINE   PAGE-X                131 
#DEFINE   HEAD-SIZE               6
#DEFINE   FOOT-SIZE               1
#DEFINE   HEAD-LINE               4

#DEFINE   DATEMASK         DD/MM/YY
#DEFINE   MASK1              999999
#DEFINE   MASK2     9999,999,999.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

 create-array name=   hmer_arr size=30 field=total_xre:number
                                       field=total_pis:number
                                       field=perigrafh:char
                                       field=hmerologio:char
                                       field=pre_xre:number
                                       field=pre_pis:number
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 input $omhm      'Ομαδα Ημερολογ. : '
 unstring $omhm by '"' into $filler1 $omada $filler1

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 move 0 to #maxarr
 move $L_CID to $cid

 do init
 do loadall
 do main

End-Report

! ****************************************************************************
begin-procedure init
begin-select 
xrhsh		() NOP
convert(datetime, xrhsh_arxh)	&xrhsh_arxh () NOP
	move &xrhsh      to #xrhsh
        move &xrhsh_arxh to $datestart
from ap_xrhsh
where cid = $cid
  and $apohm between xrhsh_arxh and xrhsh_telos
end-select
end-procedure
! ****************************************************************************

begin-procedure main

move 'F' to $pageflag

begin-select
convert(datetime,a.hmer_e) &a.hmer_e (,10,08) on-break print=change/top-page 
                                     before=hmerbreak save=$hmer level=1
                                     after=totalbreak 
a.mhxar                     (,01,06) edit {MASK1}
b.perigrafh                 (,+1,25) 
a.logar                     () NOP
   do  makelogform($cid, #xrhsh, &a.logar, $logarpr)
   print $logarpr           (,+1,15)
a.parid                     (,+1,10)
a.aitia                     (,+1,30)
a.xrevsh		    (,+1,15)	edit {MASK2}
a.pistvsh		    (,+1,15)	edit {MASK2}
a.hmerologio    &hmer       () NOP

   position (,+5)

   do addtotals(&hmer,&xrevsh,&pistvsh)

   next-listing

from  gl_kin a, gl_logar b, gl_hmerandomada ss
where a.cid=$cid
and   a.logar=b.logar
and   a.cid=b.cid
and   a.hmer_e between $apohm and $evshm
and   a.hmerologio = ss.hmerologio
and   ss.omada = $omada 
! (v) 28.9.94 Nick - Ισχυει μονο για το θεωρημενο
! and   a.mhxar is not null
[$SQLCMD]
order by a.hmer_e, a.mhxar
end-select

next-listing 

end-procedure

! ****************************************************************************

begin-procedure totalbreak

  next-listing

  move 0 to #pro_xre
  move 0 to #pro_pis
  move 0 to #gen_xre
  move 0 to #gen_pis

  move 1 to #inx

  while #maxarr>=#inx

     get $hmerologio      from hmer_arr(#inx) hmerologio

     do getdioru($hmerologio,$hmer,$hmer,#dio_xre,#dio_pis)
 
     get $perigrafh       from hmer_arr(#inx) perigrafh
     get #pre_xrevsh      from hmer_arr(#inx) pre_xre
     get #pre_pistvsh     from hmer_arr(#inx) pre_pis
     get #total_xrevsh    from hmer_arr(#inx) total_xre
     get #total_pistvsh   from hmer_arr(#inx) total_pis

     print 'ΠΡΟΗΓΟΥΜΕΝΑ ΣΥΝΟΛΑ '  (,30)
     print $perigrafh             (,+1,20)
     print ' : '                  (,+1,18)
     print #pre_xrevsh            (,+1,15) edit {MASK2}
     print #pre_pistvsh           (,+1,15) edit {MASK2}

     next-listing

     let #total_xrevsh =#total_xrevsh+ #dio_xre
     let #total_pistvsh=#total_pistvsh+#dio_pis
     print 'ΣΥΝΟΛΑ ΗΜΕΡΑΣ      '  (,30)
     print $perigrafh             (,+1,20)
     print ' : '                  (,+1,18)
     print #total_xrevsh          (,+1,15) edit {MASK2}
     print #total_pistvsh         (,+1,15) edit {MASK2}

     next-listing

     let #pro_xre=#pre_xrevsh +#total_xrevsh
     let #pro_pis=#pre_pistvsh+#total_pistvsh

     print 'ΠΡΟΟΔΕΥΤΙΚΑ ΣΥΝΟΛΑ '  (,30)
     print $perigrafh             (,+1,20)
     print ' : '                  (,+1,18)
     print #pro_xre               (,+1,15) edit {MASK2}
     print #pro_pis               (,+1,15) edit {MASK2}

     let #gen_xre=#gen_xre+#pro_xre
     let #gen_pis=#gen_pis+#pro_pis

     next-listing


     put #pro_xre into hmer_arr(#inx) pre_xre
     put #pro_pis into hmer_arr(#inx) pre_pis
     move 0 to #tmp
     put #tmp   into hmer_arr(#inx) total_xre
     put #tmp   into hmer_arr(#inx) total_pis

     let #inx=#inx+1
  end-while
  next-listing
  
     print 'ΓΕΝΙΚΑ ΠΡΟΟΔΕΥΤΙΚΑ ΣΥΝΟΛΑ' (,30)
     print ' '                    (,+1,14)
     print ' : '                  (,+1,18)
     print #gen_xre               (,+1,15) edit {MASK2}
     print #gen_pis               (,+1,15) edit {MASK2}

   next-listing

end-procedure

! ****************************************************************************

begin-procedure getdioru($hmerologio,$hmer1,$hmer2,:#xrevsh,:#pistvsh)

move 0 to #xrevsh
move 0 to #pistvsh
begin-select
sum(xrevsh)    &dio_xre    () NOP
sum(pistvsh)   &dio_pis    () NOP

  move &dio_xre  to #xrevsh
  move &dio_pis  to #pistvsh

from  gl_dioru a, ap_user b
where a.cid=b.company
and   b.suname=suser_name()
and   a.hmerologio=$hmerologio
and   a.hmeromhnia between $hmer1 and $hmer2
end-select

end-procedure

! ****************************************************************************

begin-procedure hmerbreak
  
  if $pageflag='T' 
     new-page
  else 
     next-listing
  end-if
  next-listing
  print 'Ημερομηνία : '  (1) center
  print &a.hmer_e        (,+2) edit {DATEMASK}
  next-listing
  next-listing
  move 'T' to $pageflag

end-procedure

! ****************************************************************************

begin-procedure makelogform($cid, #xrhsh, $inlogar, :$outlogar)
  
  execute glp_logfrm @cid       = $cid,
                     @xrhsh     = #xrhsh, 
                     @in_logar  = $inlogar, 
                     @out_logar = $outlogar output
end-procedure

! ****************************************************************************

begin-procedure  addtotals($hmerologio,#xrevsh,#pistvsh)
  move 0 to #inx
  while #inx<20
    let #inx=#inx+1
    get $tmphmer from hmer_arr(#inx) hmerologio
    if $tmphmer=$hmerologio
       break
    end-if
  end-while

     get #tmp from hmer_arr(#inx) total_xre
     let #tmp=#tmp+#xrevsh
     put #tmp into hmer_arr(#inx) total_xre
     get #tmp from hmer_arr(#inx) total_pis
     let #tmp=#tmp+#pistvsh
     put #tmp into hmer_arr(#inx) total_pis

end-procedure


! ****************************************************************************

begin-procedure loadall

begin-select
dateadd(dd,-1,$apohm) &evshmeromhnia
end-select

begin-select 
a.hmerologio          () NOP
a.seira               () NOP
c.perigrafh           () NOP

  let #inx=&a.seira
  do getdioru(&a.hmerologio,$datestart,&evshmeromhnia,#dio_xre,#dio_pis)
  put 0        into hmer_arr(#inx) total_xre
  put 0        into hmer_arr(#inx) total_pis
  put #dio_xre into hmer_arr(#inx) pre_xre
  put #dio_pis into hmer_arr(#inx) pre_pis
  put &c.perigrafh  into hmer_arr(#inx) perigrafh
  put &a.hmerologio into hmer_arr(#inx) hmerologio
  if &a.seira>#maxarr
     move &a.seira to #maxarr
  end-if

from  gl_hmerandomada a, gl_hmerologia c
where a.cid=$cid
and   a.hmerologio=c.kvdikos
and   a.cid=c.cid
! (v) 1.9.94 Nick
and   a.omada = $omada 
end-select


  execute do=loadall2 glp_hmomadatot 
          @hmer_apo=$datestart, @hmer_evs=&evshmeromhnia, 
          @omada=$omada
     into &hmerologio    char(03),
          &perigrafh     char(30),
          &xrevsh        float,
          &pistvsh       float,
          &seira         int

end-procedure

begin-procedure loadall2

   move &seira to #inx


   get #tmp from hmer_arr(#inx) pre_xre
   let #tmp=#tmp+&xrevsh
   put #tmp into hmer_arr(#inx) pre_xre

   get #tmp from hmer_arr(#inx) pre_pis
   let #tmp=#tmp+&pistvsh
   put #tmp into hmer_arr(#inx) pre_pis

end-procedure 

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΗΜΕΡΟΛΟΓΙΟ ΕΓΓΡΑΦΩΝ ΓΕΝΙΚΗΣ ΛΟΓΙΣΤΙΚΗΣ' (2,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print 'Μηχ.Αρ'          (5,01,06)
   print 'Περιγραφή'        (,+1,25) 
   print 'Λογαριασμός'      (,+1,15)
   print 'Παρ/κό  '         (,+1,10)
   print 'Αιτιολογία'       (,+1,30)
   position (,+5)
   print 'Χρέωση    -     Πίστωση'       (,+1,25)

End-Heading
!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	2	1	19	hmer	rs	X	01},TITLE{Ημερολόγιο Κινήσεων Γ.Λ.}
!	PARCOUNT{2}
!	PAR1{Ημερομηνίες	DUMP	2	ΑΠΟ-ΕΩΣ	datetime		X}
!	PAR2{Ομαδα Ημερολογιων	DUMP2	1	=	varchar(3)		X}
!REPORTHELP{T},RNHELP{πχ ΓΕΝ (Γενικο Ημερολόγιο) κ.τ.λ.},
!</pcalib>
