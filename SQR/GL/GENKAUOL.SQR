!***********************************************************************
!***** REPORT      : genkauol.sqr                                   ****
!***** DATE        : 1/9/99                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : --- ΓΕΝΙΚΟ ΚΑΘΟΛΙΚΟ                ---         ****
!***********************************************************************

#DEFINE   PAGE-Y    58
#DEFINE   PAGE-X    131 
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1     999,999,999.99c
#DEFINE   MASK11    999,999,999.99c
#DEFINE   MASK2     999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

 create-array name=   total_arr size=20 field=total_xre:number
                                        field=total_pis:number
 create-array name=pretotal_arr size=20 field=total_xre:number
                                        field=total_pis:number
 create-array name=ypototal_arr size=20 field=total_xre:number
                                        field=total_pis:number
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 move 1 to #apolevel
 move 1 to #evslevel
 move $L_XRHSH to #xrhsh

 move '1 jan 1992' to $datestart

 do main($SQLCMD,#xrhsh,$datestart,$apohm,$evshm,#apolevel,#evslevel)

End-Report

! ****************************************************************************

begin-procedure main($SQLCMD,#xrhsh,$datestart,$apohm,$evshm,#apolevel,#evslevel)

move 0 to #maxinx

next-listing

begin-select
a.baumos          (,1) on-break print=change/top-page after=levelbreak 
a.logar             () NOP
a.perigrafh         () NOP

      do makelogform(&a.logar,$logarpr)

      print $logarpr        (, 1,15)
      print &a.perigrafh    (,+0,25)

      do printrest(&a.logar,$datestart,$apohm,$evshm,#xrevsh,#pistvsh,#pre_xrevsh,#pre_pistvsh)

      print #xrevsh         (,+1,14)  edit {MASK1}
      print #pistvsh        (,+1,14)  edit {MASK1}
      print #pre_xrevsh     (,+1,14)  edit {MASK1}
      print #pre_pistvsh    (,+1,14)  edit {MASK1}

      move &a.baumos to #inx
      let #diff=#pre_xrevsh-#pre_pistvsh
      if #diff > 0
         print #diff        (,+1,14)  edit {MASK1}
         position (,+14)
         get #tmp from ypototal_arr(#inx) total_xre
         let #tmp=#tmp+#diff
         put #tmp into ypototal_arr(#inx) total_xre
      else
         let #diff=#diff*(-1)
         position (,+15)
         print #diff        (,+1,14)  edit {MASK1}
         get #tmp from ypototal_arr(#inx) total_pis
         let #tmp=#tmp+#diff
         put #tmp into ypototal_arr(#inx) total_pis
      end-if

      get #tmp from total_arr(#inx) total_xre
      let #tmp=#tmp+#xrevsh
      put #tmp into total_arr(#inx) total_xre

      get #tmp from total_arr(#inx) total_pis
      let #tmp=#tmp+#pistvsh
      put #tmp into total_arr(#inx) total_pis

      get #tmp from pretotal_arr(#inx) total_xre
      let #tmp=#tmp+#pre_xrevsh
      put #tmp into pretotal_arr(#inx) total_xre

      get #tmp from pretotal_arr(#inx) total_pis
      let #tmp=#tmp+#pre_pistvsh
      put #tmp into pretotal_arr(#inx) total_pis


      if #maxinx < #inx
         move #inx to #maxinx
      end-if

      next-listing

from gl_logar a, ap_user b
where a.cid=b.company
  and b.suname=suser_name()
  and a.baumos between #apolevel and #evslevel
  and a.xrhsh = #xrhsh
[$SQLCMD]
order by a.logar, a.baumos
end-select

next-listing need=4

move 1 to #inx

  get #xrevsh  from total_arr(#inx) total_xre
  get #pistvsh from total_arr(#inx) total_pis
  get #pre_xrevsh  from pretotal_arr(#inx) total_xre
  get #pre_pistvsh from pretotal_arr(#inx) total_pis
  get #ypo_xrevsh  from ypototal_arr(#inx) total_xre
  get #ypo_pistvsh from ypototal_arr(#inx) total_pis

   print '='			(,01,{PAGE-X}) fill
   next-listing
   print 'ΓΕΝΙΚΑ ΣΥΝΟΛΑ : '  (, 1,15)
   print ' '             (,+1,25)  edit {MASK2}
   print #xrevsh         (,42,14)  edit {MASK11}
   print #pistvsh        (,+1,14)  edit {MASK11}
   print #pre_xrevsh     (,+1,14)  edit {MASK11}
   print #pre_pistvsh    (,+1,14)  edit {MASK11}
   print #ypo_xrevsh     (,+1,14)  edit {MASK11}
   print #ypo_pistvsh    (,+1,14)  edit {MASK11}

   next-listing


end-procedure

! ****************************************************************************

begin-procedure levelbreak

  next-listing 

end-procedure

! ****************************************************************************

begin-procedure makelogform($inlogar,:$outlogar)
  execute glp_logfrm @in_logar=$inlogar, @out_logar=$outlogar output
end-procedure

! ****************************************************************************

begin-procedure printrest ($logar,$datestart,$apohm,$evshm,:#xrevsh,:#pistvsh,:#pre_xrevsh,:#pre_pistvsh)

  execute glp_getypol @logar=$logar,
                      @hmer_apo=$apohm,
                      @hmer_evs=$evshm,
                      @xrevsh  =#xrevsh  output,
                      @pistvsh =#pistvsh output,
                      @diafora =#diafora output

  execute glp_getypol @logar=$logar,
                      @hmer_apo=$datestart,
                      @hmer_evs=$evshm,
                      @xrevsh  =#pre_xrevsh  output,
                      @pistvsh =#pre_pistvsh output,
                      @diafora =#pre_diafora output

end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΓΕΝΙΚΟ ΚΑΘΟΛΙΚΟ ΓΕΝΙΚΗΣ ΛΟΓΙΣΤΙΚΗΣ' (2,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print ' '                                  (5,1,13)
   print ' '                                  (,+0,27)
   print '--------- Περιόδου ---------'       (,+2,28)
   print '------- Προοδευτικά --------'       (,+2,28)
   print '--------- Υπόλοιπα ---------'       (,+2,28)

   print 'Λογαριασμός'                        (6,1,13)
   print 'Περιγραφή Λογαριασμού'              (,+0,27)
   print '   Χρέωση    -    Πίστωση'          (,+2,28)
   print '   Χρέωση    -    Πίστωση'          (,+2,28)
   print '   Χρέωση    -    Πίστωση'          (,+2,28)

End-Heading

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	2	1	17	genkauol	rs	X	01},TITLE{Γενικό Καθολικό}
!	PARCOUNT{2}
!	PAR1{Ημερομηνίες	DUMP	2	ΑΠΟ-ΕΩΣ	datetime		X}
!	PAR2{Λογαριασμός	a.logar	2	BETWEEN	char		}
!REPORTHELP{F}
!</pcalib>
