! ******************************************************************************
! ***** REPORT      : hmer.sqr                                              ****
! ***** DATE WRITTEN: 28/04/93                 LAST UPDATE: 20/09/95 (JT)   ****
! ***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                                      ****
! ***** NOTE        : --- ΗΜΕΡΟΛΟΓΙΟ ΟΜΑΔΑΣ    ΓΕΝ.ΛΟΓ.  ---                ****
! ******************************************************************************

#DEFINE   PAGE-Y                 58
#DEFINE   PAGE-X                131 
#DEFINE   HEAD-SIZE               6
#DEFINE   FOOT-SIZE               1
#DEFINE   HEAD-LINE               4

#DEFINE   DATEMASK         DD/MM/YY
#DEFINE   MASK1              999999
#DEFINE   MASK2     999,999,999,999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 input $omhm      'Ομαδα Ημερολογ. : '
 unstring $omhm by '"' into $filler1 $omada $filler1

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 move 0 to #maxarr

 move '1 jan 1992' to $datestart
! move 'ΓΕΝ'        to $omada

 do init
 do main

End-Report

! ****************************************************************************

begin-procedure main

move 'F' to $pageflag

begin-select
convert(datetime,a.hmer_e) &a.hmer_e (,10,08) on-break print=change/top-page 
                                     before=hmerbreak save=$hmer level=1
                                     after=totalbreak 
a.mhxar                     (,01,06) edit {MASK1}
a.logar                     () NOP
a.synal                     () NOP
   do makelogform($cid, &a.synal, &a.logar, $logarpr, $perigrafh)
   print $perigrafh         (,+1,25)
   print $logarpr           (,+1,15)
a.parid                     (,+1,08)
a.aitia                     (,+1,30)
a.poso                      () NOP
d.flag_xp                   () NOP
a.hmerologio    &hmer       () NOP

   position (,+5)

   if &d.flag_xp='X'
      let #pro_xre = #pro_xre + &a.poso
      print &a.poso         (,+1,15) edit {MASK2}
      position (,+16)
   else
      let #pro_pis = #pro_pis + &a.poso
      position (,+16)
      print &a.poso         (,+1,15) edit {MASK2}
   end-if

   next-listing

from  gl_kin a, gl_glpar d, gl_hmerandomada ss
where a.cid    = $cid       
and   ss.cid   = $cid
and   a.parast = d.parast
and   a.hmerologio = ss.hmerologio
and   ss.omada = $omada 
and   a.hmer_e between $apohm and $evshm
! (v) 28.9.94 Nick - Ισχυει μονο για το θεωρημενο
! and   a.mhxar is not null
[$SQLCMD]
order by a.hmer_e, a.mhxar
end-select

next-listing 

end-procedure

! ****************************************************************************

begin-procedure hmerbreak
  
  next-listing
  print 'Ημερομηνία : '  (1) center
  print &a.hmer_e        (,+2) edit {DATEMASK}
  next-listing
  next-listing

  print 'ΣΥΝΟΛΑ ΑΠΟ ΜΕΤΑΦΟΡΑ'  (,50)
  print ' : '                  (,+1,24)
  print #pro_xre               (,+1,15) edit {MASK2}
  print #pro_pis               (,+1,15) edit {MASK2}

  next-listing

end-procedure

! ****************************************************************************

begin-procedure totalbreak

  next-listing

  print 'ΣΥΝΟΛΑ ΣΕ ΜΕΤΑΦΟΡΑ'   (,50)
  print '['                    (,+1,01)
  print $hmer                  (,+1,08) edit {DATEMASK}
  print ']'                    (,+1,01)
  print ' : '                  (,+1,12)
  print #pro_xre               (,+1,15) edit {MASK2}
  print #pro_pis               (,+1,15) edit {MASK2}

  new-page

end-procedure

! ****************************************************************************
begin-procedure init
 
begin-select
dateadd(dd, -1, $apohm) &evshmeromhnia
end-select

  move 0 to #pro_xre
  move 0 to #pro_pis
  do getdioru($cid, $omada, $datestart, &evshmeromhnia, #pro_xre, #pro_pis)

end-procedure

! ****************************************************************************

begin-procedure getdioru($cid, $hmerologio, $hmer1, $hmer2, :#xrevsh, :#pistvsh)

move 0 to #xrevsh
move 0 to #pistvsh

begin-select
sum(xrevsh)    &dio_xre    () NOP
sum(pistvsh)   &dio_pis    () NOP

  move &dio_xre  to #xrevsh
  move &dio_pis  to #pistvsh

from  gl_dioru a
where a.cid = $cid 
and   a.hmerologio = $hmerologio
and   a.hmeromhnia between $hmer1 and $hmer2
end-select

end-procedure

! ****************************************************************************

begin-procedure makelogform($cid,$synal,$inlogar,:$outlogar,:$logper)

if $synal = ''
  execute glp_logfrm @in_logar=$inlogar, @out_logar=$outlogar output
  do getlog ($cid, $inlogar, $logper)
else
  let $outlogar = $inlogar||'/'||$synal
  do getsynal ($synal,$logper)
end-if
end-procedure

begin-procedure getlog($cid,$inlogar,:$logper)
  move '' to $logper
begin-select
perigrafh     () NOP
  move &perigrafh to $logper
from gl_logar
where logar=$inlogar
  and cid=$cid
end-select
end-procedure

begin-procedure getsynal($synal,:$epvnymia)
  move '' to $epvnymia
begin-select
epvnymia     () NOP
  move &epvnymia to $epvnymia
from sy_synal
where synal=$synal
end-select
end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΗΜΕΡΟΛΟΓΙΟ ΕΓΓΡΑΦΩΝ ΓΕΝΙΚΗΣ ΛΟΓΙΣΤΙΚΗΣ' (2,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print 'Μηχ.Αρ'          (5,01,06)
   print 'Περιγραφή'        (,+1,25) 
   print 'Λογαριασμός'      (,+1,15)
   print 'Παρ/κό  '         (,+1,08)
   print 'Αιτιολογία'       (,+1,30)
   position (,+5)
   print 'Χρέωση     -     Πίστωση'       (,+7,25)

End-Heading
