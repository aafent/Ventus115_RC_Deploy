!***********************************************************************
!***** REPORT      : kartm.sqr                                      ****
!***** DATE        : 14/09/94                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : - ΚΑΤΑΣΤΑΣΗ ΚΑΡΤΕΛΑΣ ΛΟΓΑΡΙΑΣΜΟΥ -             ****
!***********************************************************************

#DEFINE   PAGE-Y    58
#DEFINE   PAGE-X    131 
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1     99,999,999,999c
#DEFINE   MASK7     999,999c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $apohmnia  'Από Ημερομηνία    : '
 input $evshmnia  'Eως Ημερομηνία    : '
 input $iapologar 'Λογαριασμός από   : '
 input $ievslogar 'Λογαριασμός έως   : '

 execute app_datecnv $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv $evshmnia, $evshm output, $evshmpr output
 unstring $iapologar by '"' into $filler $apologar $filler
 unstring $ievslogar by '"' into $filler $evslogar $filler

 let #col1   = 53
 let #col2   = 69
 let #prcol1 = 85
 let #prcol2 = 101
 let #ypcol  = 117

 display $cid
 display $apohm
 display $evshm
 display $apologar 
 display $evslogar
 display #apofpa   
 display #evsfpa
 display $apokathg 
 display $evskathg

 do main

End-Report

! ****************************************************************************

begin-procedure main
next-listing

begin-select
a.logar                                    () on-break print=never level=1
                                              before=printlogar 
                                              after=printtot
a.kathgoria                                () NOP
a.fpa                                      () NOP
convert(datetime,a.hmer_e)      &hmer      () NOP
a.parid                                    () NOP
a.aitia                                    () NOP
a.poso                                     () NOP
a.parast                                   () NOP

      do getxp(&a.parast, &a.poso, #xrevsh, #pistvsh)

      let #ypoloipo  = #ypoloipo  + #xrevsh - #pistvsh
      let #prxrevsh  = #prxrevsh  + #xrevsh
      let #prpistvsh = #prpistvsh + #pistvsh

      print &hmer                          (  ,01,08) edit {DATEMASK}
      print &a.parid                       (  ,+1,10)
      print &a.aitia                       (  ,+1,25)
      print &a.fpa                         (  ,+1,02) edit 99
      print &a.kathgoria                   (  ,+1,01)
      print #xrevsh                        (  ,#col1,14) edit {MASK1}
      print #pistvsh                       (  ,#col2,14) edit {MASK1}
      print #prxrevsh                      (  ,#prcol1,14) edit {MASK1}
      print #prpistvsh                     (  ,#prcol2,14) edit {MASK1}
      print #ypoloipo                      (  ,#ypcol,14) edit {MASK1}

      next-listing 

from gl_kin a
where a.cid   = $cid
  and a.logar between $apologar and $evslogar 
  and a.hmer_e  between $apohm and $evshm
[$SQLCMD]
order by a.logar, a.hmer_e, a.aruro
[$SQLORD]
end-select

end-procedure

! ****************************************************************************
begin-procedure printtot
  ! next-listing
  ! print 'ΥΠΟΛΟΙΠΟ ΚΑΡΤΕΛΑΣ'          ( ,30,20)
  ! print #ypoloipo                    ( ,#ypcol,14) edit {MASK1}
  move 0 to #ypoloipo
  print '. . '                      ( ,  1,{PAGE-X}) fill
  next-listing
  next-listing
end-procedure
! ****************************************************************************

begin-procedure getxp($parast, #poso, :#xrevsh, :#pistvsh)

  let #xrevsh  = 0
  let #pistvsh = 0

  if $parast = '01'
     let #xrevsh = #poso 
  end-if

  if $parast = '02'
     let #pistvsh = #poso 
  END-IF

end-procedure

! ****************************************************************************
begin-procedure printlogar
  if &a.logar != ''
     ! do getypol
  end-if
end-procedure
! ****************************************************************************

begin-procedure getypol 

 move 0 to #prxrevsh
 move 0 to #prpistvsh
 do getlog($cid,&a.logar,$logarpr,$logar_txt)
begin-select 
sum(a.xrevsh)            &xrevsh  () NOP
sum(a.pistvsh)           &pistvsh () NOP

  move &xrevsh  to #prxrevsh
  move &pistvsh to #prpistvsh

  next-listing need = 7 no-advance

  let $msg=$L_X3||'  '||$logarpr||' '||$logar_txt||'  '||$L_X3
  let #ll = length($msg)
  if #ll < 1
     move 1 to #ll
  end-if
  let #pp = ( {PAGE-X} / 2 ) - ( #ll / 2 )
  if #pp < 1
     move 1 to #pp
  end-if

  print $L_X1                       ( ,#pp,#ll) fill
  next-listing 
  print $msg                        ( ,#pp,#ll)
  next-listing
  print $L_X1                       ( ,  1,{PAGE-X}) fill
  next-listing

  move ' ' to $msg
  let $msg='ΑΠΟ ΜΕΤΑΦΟΡΑ '
  print $msg                        ( ,21,50)
  print #prxrevsh                   ( ,#prcol1,14) edit {MASK1}
  print #prpistvsh                  ( ,#prcol2,14) edit {MASK1}
  let #ypoloipo = #prxrevsh - #prpistvsh
  print #ypoloipo                    ( ,#ypcol,14) edit {MASK1}
from gl_ypol a
where a.hmeromhnia < $apohm
  and a.cid = $cid
  and a.logar = &a.logar
end-select
 next-listing
end-procedure

! ****************************************************************************

begin-procedure getlog($cid,$inlogar,:$outlogar,:$logar_txt)
  execute glp_logfrm @cid=$cid, @in_logar=$inlogar, @out_logar=$outlogar output
  move '' to $logar_txt
begin-select
perigrafh      () NOP
  move &perigrafh to $logar_txt
from gl_logar
where cid=$cid
  and logar=$inlogar
end-select
end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 let $msg = 'ΚΑΡΤΕΛΑ ΚΙΝΗΣΗΣ ΛΟΓΑΡΙΑΣΜΩΝ ['||$logar||']'

 let $msg = $msg || ' AΠΟ '||$apohmpr||' ΕΩΣ '||$evshmpr
 print $msg (3,+1) center

   print '|----- Κίνηση  Περιόδου -----|'        (5,#col1,30)
   print '|---- Προοδευτική Κίνηση ----|'        ( ,#prcol1,30)
   print '  Υπόλοιπο'                            ( ,#ypcol,14)

   print 'Ημερ/νία'                              (6,1,08)  
   print 'Παραστατικό'                           ( ,+1,11)
   print 'Αιτιολογία Κίνησης'                    ( ,+1,23)
   print 'ΦΠΑ Κ'                                 ( ,+1,05)
   print 'Χ ρ έ ω σ η  -  Π ί σ τ ω σ η '        ( ,#col1,30)
   print 'Χ ρ έ ω σ η  -  Π ί σ τ ω σ η '        ( ,#prcol1,30)
   print '  Καρτέλας'                            ( ,#ypcol,14)

End-Heading
