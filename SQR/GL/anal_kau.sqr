!***********************************************************************
!***** REPORT      : kart.sqr                                       ****
!***** DATE        : 12/04/93                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : --- ΚΑΤΑΣΤΑΣΗ ΚΑΡΤΕΛΑΣ ΛΟΓΑΡΙΑΣΜΟΥ ---         ****
!***********************************************************************

#DEFINE   PAGE-Y                64
#DEFINE   PAGE-X               132
#DEFINE   HEAD-SIZE              8
#DEFINE   FOOT-SIZE              3
#DEFINE   HEAD-LINE              4

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   SHORTDATE          DD/MM
! #DEFINE   MASK1     9999,999,999.99c
#DEFINE   MASK1     99999999999.99c
#DEFINE   MASK7            9999.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report

 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '
 input $ikat      'Υποκατάστημα    : '
 input $pvlhths   'Πωλητής         : '

 execute app_datecnv $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv $evshmnia, $evshm output, $evshmpr output
 unstring $ikat by '"' into $filler $kat $filler
 unstring $pvlhths by '"' into $filler $pvlhths $filler

 if $kat = '*'
    let $kat = '%'
 end-if

 CREATE-ARRAY NAME=Log_Level 			! ΣΥΝΟΛΑ ΒΑΘΜΩΝ
	      SIZE=10
              FIELD=xrevsh:number
	      FIELD=pistvsh:number

 CREATE-ARRAY NAME=Cur_Logar 			! ΣΥΝΟΛΑ ΛΟΓΑΡΙΑΣΜΟΥ
	      SIZE=3				! Index 0 -> Προηγούμενα Σύνολα
              FIELD=xrevsh:number		! Index 1 -> Σύνολα Περιόδου
	      FIELD=pistvsh:number  		! Index 2 -> Σύνολα Σελίδας

 create-array name=gen_ajies			! Γενικά Σύνολα απο-σε μεταφορα
              size=1
              field=xrevsh:number
              field=pistvsh:number
	      field=msg:char

  move 25 to #c1
  move 88 to #c2

  move 20 to #c1
  move 83 to #c2


 do mhden_cur_logar
 do zero-baumos(10)
 put 0 into gen_ajies(0) xrevsh
 put 0 into gen_ajies(0) pistvsh
 put 'ΣΕ ΜΕΤΑΦΟΡΑ Γ Ε Ν Ι Κ Α   Σ Υ Ν Ο Λ Α' into gen_ajies(0) msg
 move 'F' to $f_print_apometafora

 do pca_printcover		! sqr/sqr/pcafun1.inc
 do main
 
End-Report

#INCLUDE "{$L_GDATA}/sqr/pcafun1.inc"

! ****************************************************************************

begin-procedure main

  next-listing

begin-select -C1
a.post_flag	  () NOP
	move &a.baumos to #baumos
a.baumos	  () NOP
a.logar           () NOP
a.perigrafh       () NOP
b.xrhsh		  () NOP
b.xrhsh_arxh	  () NOP

   if $pvlhths='*' 
	move 'T' to $do_print_flag
   else
	do checkpvlhths($cid,&a.logar,$pvlhths,$do_print_flag)
   end-if

   if $do_print_flag = 'T'

      do makelogform($cid, &b.xrhsh, &a.logar, $logarpr)
      If isnull(&a.post_flag)
      	do printlogar	
       	next-listing
      else
      	do printdetails
      	do mhden_cur_logar
      end-if     
   end-if

from  gl_logar a, ap_xrhsh b
where a.cid=$cid
and   b.cid=$cid
and   a.cid=b.cid
and   a.xrhsh=b.xrhsh        ! ca 11.1.99
and   $apohm between b.xrhsh_arxh and b.xrhsh_telos
and   exists (select 1 from gl_ypol c where a.cid=c.cid and a.logar=c.logar 
                 and c.hmeromhnia between b.xrhsh_arxh and $evshm )
[$SQLCMD]
order by a.logar
end-select
put '            Γ Ε Ν Ι Κ Α   Σ Υ Ν Ο Λ Α' into gen_ajies(0) msg
move 1 to #baumos
do chk4totals

end-procedure

! ****************************************************************************

begin-procedure checkpvlhths($cid,$logar,$pvlhths,:$flag)
  move 'F' to $flag
  move ''  to $synal

  move 0 to #cnt
begin-select 
synal &synal () NOP
	move &synal to $synal
        add 1 to #cnt

	display 'LOGAR: '   noline
	display $logar      noline
	display ' SYNAL: '  noline
	display $synal      noline
	display ' COUNT : ' noline
	move #cnt to $cnt 999
	display $cnt

	if $synal > '' and $flag = 'F'
	   do checkpvlhths2($synal,$pvlhths,$flag)
	end-if

from sy_synaleidos
where cid=$cid
  and logar = $logar
  and eidos = 'PELAT-ES'
end-select


end-procedure

!begin-procedure checkpvlhths2($synal,$pvlhths,:$flag)
!  move 'F' to $flag
!begin-select
!synal &synal () NOP
!	 move 'T' to $flag
!from sy_info
!where synal=$synal
!  and info_id = 7
!  and info_data = $pvlhths
!end-select
!end-procedure


begin-procedure checkpvlhths2($synal,$pvlhths,:$flag)
  move 'F' to $flag
  move $pvlhths to #int_pvlhths
begin-select
synal &synal () NOP
	 move 'T' to $flag
from sy_synaltopos
where synal=$synal
  and pvlhths = #int_pvlhths
end-select
end-procedure

! ****************************************************************************

begin-procedure mhden_cur_logar

      put 0 into Cur_Logar(0) xrevsh
      put 0 into Cur_Logar(0) pistvsh
      put 0 into Cur_Logar(1) xrevsh
      put 0 into Cur_Logar(1) pistvsh
      put 0 into Cur_Logar(2) xrevsh
      put 0 into Cur_Logar(2) pistvsh

end-procedure


! ****************************************************************************

begin-procedure makelogform($cid, #xrhsh, $inlogar, :$outlogar)

  execute -C2 glp_logfrm @cid       = $cid, 
              	         @xrhsh     = #xrhsh, 
                         @in_logar  = $inlogar, 
                         @out_logar = $outlogar output
end-procedure

! ****************************************************************************

begin-procedure printlogar
  
  do chk4totals
  print $logarpr 			   	(,01,15)
  print &a.perigrafh			   	(,+1,30)
  next-listing
  print ' ' (,1,1)
end-procedure

! ********************************

begin-procedure printdetails2

  do printlogar
  do getypol($cid, &a.logar, &b.xrhsh_arxh, $apohm, #pre_xrevsh, #pre_pistvsh)

  put #pre_xrevsh  into Cur_Logar(0) xrevsh	! Σύνολα λογαριασμού προηγούμενης περιόδου
  put #pre_pistvsh into Cur_Logar(0) pistvsh
  do add-baumos(&a.baumos, #pre_xrevsh, #pre_pistvsh)	! Πρόσθεση στους βαθμούς των αξιών
  
  let #pre_ypol = #pre_xrevsh - #pre_pistvsh

  if (#pre_xrevsh <> 0) or (#pre_pistvsh <> 0)
     let $msg='    ΠΡΟΗΓΟΥΜΕΝΗ ΚΙΝΗΣΗ '
     print $msg					  	(,#c1,37)
     print #pre_xrevsh                         		(,#c2,15) 	edit {MASK1}
     print #pre_pistvsh                         	(,+1,15) 	edit {MASK1}
     print #pre_ypol                            	(,+1,15) 	edit {MASK1} 

     add #pre_xrevsh  to #genxrevsh
     add #pre_pistvsh to #genpistvsh

     next-listing
  end-if

end-procedure

! ********************************

begin-procedure printtotals

      Get #l1_xrevsh  from Cur_Logar(1) xrevsh		! Σύνολα λογαριασμού περιόδου
      Get #l1_pistvsh from Cur_Logar(1) pistvsh
      Let #ypol = #l1_xrevsh - #l1_pistvsh

      if (#l1_xrevsh <> 0) or (#l1_pistvsh <> 0)
         print '    ΣΥΝΟΛΑ ΑΝΑΛΥΣΗΣ'	(,#c1)
         print #l1_xrevsh		(,#c2,15)	edit {MASK1}
         print #l1_pistvsh		(,+1,15)	edit {MASK1}
         print #ypol 			(,+1,15)	edit {MASK1}
      
         next-listing
      end-if

end-procedure

! ********************************

begin-procedure printdetails

move 0 to #flag1

	if #flag1 = 0 
             do printdetails2
	end-if

begin-select -C2

convert(datetime, k.hmer_e) &a.hmer_e    (,#c1,5)   edit {SHORTDATE}
k.aruro                       () NOP
k.parid                       (,+1,10)
k.kat                         (,+1,02)
k.aitia                       (,+1,40)    ! it was : (,+1,17)
k.xrevsh    () NOP   !    (,#c2,15)   	edit {MASK1}
k.pistvsh   () NOP   !    (,+1,15)   	edit {MASK1}

   move &k.xrevsh  to #xrevsh

   if #xrevsh<> 0 
	print #xrevsh   (,#c2,15)   	edit {MASK1}
   else
	print ''        (,#c2,15)   	edit {MASK1}
   end-if

   move &k.pistvsh to #pistvsh

   if #pistvsh<> 0 
	print #pistvsh  (,+1,15)   	edit {MASK1}
   else
	print ''        (,+1,15)   	edit {MASK1}
   end-if


   add 1 to #flag1
   let #pre_ypol = #pre_ypol + &k.xrevsh - &k.pistvsh
   print #pre_ypol	      (,+1,15)		edit {MASK1}

   !add &k.xrevsh  to #genxrevsh
   !add &k.pistvsh to #genpistvsh

   array-add &k.xrevsh  to Cur_Logar(1) xrevsh		! σύνολα λογαριασμού περιόδου
   array-add &k.pistvsh to Cur_Logar(1) pistvsh
   array-add &k.xrevsh  to Cur_Logar(2) xrevsh		! σύνολα λογαριασμού Σελίδας
   array-add &k.pistvsh to Cur_Logar(2) pistvsh	
   do add-baumos(&a.baumos, &k.xrevsh, &k.pistvsh)	! Πρόσθεση στους βαθμούς των αξιών
   next-listing need = 3

from  gl_kin  k
where k.cid   = $cid
and   k.logar = &a.logar
and   k.hmer_e between $apohm and $evshm

	! and   k.kat like $kat 
	! ΕΛΑΙΟΥΡΓΙΚΗ : υπάρχει πρόβλημα με τα υποκαταστήματα 
	! δέν έχουν όλες οι εγγραφές
order by  k.hmer_e, k.parid
end-select

 if #flag1 = 0 
 else
	do printtotals
 end-if

end-procedure printdetails

! ****************************************************************************

begin-procedure getypol ($cid, $logar, $xrhsh_arxh, $hmerapo, :#xrevsh, :#pistvsh)

begin-select -C2
sum(a.xrevsh)       &xrevsh    () NOP
sum(a.pistvsh)      &pistvsh   () NOP

  move  &xrevsh   to #xrevsh
  move  &pistvsh  to #pistvsh

from   gl_ypol a
where  a.cid=$cid
and    a.logar=$logar
and    a.hmeromhnia >= $xrhsh_arxh
and    a.hmeromhnia < $hmerapo
end-select

end-procedure

! ****************************************************************************

begin-procedure chk4totals 

    if #baumos < #old_baumos
	let #i = #old_baumos
    else
        if #baumos = #old_baumos
		let #i = #baumos
	else
		if #baumos > #old_baumos
			let #i = #baumos + 1  
			! nothing to do
		end-if
	end-if
    end-if
	
    move #old_baumos to #i

    While #i >= #baumos

    	Get #s_xrevsh  From Log_Level(#i) xrevsh
    	Get #s_pistvsh From Log_Level(#i) pistvsh
    	let #s_ypol = #s_xrevsh - #s_pistvsh

        if #old_baumos = #i
           let $msg=' ** ΣΥΝΟΛΑ'
        else
	   let $msg='  * ΣΥΝΟΛΑ'
	end-if
    	print $msg  		    (,#c1,11)
	print #i		    (,+1,01)
    	print '- ΒΑΘΜΙΟΥ'           (,+1,10)

    	print #s_xrevsh		    (,#c2,15)	edit {MASK1}
    	print #s_pistvsh	    (,+1,15)	edit {MASK1}
    	print #s_ypol 		    (,+1,15)	edit {MASK1}

    	next-listing 

	let #i = #i - 1

    End-While

    next-listing 
    do zero-baumos(#baumos)

   move #baumos to #old_baumos

end-procedure

! ****************************************************************************

begin-procedure add-baumos (#tmpbaumos, #xrevsh, #pistvsh)

  array-add #xrevsh   to gen_ajies(0) xrevsh
  array-add #pistvsh  to gen_ajies(0) pistvsh

  while #tmpbaumos > 0

    array-add #xrevsh  to Log_Level(#tmpbaumos) xrevsh
    array-add #pistvsh to Log_Level(#tmpbaumos) pistvsh

    let #tmpbaumos = #tmpbaumos - 1

  end-while

end-procedure

! ****************************************************************************

begin-procedure zero-baumos (#baumos)

  While #baumos < 10

    Put 0 into Log_Level(#baumos) xrevsh
    Put 0 into Log_Level(#baumos) pistvsh

    let #baumos = #baumos + 1

  End-While

end-procedure

! **********************************************************************

begin-procedure print_gentotals(#ln,#msgpos,#ajiapos,$msg,#s_xrevsh,#s_pistvsh,$f_print_apometafora,$logarpr)

 let #s_ypol = #s_xrevsh - #s_pistvsh
 print $msg			(#ln,#msgpos)
 print #s_xrevsh		(,#ajiapos,15)	edit {MASK1}
 print #s_pistvsh		(,+1,15)	edit {MASK1}
 print #s_ypol 		        (,+1,15)	edit {MASK1}
 
 if $f_print_apometafora = 'T'
     let $msg=$logarpr||' ...συνέχεια...'
     print $msg       (+1,1)
!     next-listing
 end-if


end-procedure

! **********************************************************************

Begin-Footing {FOOT-SIZE}

 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"   ! pos: 1 
 
 Get #page_xrevsh  From gen_ajies(0) xrevsh
 Get #page_pistvsh From gen_ajies(0) pistvsh
 Get $msg       From gen_ajies(0) msg
 move $logarpr to $page_logarpr
 do print_gentotals(2,#c1,#c2,$msg,#page_xrevsh,#page_pistvsh,$f_print_apometafora,$page_logarpr)
 move 'T' to $f_print_apometafora

 print $L_X2			(3,01,{PAGE-X})	fill

End-Footing

Begin-Heading {HEAD-SIZE}

   move 6 to #HEAD_END_LINE
   #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"  

   print 'Α Ν Α Λ Υ Τ Ι Κ Ο   Κ Α Θ Ο Λ Ι Κ Ο   Γ Ε Ν Ι Κ Η Σ   Λ Ο Γ Ι Σ Τ Ι Κ Η Σ' (2,01) center
   let $msg = '( ' || $apohmpr || ' ΕΩΣ ' || $evshmpr || ' )'
   print $msg (3,01) center

   print 'ΛΟΓΑΡΙΑΣΜΟΣ'       			(5,1,15)
   print ' '       				(,+1,30)

   print 'ΗΜΕΡ.'       				(,#c1,05)
   print 'ΠΑΡΑΣ/ΚΟ'				(,+1,10)
   print 'ΥΠ'      				(,+1,02)
   print 'ΑΙΤΙΟΛΟΓΙΑ'				(,+1,17)
   
   print '         ΧΡΕΩΣΗ'      	        (,#c2,15)
   print '        ΠΙΣΤΩΣΗ'      	        (,+1,15)  
   print '       ΥΠΟΛΟΙΠΟ'      	        (,+1,15)  

  if $f_print_apometafora = 'T'
	 !Get #s_xrevsh  From gen_ajies(0) xrevsh
	 !Get #s_pistvsh From gen_ajies(0) pistvsh
	 move 'ΣΥΝΟΛΑ ΑΠΟ ΜΕΤΑΦΟΡΑ' to $msg
	 do print_gentotals(7,#c1,#c2,$msg,#page_xrevsh,#page_pistvsh,$f_print_apometafora,$page_logarpr)
	 move 'F' to $f_print_apometafora
  end-if

End-Heading

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	2	99	16	anal_kau	rs	X	01},TITLE{Αναλυτικό Καθολικό Λογαριασμών}
!	PARCOUNT{5}
!	PAR1{Ημερομηνία	HMER	2	ΑΠΟ-ΕΩΣ	datetime		X}
!	PAR2{Λογαριασμός	a.logar	2	BETWEEN	char(25)		}
!	PAR3{Υποκατάστημα	YPOK	1	*/ΕΠΙΛΟΓΗ	char(02)		X}
!	PAR4{Για τον Πωλητη	PVL	1	*/ΕΠΙΛΟΓΗ	varchar(20)		X}
!	PAR5{Λογαριασμός	a.logar	1	LIKE	char(25)	Χ	}
!REPORTHELP{F}
!</pcalib>

