!***********************************************************************
!***** REPORT      : elex.sqr                                       ****
!***** DATE        : 12/04/93                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : --- ΚΑΤΑΣΤΑΣΗ ΕΛΕΓΧΟΥ ---                      ****
!***********************************************************************
#DEFINE   PAGE-Y    58
#DEFINE   PAGE-X    160
#DEFINE   HEAD-SIZE  6 
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   MASK1     99999999.99c
#DEFINE   MASK7     9999.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $ypokatasthma 'Υποκατάστημα  : '
 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '
 input $flagdate  'Καταχωρηση/Ενημέρωση  : '
 input $flag_style '1=αναλυτικά/2=Συγκεντρωτικά/3=Ανα ημέρα  : '

 execute app_datecnv $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv $evshmnia, $evshm output, $evshmpr output
 unstring $ypokatasthma by '"' into $filler $ypokatasthma $filler
 unstring $flagdate     by '"' into $filler $flagdate   $filler
 unstring $flag_style   by '"' into $filler $flag_style $filler

 if $flag_style <> '1' and $flag_style <> '2' and $flag_style <> '3' 
	move '3' to $flag_style
 end-if

 display 'FLAG DATE : ' noline
 display $flagdate
 If ($flagdate = 'E') or ($flagdate = 'e') or ($flagdate = 'Ε') or ($flagdate = 'ε')
    move ' and a.hmer_e ' to $DYNSQL
 else
    move ' and a.hmer_k ' to $DYNSQL
 end-if 


 Do Init
 Do Main

 Do Print_Date_Totals

End-Report

! ****************************************************************************

Begin-Procedure Init

move 0 to #total_date_diff
move 0 to #total_logar_tam
move 0 to #prood_xrevsh
move 0 to #prood_pistvsh
move 'T' to $flag_1time

move '3800' to $logar_tameioy ! default
begin-select -C1
log_tam () NOP
  move &log_tam to $logar_tameioy
from ap_katasthma
where cid=$cid and kat = $ypokatasthma
end-select


Begin-select -C1
x.xrhsh			&xrhsh		() NOP
x.xrhsh_arxh            &xrhsh_arxh     () NOP
from ap_xrhsh x
where cid = $cid
and $apohm between x.xrhsh_arxh and x.xrhsh_telos
End-select


begin-select -C1
sum(xrevsh) - sum(pistvsh) &ypol_tam () NOP
   move &ypol_tam to #total_logar_tam
from gl_ypol
where cid = $cid
and logar = $logar_tameioy
and hmeromhnia < $apohm
and hmeromhnia >= &xrhsh_arxh
end-select



display 'TAMEIO : ' noline
display $logar_tameioy

display 'DYN SQL  : ' noline
display $DYNSQL


display 'STYLE : ' noline
display $flag_style

display 'DATES FROM : ' noline
display $apohm noline
display ' TO : ' noline
display $evshm

End-Procedure

! ****************************************************************************

Begin-Procedure Main

Begin-select
a.aruro 		&aruro 		() NOP
a.hmer_e		&hmer_e		() NOP
a.hmer_k		&hmer_k		() NOP
a.logar			&logar		() NOP
a.parid			&parid		() NOP
a.xrevsh		&xrevsh		() NOP
a.pistvsh		&pistvsh	() NOP
a.xrevshx		&xrevshx	() NOP
a.pistvsh		&pistvshx	() NOP
a.aitia			&aitia		() NOP
a.hmer_ekt		&hmer_ekt	() NOP
a.mhxar			&mhxar		() NOP
a.synal 		&synal		() NOP
a.hmerologio            &hmerologio     () NOP
a.sxet                  &sxet		() NOP

	do Printing

from gl_kin a
where cid = $cid
[$DYNSQL] between $apohm and $evshm
[$SQLCMD]
order by a.hmer_e, a.hmerologio, a.parid, case substring(a.logar,1,1) when '9' then 1 else 0 end, a.aa

End-select
End-Procedure

! ****************************************************************************

Begin-Procedure Printing

 if &logar = $logar_tameioy
	let #diff = &xrevsh - &pistvsh
	let #total_logar_tam = #total_logar_tam + #diff
 end-if


 do makelogform($cid, &xrhsh, &synal, &logar, $logarpr, $eponymia)

Begin-Select 
convert(varchar(11),&hmer_e,4) &hmer_e_pr
convert(varchar(11),&hmer_k,4) &hmer_k_pr
convert(varchar(11),&hmer_ekt,4) &hmer_ekt_pr 
End-Select

 If $flagdate='K' 
 print &hmer_k_pr () on-break print=never Level=1 procedure=Print_Date_Totals
                              save = $hmer_k_pr
 Else
 print &hmer_e_pr () on-break print=never Level=1 procedure=Print_Date_Totals
                              save = $hmer_e_pr
 End-If

 if $flag_style = '1' or $flag_style = '2'
	 print &hmerologio     () on-break print=never Level=2 procedure=Hmerol_tot
	 print &aruro          () on-break print=never Level=3 procedure=Ar_tot 
 end-if

 
 if $flag_style = '2' 
    if $flag_1time='T'
	 print &hmerologio     (,01,05)
	 print &parid          (,+1,10)
	 move 'F' to $flag_1time
    end-if
 end-if

 if $flag_style = '3' 
    if $flag_1time='T'
	 print &hmer_e_pr      (,73,10)
	 move 'F' to $flag_1time
    end-if
 end-if

 if $flag_style = '1'
	 print &hmerologio      (,01,05)
	 print &parid           (,+1,10) 

	 print $logarpr         (,+1,12)
	 print $eponymia        (,+1,40)
	 print &sxet            (,+1,10)
	 print &aitia           (,+1,25) 
	 print &xrevsh          (,+1,11) edit {MASK1}
	 print &pistvsh         (,+1,11) edit {MASK1}
	 print #total_logar_tam (,+1,11) edit {MASK1}

	 Next-Listing
 end-if

 Let #date_pistvsh=#date_pistvsh+&pistvsh
 Let #hm_pistvsh=#hm_pistvsh+&pistvsh
 Let #ar_pistvsh=#ar_pistvsh+&pistvsh

 Let #date_xrevsh=#date_xrevsh+&xrevsh
 Let #hm_xrevsh=#hm_xrevsh+&xrevsh
 Let #ar_xrevsh=#ar_xrevsh+&xrevsh


End-Procedure Printing

! ****************************************************************************

begin-procedure makelogform($cid,#xrhsh,$synal,$inlogar,:$outlogar,:$logper)

execute glp_logfrm @in_logar=$inlogar, @out_logar=$outlogar output
do getlog ($cid, #xrhsh, $inlogar, $logper)

! Following lines are temporary commented-out for elaioyrgikh :
!move $inlogar to $omada x
!if $synal = '' or $omada = '1' or $omada = '2' or $omada = '6' or $omada = '7' 
!  execute glp_logfrm @in_logar=$inlogar, @out_logar=$outlogar output
!  do getlog ($cid, #xrhsh, $inlogar, $logper)
!else
!  let $outlogar = $synal
!  do getsynal ($synal,$logper)
!end-if
end-procedure

! ****************************************************************************

begin-procedure getlog($cid, #xrhsh, $inlogar, :$logper)
  move '' to $logper
begin-select -C5
perigrafh     () NOP
  move &perigrafh to $logper
from gl_logar
where logar = $inlogar
  and cid   = $cid
  and xrhsh = #xrhsh
end-select
end-procedure

! ****************************************************************************

begin-procedure getsynal($synal,:$epvnymia)
  move '' to $epvnymia
begin-select -C5
epvnymia     () NOP
  move &epvnymia to $epvnymia
from sy_synal
where synal=$synal
end-select
end-procedure

! ****************************************************************************

Begin-Procedure Ar_tot

 move 'T' to $flag_1time

 print 'Σύνολα Αρθρου ....... :' (,83,25)
 print #ar_xrevsh        (,+1,11) edit {MASK1}
 print #ar_pistvsh       (,+1,11) edit {MASK1}

 Let #ar_xrevsh=0
 Let #ar_pistvsh=0
 Next-Listing
 if $flag_style='1'
	 Next-Listing
 end-if
End-Procedure

! ****************************************************************************

Begin-Procedure Hmerol_tot
 
 Do Ar_Tot
 print $L_X2 (,83,49)  Fill
 Next-Listing
 print 'Σύνολα Ημερολογίου .. :' (,83,25)
 print #hm_xrevsh        (,+1,11) edit {MASK1}
 print #hm_pistvsh       (,+1,11) edit {MASK1}
 If #hm_xrevsh!=#hm_pistvsh
  print '*' (,+2)
 end-if
 

 Let #hm_xrevsh=0
 Let #hm_pistvsh=0
 Next-Listing
 Next-Listing
End-Procedure
! ****************************************************************************

Begin-Procedure Print_Date_Totals
 if $flag_style='1' or $flag_style='2' 
 	Do Hmerol_Tot
	print $L_X2 (,83,49)  Fill
	Next-Listing
 end-if
 print 'Σύνολα Ημέρας ....... :' (,83,25)
 print #date_xrevsh         (,+1,11) edit {MASK1}
 print #date_pistvsh        (,+1,11) edit {MASK1}

 add #date_xrevsh   to #prood_xrevsh
 add #date_pistvsh  to #prood_pistvsh

 let #diff =  #date_xrevsh - #date_pistvsh
 let  #total_date_diff = #total_date_diff + #diff
 if $flag_style = '3'
	 print 'Διαφορά ημέρας  : ' (,1,20)
	 print #diff         (,+1,11) edit {MASK1}
	 print 'Υπόλοιπο : ' (,+5,10)
	 print #total_date_diff    (,+1,11) edit {MASK1}
 end-if

 next-listing
 print 'Προοδευτικά Σύνολα .. :' (,83,25)
 print #prood_xrevsh             (,+1,11) edit {MASK1}
 print #prood_pistvsh            (,+1,11) edit {MASK1}
 let #diff =  #prood_xrevsh - #prood_pistvsh
 print ' Διαφορά : '             ()
 print #diff                     (,+1,11) edit {MASK1}
 

 Let #date_xrevsh=0
 Let #date_pistvsh=0
 move 'T' to $flag_1time
 if $flag_style='1' or $flag_style='2'
	 New-Page
 else
	 next-listing
 end-if
End-Procedure Printing

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
 ! print $reset_printer () code
End-Footing

! ****************************************************************************

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
 print $select12cpi () code
 print 'ΚΑΤΑΣΤΑΣΗ ΕΛΕΓΧΟΥ' (2,+1) center

 if $flag_style='3' 
	let $msg='ΣΥΝΟΛΩΝ ΑΝΑ ΗΜΕΡΟΜΗΝΙΑ'
 else
	 If $flagdate='Κ' or  $flagdate='κ' or  $flagdate='K' or  $flagdate='k' 
	   let $msg2='ΚΑΤΑΧΩΡΗΣΗΣ : '||$hmer_k_pr   
	 Else
	   let $msg2='ΕΝΗΜΕΡΩΣΗΣ : '||$hmer_e_pr    
	 End-If
  	 let $msg='ΜΕ ΗΜΕΡΟΜΗΝΙΑ '||$msg2
 end-if
 
print $msg (3,+1) center 
 print
 'Ημερ. Παρασ/κό   Λογαριασμός  Περιγραφή                                Σχετικό    Αιτιολογία                 Χρέωση    Πίστωση  Υπ.Ταμείου'   (5,1)

!'   Αρθρο   Λογαριασμός     Περιγραφή           Παραστατικό  Αιτιολογία           Χρέωση     Πίστωση     Ημερομηνία  Μηχανογ.'   (7,1)
!print 
!'                                                                                                       Λογιστ.    Αριθμός   '   (8,1)
End-Heading

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	1	2	13	elex	rs	X	01},TITLE{Κατάσταση Ελέγχου Εγγραφών}
!	PARCOUNT{8}
!	PAR1{Υποκατάστημα	DUMP_YPOK	1	ΕΠΙΛΟΓΗ	char(2)		X}
!	PAR2{Ημερομηνία	DUMP1	2	ΑΠΟ-ΕΩΣ	datetime		X}
!	PAR3{Ενημέρωση/Καταχώρηση	DUMP2	1	1ο ΓΡΑΜΜΑ	char(1)		X}
!	PAR4{Διαχειριστικό Ημερολ	a.hmerologio	1	=	char(3)		}
!	PAR5{Αναλυτικά/Συγκεντρωτ	DUMP3	1	=/ΕΠΙΛΟΓΗ	char(1)		X}
!	PAR6{Τους Λογαριασμούς	a.logar	1	LIKE	varchar(25)		}
!	PAR7{Οχι τους Λογαρ/μούς	a.logar	1	NOT LIKE	varchar(25)		}
!	PAR8{Οχι τους Λογαρ/μούς	a.logar	1	NOT LIKE	varchar(25)		}
!REPORTHELP{T},RNHELP{Ε=Ημ.Ενημέρωσης, Κ=Ημ.Καταχώρησης | 1=Αναλυτικά, 2=Συγκεντρωτικά, 3=Κατά ημέρα},RNMODDT{1998-03-23},
!</pcalib>
