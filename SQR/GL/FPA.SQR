!***********************************************************************
!***** REPORT      : fpa.sqr                                        ****
!***** DATE        : 11/07/94                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** NOTE        : --- ΣΥΓΚΕΝΤΡΩΣΗ ΦΠΑ ΚΑΤ Α'ΒΑΘΜΙΟ   ---         ****
!***********************************************************************

#DEFINE   PAGE-Y     62
#DEFINE   PAGE-X     132 
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  5

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK2     99
#DEFINE   MASK12    99,999,999,999c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 do crtfpatable($cid, $apohm, $evshm)

 do main

End-Report

! ****************************************************************************

begin-procedure main
begin-select
substring(a.logar,1,1)  &omada   () on-break level=1 print=never after=l2break
  move &omada to $omada
a.logar                    (,01,02) on-break level=2 print=change/top-page 
				    after=lbreak
  move &a.logar to $logar
b.perigrafh                (,+1,35) on-break level=3 print=change/top-page
a.column                   () NOP
d.perigrafh                (,+3,25) 
a.ajia                     () NOP
a.fpa                      () NOP
  do getfpa($cid, $apohm, $evshm, &a.logar, &a.column, #prfpa)
  let #tot_ajia  = #tot_ajia + &a.ajia
  let #tot_ypfpa = #tot_ypfpa + &a.fpa
  let #tot_prfpa = #tot_prfpa + #prfpa
  let #diaf      = #prfpa - &a.fpa
  print &a.ajia            (  ,+2,14) edit {MASK12}
  print #prfpa             (  ,+2,14) edit {MASK12}
  print &a.fpa             (  ,+2,14) edit {MASK12}
  print #diaf              (  ,+2,14) edit {MASK12}
  
  next-listing

from gl_ajiesfpa a, gl_logar b, gl_fkfpa d
where a.logar = b.logar
  and b.cid = $cid
  and a.column = d.column
[$SQLCMD]
order by a.logar, a.column  
end-select
end-procedure

! ****************************************************************************

begin-procedure crtfpatable($cid, $apohm, $evshm)

  execute glp_crtfpa $cid, $apohm, $evshm

end-procedure

! ****************************************************************************

begin-procedure getfpa($cid, $apohm, $evshm, $logar_aj, #column, :#fpa)
  move 0 to #fpa
begin-select
sum(a.xrevsh)-sum(a.pistvsh)  &totfpa
  move &totfpa to #fpa
from gl_ypol a, gl_fpa b
where a.cid = $cid 
  and a.logar = b.logar_fpa
  and a.hmeromhnia between $apohm and $evshm
  and b.logar_ajias = $logar_aj
  and b.column = #column
end-select
end-procedure 

! ****************************************************************************

begin-procedure lbreak

  print $L_X2                     (  ,01,{PAGE-X}) fill
  next-listing
  let $tot_msg = '   Σύνολα Πρωτοβαθμίου ('||$logar||') :' 
  print $tot_msg                  (  ,01,66)
  print #tot_ajia                 (  ,+2,14) edit {MASK12}
  print #tot_prfpa                (  ,+2,14) edit {MASK12}
  print #tot_ypfpa                (  ,+2,14) edit {MASK12}
  let #gtot_ajia = #gtot_ajia + #tot_ajia
  let #gtot_ypfpa = #gtot_ypfpa + #tot_ypfpa
  let #diaf = #tot_prfpa - #tot_ypfpa
  print #diaf                     (  ,+2,14) edit {MASK12}
  next-listing
  print $L_X2                     (  ,01,{PAGE-X}) fill
  move 0 to #tot_ajia
  move 0 to #tot_prfpa
  move 0 to #tot_ypfpa
			       
end-procedure

! ****************************************************************************

begin-procedure l2break

  let $tot_msg = '   ΣΥΝΟΛΑ ΟΜΑΔΑΣ ('||$omada||') :' 
  print $tot_msg                 (  ,01,66)
  print #gtot_ajia                 (  ,+2,14) edit {MASK12}
  do getfpa($cid, $apohm, $evshm, $omada, 0, #gtot_prfpa)
  print #gtot_prfpa               (  ,+2,14) edit {MASK12}
  print #gtot_ypfpa                (  ,+2,14) edit {MASK12}
  let #diaf = #gtot_prfpa - #gtot_ypfpa
  print #diaf                     (  ,+2,14) edit {MASK12}
  next-listing
  print $L_X1                     (  ,01,{PAGE-X}) fill
  move 0 to #gtot_ajia
  move 0 to #gtot_prfpa
  move 0 to #gtot_ypfpa
			       
end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΣΥΓΚΕΝΤΡΩΣΗ Φ.Π.Α. ΚΑΤΑ ΠΡΩΤΟΒΑΘΜΙΟ' (3,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (4,+1) center

  print '   Λογαριασμός       '   (06,01,38)
  print 'Περιγραφή Φ.Π.Α.'        (  ,+1,25)
  print '   Αξία'                 (  ,+4,12)
  print 'Πραγμ. ΦΠΑ'              (  ,+4,12)
  print 'Υπολογ. ΦΠΑ'             (  ,+4,12)
  print 'Διαφορά'                 (  ,+4,12)

End-Heading
