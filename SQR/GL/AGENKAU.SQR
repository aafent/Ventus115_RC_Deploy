!***********************************************************************
!***** REPORT      : agenkau.sqr                                    ****
!***** DATE        : 04/03/96                                       ****
!***** APPLICATION : Áíáëõôéêü Äçìüóéï Êáèïëéêü                     ****
!***** NOTE        : --- ÈÅÙÑÇÌÅÍÏ ÁÍÁËÕÔÉÊÏ ÊÁÈÏËÉÊÏ   ---         ****
!***********************************************************************

#DEFINE   PAGE-Y    58
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1     99,999,999,999
#DEFINE   MASK2     999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $xrhsh     '×ñÞóç           : ' 
 input $apohmnia  'Áðü Çìåñïìçíßá  : '
 input $evshmnia  'Eùò Çìåñïìçíßá  : '
 input $kathgoria 'Êáôçãïñßá       : '

 unstring $xrhsh by '"' into $filler $xrhsh $filler
 unstring $kathgoria by '"' into $filler $kathgoria $filler
 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output
 move $xrhsh to #xrhsh

  do main

End-Report

! ****************************************************************************

begin-procedure main
execute app_getxrhsh @FE = null,
			@xrhsh_arxh=$xrhsh_arxh output, 
			@xrhsh_telos=$xrhsh_telos output


move '0' to $memka
begin-select
convert(char(10),convert(datetime,hmeromhnia),103) &hmeromhnia,a.ka
poso_proyp,poso_desm,poso_prok,poso_ejof,sxolia,ar_entalma,apofash_anaf,
perigrafh
		if $memka = '0' 
			move &a.ka to $memka
			let $sxo = &a.ka ||'   '||&perigrafh
			print   $sxo	(+1)  center
                        next-listing
			move 'ÅÊ ÌÅÔÁÖÏÑÁÓ ' to $sxolio
			do ekmetaforas
			do synola
		end-if
		if $memka != &a.ka
			do synola
			move 0 to #suma_proyp
			move 0 to #suma_desm
			move 0 to #suma_prok
			move 0 to #suma_ejof
			move &a.ka to $memka
			new-page
			let $sxo = &a.ka ||'   '||&perigrafh
			print   $sxo	(+1)  center
                        next-listing
			move 'ÅÊ ÌÅÔÁÖÏÑÁÓ ' to $sxolio
			do ekmetaforas
			do synola
		end-if

 		do ektyp
from dl_kakin a, dl_ka b
where xrhsh=#xrhsh and hmeromhnia between $apohm and $evshm
and a.kathgoria=$kathgoria and a.kathgoria=b.kathgoria and a.ka=b.ka and 
b.epipedo=9 
![$SQLCMD]
order by ka,hmeromhnia
end-select
		do synola

end-procedure

begin-procedure ekmetaforas
begin-select
sum(poso_proyp)    &metaf_proyp,
sum(poso_desm)     &metaf_desm,
sum(poso_prok)     &metaf_prok,
sum(poso_ejof)     &metaf_ejof

		move &metaf_proyp to #sum_proyp
                move &metaf_desm to #sum_desm
                move &metaf_prok to #sum_prok
                move &metaf_ejof to #sum_ejof

from dl_kakin 
where hmeromhnia>= convert(datetime,$xrhsh_arxh,103) 
and   hmeromhnia<  $apohm and ka=&a.ka and xrhsh=#xrhsh

end-select
end-procedure


begin-procedure ektyp
print &hmeromhnia		(+1,1,10)
print &sxolia			(,+1,25)
print &poso_proyp		(,+1,14)  edit {MASK1}
print &poso_desm                (,+1,14)  edit {MASK1}
print &poso_prok                (,+1,14)  edit {MASK1}
print &poso_ejof                (,+1,14)  edit {MASK1}
print &ar_entalma               (,+1,10)
print &apofash_anaf             (,+1,10)
add &poso_proyp to #suma_proyp
add &poso_desm to #suma_desm
add &poso_prok to #suma_prok
add &poso_ejof to #suma_ejof
end-procedure


begin-procedure synola
print $sxolio			(+2,21,16)
print #suma_proyp		(,+1,14)  edit {MASK1}
print #suma_desm                (,+1,14)  edit {MASK1}
print #suma_prok                (,+1,14)  edit {MASK1}
print #suma_ejof                (,+1,14)  edit {MASK1}
let #diaf = #suma_proyp - #suma_desm
print '-'			(+1,1,132) fill
print 'ÕÐÏËÏÉÐÏ ÐÑÏÓ ÄÉÁÈÅÓÇ'   (+1,21,27)
print #diaf		        (,+1,14)  edit {MASK1}
print '-'			(+1,1,132) fill
move 'ÓÕÍÏËÁ' to $sxolio
end-procedure
! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ÁÍÁËÕÔÉÊÏ ÊÁÈÏËÉÊÏ ÄÇÌÏÓÉÁÓ ËÏÃÉÓÔÉÊÇÓ' (2,+1) center

 let $msg='ÐÅÑÉÏÄÏÕ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

      print 'ÇÌÅÑÏÌÇÍÉÁ'     (06,01,10) 
      print '         Ó×ÏËÉÁ          ' (,+1,25)
      print 'ÐÑÏÕÐÏËÏÃÉÓÌÏÓ' (,+1,14)
      print '      ÄÅÓÌÅÕÓÇ' (,+1,14) 
      print '   ÐÑÏÊÁÔÁÂÏËÇ' (,+1,14) 
      print '      EÎÏÖËÇÓÇ' (,+1,14)
      print '   ÅÍÔÁËÌÁ'     (,+1,10)
      print '   ÁÐÏÖÁÓÇ'     (,+1,10) 

End-Heading
