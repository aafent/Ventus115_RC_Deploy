!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : myf_rep.sqr                                    ****
!***** DATE        : 26/11/94                                       ****
!***** APPLICATION : synalages                                      ****
!***** AUTHOR      : THEOTOKAS JOHN                                 ****
!***** NOTE        : Συγκεντρωτική Κατάσταση Υποβολής Στοιχείων     ****
!***********************************************************************

#DEFINE   PAGE-Y    64
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   SMALLDATE DD/MM
#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1     9999,999,999.99c
#DEFINE   MASK2     999999

#DEFINE   MASK3     99
#DEFINE   MASK4     9999999
#DEFINE   MASK5     9999,999,999.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid
 move $L_XRHSH to #xrhsh

 input $hmer_apo  'Από Ημερομηνία  : ' 
 input $hmer_evs  'Eως Ημερομηνία  : ' 

 execute app_datecnv   $hmer_apo, $hmer_a output, $hmer_ap output
 execute app_datecnv   $hmer_evs, $hmer_e output, $hmer_ev output

 input $iflag  'FLAG  :'
 unstring $iflag by '"' into $filler $flag $filler

 input $xrepis  'FLAG  :'
 unstring $xrepis by '"' into $filler $xrepis $filler

 input $ypobol  '1=Ολα, 2=Υποβολή'
 unstring $ypobol by '"' into $filler $ypobol $filler


 ! input $ixrhsh 'XRHSH :'
 ! execute app_floatcnv $ixrhsh, #xrhsh output

   if $xrepis = '1'
      move 'and a.ajia >= 0' to $EXTRASQL
   else
   if $xrepis='2'
      move 'and a.ajia < 0' to $EXTRASQL
   else
      move ' ' to $EXTRASQL
   end-if
   end-if

   if $ypobol <> '1' and $ypobol <> '2' 
      move '2' to $ypobol
   end-if
 
   if $ypobol = '1' 
      move '' to $EXTRASQL2
   else
      move ' and a.ajia_flag is null ' to $EXTRASQL2
   end-if

! (v) build title

  if $flag = '2'
     let $title='ΣΥΓΚΕΝΤΡΩΤΙΚΗ ΚΑΤΑΣΤΑΣΗ ΠΕΛΑΤΩΝ '
  else
     let $title='ΣΥΓΚΕΝΤΡΩΤΙΚΗ ΚΑΤΑΣΤΑΣΗ ΠΡΟΜΗΘΕΥΤΩΝ '
  end-if
  if $xrepis ='1' 
     let $title=$title || 'ΧΡΕΩΣΤΙΚΑ'
  else
  if $xrepis = '2'
     let $title=$title || 'ΠΙΣΤΩΤΙΚΑ'
  else
     let $title=$title || 'ΧΡΕΩΣΤΙΚΑ+ΠΙΣΤΩΤΙΚΑ'
  end-if
  end-if
  if $ypobol = '1'
     let $title=$title || '-ΑΠΑΝΤΑ'
  else
     let $title=$title || '-ΓΙΑ ΥΠΟΒΟΛΗ'
  end-if

   

   display 'EXTRASQL :' noline
   display $EXTRASQL
   display 'EXTRASQL2 :' noline
   display $EXTRASQL2

   do main

End-Report

! *************************************************************************

begin-procedure main
  move 0 to #aa
show $cid 
show $hmer_a
show $hmer_e
show #xrhsh	

 ! NA PAEI STON PB : execute syp_insertmyf 'SQl' $cid $hmer_a $hmer_e #xrhsh	

begin-select 
a.flag                                () NOP
b.afm                                 () NOP  
!a.synal                              () NOP    
b.epvnymia                            () NOP    
b.epaggelma                           () NOP
b.dieyuynsh                           () NOP  
b.polh                                () NOP  
b.taxkvd                              () NOP
sum(a.ar_tim)	&a.ar_tim             () NOP
sum(a.ajia)       &a.ajia             () NOP 

  if length(&b.afm)=8
     let $afm = '0' || &b.afm
  else
     let $afm = &b.afm
  end-if

 !  do getdoy(&a.synal, $doy_perig)
  do getepagg(&b.epaggelma, $epag_per, $drast, $dras_per, $afm)

  move 'T' to $print_flag
!  if &a.synal = '3117958'
!     if &a.flag = '1'
!        move 'N' to $print_flag
!     end-if
!  end-if

  if $print_flag = 'T'
     let #cnt_tot = #cnt_tot + &a.ar_tim
     let #totals  = #totals  + &a.ajia
  end-if

  IF &a.flag = '2'
     if $drast = '0988' 
        let #cnt_dhmosio = #cnt_dhmosio + &a.ar_tim
        let #tot_dhmosio = #tot_dhmosio + &a.ajia 
        move 'T' to $print_flag
     end-if
  END-IF

  if $print_flag = 'T'
     let #aa = #aa + 1
     print #aa                                    (,01,04) edit 9999
     print $afm                                   (,+2,15)
     print &b.epvnymia                            (,+2,35)  
     print &b.dieyuynsh                           (,+2,30)
     print &a.ar_tim                              (,+3,06) edit {MASK2}
     print &a.ajia                                (,+3,15) edit {MASK1}
     next-listing

  end-if

from sy_myf a, sy_synal b
Where a.group_cid = $cid
  and a.hmer between $hmer_a and $hmer_e                   
  and a.flag  = $flag
  and b.synal = a.synal
[$EXTRASQL]
[$EXTRASQL2]
[$SQLCMD]
group by a.group_cid,a.flag,b.afm,b.epvnymia,b.epaggelma,b.dieyuynsh,b.polh,b.taxkvd
order by b.epvnymia
end-select
  
  IF $flag = '2'
     do tot_pwl
  ELSE
     do tot_ago
  END-IF

end-procedure

! ******************************************************************
begin-procedure tot_ago
  next-listing     need = 2
  print $L_X2                         (,01,{PAGE-X}) fill
  next-listing
  print 'Σ Υ Ν Ο Λ Ο    Α Γ Ο Ρ Ω Ν'              (,09,75)
  print #cnt_tot                                  (,+9,06) edit {MASK4}
  print #totals                                   (,+4,15) edit {MASK5}
end-procedure
! ******************************************************************
begin-procedure tot_pwl
  next-listing     need = 5
  print $L_X2                         (,01,{PAGE-X}) fill
  next-listing
  print '99999011    Πωλήσεις στο Δημόσιο κλπ'    		(,09,77)
  print #cnt_dhmosio                              (,+9,06) edit {MASK4}
  print #tot_dhmosio                              (,+2,15) edit {MASK5}
  next-listing
  print '99999999    Σ Υ Ν Ο Λ Ο    Π Ω Λ Η Σ Ε Ω Ν'          (,09,77)
  print #cnt_tot                                  (,+9,06) edit {MASK4}
  print #totals                                   (,+2,15) edit {MASK5}
end-procedure
! ******************************************************************
begin-procedure getsum($cid, $synal, $hm_apo, $hm_evs, :#timol, :#ajia)
  move 0 to #timol
  move 0 to #ajia

begin-select
sum(c.ar_tim) &tim      () NOP
sum(c.ajia)   &ajia     () NOP
  move &tim   to #timol
  move &ajia  to #ajia

from sy_myf c
where c.group_cid = $cid
  and c.synal = $synal
  and c.hmer between $hm_apo and $hm_evs
  and c.flag = 'Π'
end-select
end-procedure

! ******************************************************************

begin-procedure getdoy($synal, :$doy_perig)
  move '' to $doy_perig
begin-select
c.doy_perig            () NOP
  move &c.doy_perig to $doy_perig
from sy_synal c
where c.synal = $synal
end-select
end-procedure

! ******************************************************************

begin-procedure getepagg($epagg,:$epagg_per,:$drast,:$dras_per,$afm)
  move '' to $epagg_per
  move '' to $drast
  move '' to $drast_per
  if $epagg = ''
     move '9999' to $epagg
  end-if
begin-select
a.perigrafh     () NOP
a.drast         () NOP
b.perigrafh     () NOP
  move &a.perigrafh to $epagg_per
  move &a.drast     to $drast
  move &b.perigrafh to $dras_per
from ap_epaggelma a, ap_drast b
where a.kvdikos = $epagg and a.drast = b.kvdikos
end-select
!if $cid = '02'
   if $afm = ''
      let $drast = '0988'
   else
   if $afm = '090078291'
      let $drast = '0988'
   else
   if $afm = '090165560'
      let $drast = '0988'
   end-if
   end-if
   end-if
!end-if
end-procedure

! ******************************************************************

begin-procedure getnomos(#nomos,:$nomos_per)
  move '' to $nomos_per
begin-select
perigrafh     () NOP
  move &perigrafh to $nomos_per
from ap_nomos
where kvdikos = #nomos
end-select
end-procedure

! ******************************************************************

begin-procedure getxvra(#xvra,:$xvra_per)
  move '' to $xvra_per
begin-select
perigrafh     () NOP
  move &perigrafh to $xvra_per
from ap_xvra
where kvdikos = #xvra
end-select
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  move $title to $msg
  print $msg (3,) center
  print 'A/A'                      (5, 1,04)
  print 'Α.Φ.Μ.'                   ( ,+1,15)
  print 'Επωνυμία'                 ( ,+2,35)
  print 'Διεύθυνση'                ( ,+2,30)
  print 'Αρ.Στοιχ.'                ( ,+2,10)
  print '     Αξία'                ( ,+1,11)

end-heading
!<pcalib help>
!Αυτή η κατάσταση είναι η κατάσταση ελέγχου.
!Στις θεωρημένες καταστάσεις υπάρχει η επιλογή για
!την εξαγωγή των στοιχείων Μ.Υ.Φ. προς δισκέτα
!</pcalib help>

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{GL	1	11	5165	myf_rep	rs	X	01},TITLE{Κατάσταση ΜΥΦ}
!	PARCOUNT{3}
!	PAR1{Περίοδος	hmeromhnia	2	ΑΠΟ-ΕΩΣ	datetime		X}
!	PAR2{1=Αγορά,2=Πώληση	flag	1	ΕΠΙΛΟΓΗ	char(1)		X}
!	PAR3{Α=αυτομ.,Μ=χειρογραφ	a.sys_flag	1	=	char(1)	Χ	}
!REPORTHELP{T},RNHELP{το Α και το Μ θα πρέπει να είναι στα Αγγλικά},
!</pcalib>

