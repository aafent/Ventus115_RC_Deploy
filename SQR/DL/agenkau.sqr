!***********************************************************************
!***** REPORT      : agenkau.sqr                                    ****
!***** DATE        : 04/03/96                                       ****
!***** APPLICATION : Αναλυτικό Δημόσιο Καθολικό                     ****
!***** NOTE        : --- ΘΕΩΡΗΜΕΝΟ ΑΝΑΛΥΤΙΚΟ ΚΑΘΟΛΙΚΟ   ---         ****
!***********************************************************************
 
#DEFINE   PAGE-Y    58
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  6
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1     999,999,999.99c
#DEFINE   MASK2     999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $xrhsh     'Χρήση           : ' 
 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '
 input $kathgoria 'Κατηγορία       : '

 unstring $xrhsh by '"' into $filler $xrhsh $filler
 unstring $kathgoria by '"' into $filler $kathgoria $filler
 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output
 move $xrhsh to #xrhsh

  do main

End-Report

! ****************************************************************************

begin-procedure main
execute app_getxrhsh @FE = null,
			@xrhsh_arxh=$xrhsh_arxh output, 
			@xrhsh_telos=$xrhsh_telos output

	show '1'
move '0' to $memka
begin-select
convert(char(10),convert(datetime,hmeromhnia),103) &hmeromhnia  () NOP
a.ka                                                            () NOP 
a.apofash                                                       () NOP
a.poso_proyp                                                    () NOP
a.poso_desm                                                     () NOP
a.poso_prok                                                     () NOP
a.poso_ejof                                                     () NOP
a.sxolia                                                        () NOP
a.ar_entalma                                                    () NOP
a.apofash_anaf                                                  () NOP
b.perigrafh2                                                    () NOP

		if $memka = '0' 
			move &a.ka to $memka
			let $sxo = &a.ka || '  ' || &b.perigrafh2
			print   $sxo    (+1,70)  center
			next-listing
			next-listing
			move 'ΕΚ ΜΕΤΑΦΟΡΑΣ ' to $sxolio
			do ekmetaforas
			do synola
		end-if
		if $memka != &a.ka
			do synola
			move 0 to #suma_proyp
			move 0 to #suma_desm
			move 0 to #suma_prok
			move 0 to #suma_ejof
			move &a.ka to $memka
			new-page
			let $sxo = &a.ka ||'   '||&b.perigrafh2
			print   $sxo    (+1)  center
			next-listing
			move 'ΕΚ ΜΕΤΑΦΟΡΑΣ ' to $sxolio
			do ekmetaforas
			do synola
		end-if



		do ektyp

from dl_kakin a, dl_ka b
where   xrhsh=#xrhsh 
and     hmeromhnia between $apohm and $evshm
and     a.kathgoria=$kathgoria
and     a.kathgoria=b.kathgoria 
and     a.ka=b.ka 
!and     a.ka='0211'
and     b.epipedo=9 
[$SQLCMD]
order by a.ka, a.hmeromhnia
end-select
		do synola

end-procedure

begin-procedure ekmetaforas
begin-select
sum(poso_proyp)    &metaf_proyp,
sum(poso_desm)     &metaf_desm,
sum(poso_prok)     &metaf_prok,
sum(poso_ejof)     &metaf_ejof

		move &metaf_proyp to #sum_proyp
		move &metaf_desm to #sum_desm
		move &metaf_prok to #sum_prok
		move &metaf_ejof to #sum_ejof

from dl_kakin 
where hmeromhnia>= convert(datetime,$xrhsh_arxh,103) 
and   hmeromhnia<  $apohm and ka=&a.ka and xrhsh=#xrhsh

end-select
end-procedure


begin-procedure ektyp
print &hmeromhnia                       (+1,1,10)
print &a.sxolia                         (,+1,25)
print &a.poso_proyp                     (,+1,14)  edit {MASK1}
print &a.poso_desm                      (,+1,14)  edit {MASK1}
print &a.poso_prok                      (,+1,14)  edit {MASK1}
print &a.poso_ejof                      (,+1,14)  edit {MASK1}
print &a.ar_entalma                     (,+4,8)
print &a.apofash                        (,+1,8)
print &a.apofash_anaf                   (,+1,8)
add &a.poso_proyp to #suma_proyp
add &a.poso_desm to #suma_desm
add &a.poso_prok to #suma_prok
add &a.poso_ejof to #suma_ejof
end-procedure


begin-procedure synola
let #ele=#suma_proyp+#suma_desm+#suma_prok+#suma_ejof
if #ele > 0
   print $sxolio                        (+2,21,16)
   print #suma_proyp            (,+1,14)  edit {MASK1}
   print #suma_desm                (,+1,14)  edit {MASK1}
   print #suma_prok                (,+1,14)  edit {MASK1}
   print #suma_ejof                (,+1,14)  edit {MASK1}
   let #diaf = #suma_proyp - #suma_desm
   print '-'                    (+1,1,131) fill
   print 'ΥΠΟΛΟΙΠΟ ΠΡΟΣ ΔΙΑΘΕΣΗ'   (+1,21,31)
   print #diaf                  (,+1,14)  edit {MASK1}
   print '-'                    (+1,1,131) fill
end-if
   move 'ΣΥΝΟΛΑ' to $sxolio
   end-procedure
! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΑΝΑΛΥΤΙΚΟ ΚΑΘΟΛΙΚΟ ΔΗΜΟΣΙΑΣ ΛΟΓΙΣΤΙΚΗΣ' (2,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

      print 'ΗΜΕΡΟΜΗΝΙΑ'                (5,1,10) 
      print '        ΣΧΟΛΙΑ'            (,+1,25)
      print 'ΠΡΟΥΠΟΛΟΓΙΣΜΟΣ'            (,+1,14)
      print '      ΔΕΣΜΕΥΣΗ'            (,+1,14) 
      print '   ΠΡΟΚΑΤΑΒΟΛΗ'            (,+1,14) 
      print '      EΞΟΦΛΗΣΗ'            (,+1,14)
      print 'ΕΝΤΑΛΜΑ'                   (,+4,8)
      print 'ΑΠΟΦΑΣΗ'                   (,+1,8) 
      print 'ΤΡΟΠ.ΑΠ.'                  (,+1,8) 

End-Heading
!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{DL	1	2	487	agenkau	rs	X	01},TITLE{Δημόσιο Αναλυτικό Καθολικό}
!	PARCOUNT{4}
!	PAR1{Χρήση	dump	1	=	int		X}
!	PAR2{Ημερομηνία	dump	2	ΑΠΟ-ΕΩΣ	datetime	X	X}
!	PAR3{Κατηγορία	dump	1	=	char(1)		X}
!	PAR4{Κωδικός Από - Εως	a.ka	2	BETWEEN	varchar(5)	X	}
!REPORTHELP{T},RNHELP{Κατηγορία 1=Εξοδα 2=Εσοδα},
!</pcalib>
