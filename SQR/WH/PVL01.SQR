! ******************************************************************************
! **** COMPANY     : PROLOGIC SA - SOFLOLENS SA                             ****
! **** REPORT      : pvl03.sqr                                              ****
! **** DATE        : 05/06/95                                               ****
! **** APPLICATION : ΑΠΟΘΗΚΕΣ- ΠΩΛΗΣΕΙΣ                                     ****
! **** AUTHOR      : ΜΠΟΚΙΑΣ ΝΙΚΟΣ                                          ****
! **** NOTE        : ΠΩΛΗΣΕΙΣ ΕΙΔΩΝ ΠΕΡΙΟΔΟΥ ΚΑΙ ΠΡΟΗΓΟΥΜΕΝΗΣ               ****
! ******************************************************************************

#DEFINE   PAGE-Y     62
#DEFINE   PAGE-X    132 
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   MASK1        999,999,999
#DEFINE   MASK2              9,999
#DEFINE   MASK3         99,999,999
#DEFINE   MASK4             99,999.99

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
 
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Από Ημερομηνία       : '
 input $evshmnia  'Eως Ημερομηνία       : '

 display $apohmnia
 display $evshmnia

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output
 execute app_geteom    $evshm,    $evshm output

 do main_all($SQLCMD, $apohm, $evshm)

End-Report

! ******************************************************************************

begin-procedure main_all ($SQLCMD, $apohm, $evshm)

  let      #tot_pos = 0
  let #prev_tot_pos = 0
  let      #tot_aji = 0
  let #prev_tot_aji = 0

begin-select
a.eidos                                      &eidos    (, 1,15)
b.perigrafh                                            (,+1,40)
b.ord                                                  () NOP

  do find_prev(&eidos,$apohm,$evshm,#pvl_prev_pos,#pvl_prev_aji)
 
  print #pvl_prev_pos                       (,+1,10) edit {MASK3}
  print #pvl_prev_aji                       (,+1,11) edit {MASK1}
  print '|'                                 (,+1, 1)

  let #pos     = 0

  do get_pos (&eidos,$apohm,$evshm,#pvl_pos,#pvl_aji)

  if  #pvl_prev_pos > 0
    let #dia = ((#pvl_pos - #pvl_prev_pos) * 100) / #pvl_prev_pos
  else 
    let #dia = 0
  end-if
  print #pvl_pos                       (,+1,10) edit {MASK3}
  print #dia                           (,+1, 9) edit {MASK4}
  if  #pvl_prev_aji > 0
    let #dia = ((#pvl_aji - #pvl_prev_aji) * 100) / #pvl_prev_aji
  else 
    let #dia = 0
  end-if

  print #pvl_aji                       (,+1,11) edit {MASK1}
  print #dia                           (,+1, 9) edit {MASK4}

  let      #pvl_tot_pos =      #pvl_tot_pos +      #pvl_pos
  let #pvl_prev_tot_pos = #pvl_prev_tot_pos + #pvl_prev_pos
  let      #pvl_tot_aji =      #pvl_tot_aji +      #pvl_aji
  let #pvl_prev_tot_aji = #pvl_prev_tot_aji + #pvl_prev_aji

  next-listing

from   wh_kint1 a,wh_eidh b, wh_pvlomades x
where  a.eidos      =       b.kvdikos
and    b.pvlomada   =       x.pvlomada
and    a.hmeromhnia between $apohm and $evshm
[$SQLCMD]
group  by a.eidos, b.perigrafh,b.ord
order by b.ord
end-select

  print '=' (,1,{PAGE-X}) fill
  next-listing
  print ' '           (, 1,15)
  print 'ΣΥΝΟΛΟ : '   (,+1,40)
  print #pvl_prev_tot_pos (,+1,10) edit {MASK3}
  print #pvl_prev_tot_aji (,+1,11) edit {MASK1}
  print '|'           (,+1, 1)
  print      #pvl_tot_pos (,+1,10) edit {MASK3}

  if  #pvl_prev_tot_pos > 0
    let #pvl_tot_dia=((#pvl_tot_pos-#pvl_prev_tot_pos)*100)/#pvl_prev_tot_pos
  else 
    let #pvl_tot_dia = 0
  end-if
  print #pvl_tot_dia (,+1, 9) edit {MASK4}
  print #pvl_tot_aji (,+1,11) edit {MASK1}

  if  #pvl_prev_tot_aji > 0
    let #pvl_tot_dia=((#pvl_tot_aji-#pvl_prev_tot_aji)*100)/#pvl_prev_tot_aji
  else 
    let #pvl_tot_dia = 0
  end-if
  print #pvl_tot_dia                           (,+1, 9) edit {MASK4}

end-procedure

! ******************************************************************************

begin-procedure get_pos ($eidos,$apohm,$evshm,:#pos,:#aji)
 
  let #pos = 0
  let #aji = 0

begin-select
sum(isnull(a.pvl_p,0)) &pos () NOP
sum(isnull(a.pvl_a,0)) &aji () NOP
 
  let #pos = &pos
  let #aji = &aji

from  wh_kint1 a
where a.eidos = $eidos
and   a.hmeromhnia between $apohm and $evshm
end-select

end-procedure

! ------------------------------------------------------------------------------

begin-procedure find_prev ($eidos,$apohm,$evshm,:#prev_pos,:#prev_aji)

begin-select

dateadd(yy,-1,$apohm) &prev_apohm () NOP
dateadd(yy,-1,$evshm) &prev_evshm () NOP

end-select

  do get_pos ($eidos,&prev_apohm,&prev_evshm,#pos,#aji)

  let #prev_pos = #pos
  let #prev_aji = #aji

  
end-procedure

! ******************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

   let $msg2 = ' / ΓΙΑ ΟΛΟΥΣ ΤΟΥΣ ΑΝΤΙΠΡΟΣΩΠΟΥΣ - ΠΩΛΗΤΕΣ '

 let   $msg  = 'ΣΤΑΤΙΣΤΙΚΑ ΚΙΝΗΣΕΩΝ ΕΙΔΩΝ' || $msg2
 print $msg (2,+1) center

 let   $msg='ΑΠΟ : ' || $apohmpr || ' ΕΩΣ : ' || $evshmpr

 print $msg (3,+1) center

   print ' '                                          (5, 1,15)
   print ' '                                           (,+1,40)
   print '| Περίοδος Προηγ.Ετους |'                    (,+1,  )
   print '|--------- Περίοδος Τρέχων Ετους --------|'  (,+1,  )

   print 'Κωδικός'                                    (6, 1,15)
   print 'Περιγραφη'                                   (,+1,40)
   print '  Ποσοτητα'                                  (,+1,10)
   print '       Αξία'                                 (,+1,11)
   print ' '                                           (,+1, 1)
   print '  Ποσοτητα'                                  (,+1,10)
   print 'Ποσοστο %'                                   (,+1, 9)
   print '       Αξία'                                 (,+1,11)
   print 'Ποσοστο %'                                   (,+1, 9)

end-Heading
