! ***********************************************************************
! ***** PROLOGIC S.A.                                                ****
! ***** REPORT      : whprotpa.sqr                                   ****
! ***** DATE        : 14/09/94                                       ****
! ***** APPLICATION : warehouse                                      ****
! ***** AUTHOR      : ΘΕΟΤΟΚΑΣ ΙΩΑΝΝΗΣ                               ****
! ***** NOTE        : Πρόταση προς Παραγγελία                        ****
! ***********************************************************************

#DEFINE   PAGE-Y     62
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   EIDOS           99999
#DEFINE   MASK1        9999,999.99
#DEFINE   MASK2     999,999,999
#DEFINE   DATEMASK1 MM/YY
#DEFINE   DATEMASK2 YY   

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 do getdates

 display $hmer
 display $hmer6
 display $hmer7
 display $hmer12
 display $hmer13
 display $hmer18
 display $lystart
 display $lyend

 do main

End-Report

! *************************************************************************

begin-procedure main
begin-select 
a.kvdikos            () NOP
a.perigrafh          () NOP

   move &a.kvdikos to $eidos

   move '1' to $apou_auhn
   move '2' to $apou_thes
 
   do getypol($cid,$eidos,$apou_auhn,#ypol1,#anam1,#desm1)
   do getypol($cid,$eidos,$apou_thes,#ypol2,#anam2,#desm2)

   let #eley1 = #ypol1 - #desm1
   let #eley2 = #ypol2 - #desm2
   let #eley9 = #eley1 + #eley2
   let #anam9 = #anam1 + #anam2

   move ''   to $apou
   move '1%' to $apou_auhn
   move '2%' to $apou_thes

   do getpvl($cid, $eidos, $apou, $hmer6,  $hmer,   #pvlhseis1)
   do getpvl($cid, $eidos, $apou, $hmer12, $hmer7,  #pvlhseis2)
   do getpvl($cid, $eidos, $apou, $hmer18, $hmer13, #pvlhseis3)
   do getpvl($cid, $eidos, $apou, $tystart, $hmer, #pvlyear)
   do getpvl($cid, $eidos, $apou_auhn, $lystart, $lyend, #pvlath_1)
   do getpvl($cid, $eidos, $apou_thes, $lystart, $lyend, #pvlthes_1)

   ! do getpvl($cid, $eidos, $apou_thes, $hmer6,  $hmer,   #pvlhseis21)
   ! do getpvl($cid, $eidos, $apou_thes, $hmer12, $hmer6,  #pvlhseis22)
   ! do getpvl($cid, $eidos, $apou_thes, $hmer18, $hmer12, #pvlhseis23)

   ! let #pvlhseis91 = #pvlhseis11 + #pvlhseis21
   ! let #pvlhseis92 = #pvlhseis12 + #pvlhseis22
   ! let #pvlhseis93 = #pvlhseis13 + #pvlhseis23

   if #pvlhseis3 != 0 
      let #synt = #pvlhseis1 / #pvlhseis3   
   else
      let #synt = 1 
   end-if


   let #problech = #pvlhseis2 * #synt

   let #stock = #eley9 + #anam9    
   let #protash = (#stock - #problech ) * -1

   if #protash > 0    
      move 'T' to $print_flag
   else
      move 'F' to $print_flag
   end-if

   if $print_flag='T'

      print $eidos         (,01,05)
      let #col_per = #current-column + 1
      print &a.perigrafh   (,+1,25)
    
      let #pvl_1 = #pvlath_1 + #pvlthes_1

      print #eley9        (,+1,11) edit {MASK2}
      print #eley1        (,+1,11) edit {MASK2}
      print #eley2        (,+1,11) edit {MASK2}
      print #anam9        (,+1,11) edit {MASK2}
      print #pvlath_1     (,+1,11) edit {MASK2}
      print #pvlthes_1    (,+1,11) edit {MASK2}
      print #pvl_1        (,+1,11) edit {MASK2}
      print #pvlyear      (,+5,11) edit {MASK2}

      next-listing 
      next-listing 

   end-if
 
from wh_eidh a
where 1=1
[$SQLCMD]
order by
[$SQLORD]
end-select

end-procedure

! ******************************************************************

begin-procedure getdates
begin-select 

dateadd(mm,  -1, getdate())    &hmer
  move &hmer   to $hmer
dateadd(mm,  -6, getdate())    &hmer6
  move &hmer6  to $hmer6
dateadd(mm,  -7, getdate())    &hmer7
  move &hmer7  to $hmer7
dateadd(mm, -12, getdate())    &hmer12
  move &hmer12 to $hmer12
dateadd(mm, -13, getdate())    &hmer13
  move &hmer13 to $hmer13
dateadd(mm, -18, getdate())    &hmer18
  move &hmer18 to $hmer18
convert(datetime,'1 Jan'+convert(char(4),datepart(yy,getdate())),3) &tystart
  move &tystart to $tystart
convert(datetime,'1 Jan'+convert(char(4),datepart(yy,getdate())-1),3) &lystart
  move &lystart to $lystart
convert(datetime,'31 Dec'+convert(char(4),datepart(yy,getdate())-1),3) &lyend
  move &lyend  to $lyend

end-select
end-procedure

! ******************************************************************

begin-procedure getypol($cid,$eidos,$apou,:#ypol,:#anam,:#desm)
  move 0 to #ypol
  move 0 to #anam
  move 0 to #desm
begin-select
ypoloipo                        () NOP
  move &ypoloipo    to #ypol
desmeysh                        () NOP
  move &desmeysh    to #desm
anamen                          () NOP
  move &anamen      to #anam
from wh_kint2
where cid = $cid 
  and eidos = $eidos
  and apouhkh = $apou
end-select
end-procedure

! ******************************************************************

begin-procedure getpvl($cid, $eidos, $apou, $hmerapo, $hmerevs, :#pvlhseis)

 move 0 to #pvlhseis
 execute stp_pvlhseis @cid = $cid, @eidos=$eidos, @apouhkh = $apou,
                      @hmerapo = $hmerapo, @hmerevs = $hmerevs,
                      @pwlposot = #pvlhseis out

end-procedure

! ******************************************************************


begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  let $msg='Π Ρ Ο Τ Α Σ Η   ΠΡΟΣ   Π Α Ρ Α Γ Γ Ε Λ Ι Α'    
  print $msg (3,) center
  print ' '                                        (5,01,05)
  print 'Ε  ί  δ  ο  ς'                             (,+1,25)
  print '|----------- Ελεύθερα -----------|'        (,+2,35)
  print 'Αναμενόμενη'                               (,  ,11)
  print '|-------------------- Πωλήσεις --------------------|'  (,+1,53)

  print ' '                                        (6,01,31)
  print ' | Σύνολο'                                 (,+1,11)
  print '   Αθήνα'                                  (,+1,11)
  print ' Θεσσ/νίκη|'                               (,+1,11)
  print '   Ποσότητα '                              (,  ,12)
  print '| Αθήνας ['                                (,+1,10)
  print $lystart                                    (,  ,02) edit {DATEMASK2}
  print ']'                                         (,  ,01)
  print 'Θεσ/κης ['                                 (,+1,09)
  print $lystart                                    (,  ,02) edit {DATEMASK2}
  print ']'                                         (,  ,01)
  print ' Σύνολο ['                                 (,+1,09)
  print $lystart                                    (,  ,02) edit {DATEMASK2}
  print ']'                                         (,  ,01)
  print $tystart                                    (,+1,05) edit {DATEMASK1}
  print '-'                                         (,  ,01)
  print $hmer                                       (,  ,05) edit {DATEMASK1}
  print '|'                                         (,  ,01)

end-heading
