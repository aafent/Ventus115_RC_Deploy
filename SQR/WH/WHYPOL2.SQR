!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : whypol2.sqr                                    ****
!***** DATE        : 09/06/93                                       ****
!***** APPLICATION : warehouse                                      ****
!***** AUTHOR      : ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ                              ****
!***** NOTE        : Υπολοιπα ειδών κατα αποθήκη ελευθερα & δεσ/να  ****
!***********************************************************************

#DEFINE   PAGE-Y    59
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   EIDOS     99999
#DEFINE   MASK1     999,999c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid
 input $ihm_evs 'evs hmeromhnia '
 display $ihhm_evs

 execute app_datecnv   $ihm_evs, $evshm output, $evshmpr output
 
 if $cid = 'M'
    move '1'  to  $apo_a
    move '2'  to  $apo_u
    move '4'  to  $apo_l
  end-if

 if $cid = 'O'
    move '3'  to  $apo_a
    move '--'  to  $apo_u
    move '5'  to  $apo_l
  end-if

 do main
End-Report

! *************************************************************************

begin-procedure main
begin-select 
a.kvdikos            () NOP
a.perigrafh          () NOP

   move &a.kvdikos to #eidos
   do getypol($cid,#eidos,$apo_a,'F',#ypol_a,#ypol_ae,#ypol_ad)
   do getypol($cid,#eidos,$apo_u,'F',#ypol_u,#ypol_ue,#ypol_ud)
   do getypol($cid,#eidos,$apo_l,'F',#ypol_l,#ypol_le,#ypol_ld)


   let #ypol_apo  = #ypol_a  + #ypol_u  + #ypol_l
   let #ypol_des  = #ypol_ad + #ypol_ud + #ypol_ld
   let #ypol_ele  = #ypol_ae + #ypol_ue + #ypol_le

   if #ypol_apo = 0 
      move 'F' to $print_flag
   else
      move 'T' to $print_flag
   end-if

   if $print_flag='T'

       print #eidos         (,01,05) edit {EIDOS}
       print &a.perigrafh   (,+1,25)
   
       let #col_1 = #current-column+1
       print $L_X3            (,+1,01)

       print #ypol_apo        (,+1,07) edit {MASK1}
       print #ypol_ele        (,+1,07) edit {MASK1}
       print #ypol_des        (,+1,07) edit {MASK1}

       let #col_2 = #current-column+1
       print $L_X3            (,+1,01)

       print #ypol_a          (,+1,07) edit {MASK1}
       print #ypol_ae         (,+1,07) edit {MASK1}
       print #ypol_ad         (,+1,07) edit {MASK1}

       let #col_3 = #current-column+1
       print $L_X3            (,+1,01)

       print #ypol_u          (,+1,07) edit {MASK1}
       print #ypol_ue         (,+1,07) edit {MASK1}
       print #ypol_ud         (,+1,07) edit {MASK1}

       let #col_4 = #current-column+1
       print $L_X3            (,+1,01)

       print #ypol_l          (,+1,07) edit {MASK1}
       let #col_5 = #current-column+1
       print $L_X3            (,+1,01)

       ! print #ypol_le         (,+1,07) edit {MASK1}
       ! print #ypol_ld         (,+1,07) edit {MASK1}

       next-listing
       print $L_X3            (,#col_1,01)
       print $L_X3            (,#col_2,01)
       print $L_X3            (,#col_3,01)
       print $L_X3            (,#col_4,01)
       print $L_X3            (,#col_5,01)
       next-listing need=2

   end-if

from wh_eidh a
where 1=1
[$SQLCMD]
[$SQLORD]
end-select

end-procedure

! ******************************************************************

begin-procedure getypol($cid,#eidos,$apou,$flag,:#ypol,:#ypol_e,:#ypol_d)
  move 0 to #ypol
  move 0 to #ypol_e
  move 0 to #ypol_d
  if $apou <> '' and
     $apou <> '--'
     do getypol_doit($cid,#eidos,$apou,$flag,#ypol,#ypol_e,#ypol_d)
  end-if
end-procedure

begin-procedure getypol_doit($cid,#eidos,$apou,$flag,:#ypol,:#ypol_e,:#ypol_d)

 execute whp_getypol @eidos=#eidos, @apouhkh=$apou, @cid=$cid,
                     @fysikh=$flag,   
                     @pra_ypol = #ypol   out,
                     @dia_ypol = #ypol_e out

 let #ypol_d = #ypol - #ypol_e

 ! @eidos          kvdeid_type          4           1 
 ! @apouhkh        apouhkh_type         4           2 
 ! @cid            cid_type             1           3 
 ! @fysikh         char                 1           4 
 ! @pra_ypol       float                8           5 
 ! @ana_ypol       float                8           6 
 ! @dia_ypol       float                8           7 

end-procedure

! ******************************************************************


begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  let $msg='ΥΠΟΛΟΙΠΑ ΑΠΟΘΗΚΩΝ ΕΛΕΥΘΕΡΑ & ΔΕΣΜΕΥΜΕΝΑ ('||$evshmpr||')'
  print $msg (3,) center

  print $L_X3                                      (5,33,01)
  print '-- Σ  Υ  Ν   Ο  Λ  Α --'                  ( ,+1,23)
  print $L_X3                                      ( ,+1,01)
  print '--- Α   Θ   Η  Ν  Α ---'                  ( ,+1,23)
  print $L_X3                                      ( ,+1,01)
  print '-Θ Ε Σ Σ Α Λ Ο Ν Ι Κ Η-'                  ( ,+1,23)
  print $L_X3                                      ( ,+1,01)
  print '-ΛΟΙΠΑ-'                                  ( ,+1,07)
  print $L_X3                                      ( ,+1,01)


  print ' '                                        (6,01,05)
  print 'Ε  ί  δ  ο  ς'                             (,+1,25)
  print $L_X3                                      ( ,+1,01)

  print 'Υπόλ/πο'                                   (,+1,07)
  print 'Ελ/θερο'                                   (,+1,07)
  print 'Δεσμ/να'                                   (,+1,07)
  print $L_X3                                      ( ,+1,01)

  print 'Υπόλ/πο'                                   (,+1,07)
  print 'Ελ/θερο'                                   (,+1,07)
  print 'Δεσμ/να'                                   (,+1,07)
  print $L_X3                                      ( ,+1,01)

  print 'Υπόλ/πο'                                   (,+1,07)
  print 'Ελ/θερο'                                   (,+1,07)
  print 'Δεσμ/να'                                   (,+1,07)
  print $L_X3                                      ( ,+1,01)

  print 'Υπόλ/πο'                                   (,+1,07)
  print $L_X3                                      ( ,+1,01)

  

end-heading
