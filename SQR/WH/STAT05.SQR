! ******************************************************************************
! **** COMPANIES   : PROLOGIC SA - SOFLOLENNS SA                            ****
! **** REPORT      : stat05.sqr                                             ****
! **** DATE        :  5/ 5/95                                               ****
! **** APPLICATION : ΑΠΟΘΗΚΕΣ                                               ****
! **** AUTHOR      : ΜΠΟΚΙΑΣ ΝΙΚΟΣ                                          ****
! **** NOTE        : ΣΤΟΙΧΕΙΑ ΠΩΛΗΣΕΩΝ ΑΝΑ ΜΗΝΑ                             ****
! ******************************************************************************

#DEFINE   PAGE-Y                62
#DEFINE   PAGE-X               232 
#DEFINE   HEAD-SIZE              8
#DEFINE   FOOT-SIZE              1
#DEFINE   HEAD-LINE              5

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   MASK2                999

#DEFINE   MASK10         9,999,999
#DEFINE   MASK11        99,999,999
#DEFINE   MASK12       999,999,999
#DEFINE   MASK13     9,999,999,999
#DEFINE   MASK14    99,999,999,999
#DEFINE   MASK15               999.9
#DEFINE   MASK20                99

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
 
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Από Ημερομηνία                 : '
 input $armhnvn   'Αριθμος Περιοδων               : '
 input $ariumos   'Αριθμος Μηνων για καθε περιοδο : '
 input $pvlhths   'Πωλητης-Αντιπροσωπος/*         : '
 input $synal     'Συναλλασόμενος/*               : '
 input $tmhma     'Τμήμα/*                        : '
 input $eidos     'Ειδος/*                        : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output

 unstring $armhnvn by '"' into $filler1 $mhnes  $filler1
 unstring $pvlhths by '"' into $filler1 $pvl    $filler1
 unstring $tmhma   by '"' into $filler1 $tmhm   $filler1
 unstring $ariumos by '"' into $filler1 $ar_mhn $filler1

   move $mhnes                to #months
   move $ar_mhn               to #ar_mhnvn

 if $tmhm != '*'
    let $SQLSEIRA = 'and a.tmhma = ' || $tmhma   
 else
    let $SQLSEIRA = ' ' 
 end-if

 if $pvl != '*'
    let $SQLPVLHTHS = 'and c.pvlhths = ' || $pvlhths    
 else
    let $SQLPVLHTHS = ' ' 
 end-if

 do main($SQLCMD,$apohm,$SQLORDERCMD,#months,$SQLSEIRA,$SQLPVLHTHS,#ar_mhnvn,$seir,$pvl)

End-Report

! ****************************************************************************

begin-procedure main($SQLCMD,$apohm,$SQLORDERCMD,#months,$SQLSEIRA,$SQLPVLHTHS,#ar_mhnvn,$seir,$pvl)

  move 0 to #pvl

  let $evshm = ' '
  do findhm($apohm,#months,#ar_mhnvn,$evshm)

begin-select
  print ' '   (, 1, 8) fill
  print ' '   (,+1,30) fill
c.kvdikos     (, 1, 8)
c.epvnymia    (,+1,30)

  let #no    = 1
  let $hm    = $apohm
  let $newhm = ' '

  let #has         = 0
  let #pro_kat_xre = 0
  let #pro_kat_pis = 0

  do findprehm($apohm,$prehmevs)
  do printmonth(&c.kvdikos,'1 jan 1992',$prehmevs,#kat_xre,#kat_pis,$seir,$pvl,#has)

  let #pro_kat_xre = #pro_kat_xre + #kat_xre
  let #pro_kat_pis = #pro_kat_pis + #kat_pis

  do printpercent(#pro_kat_xre,#pro_kat_pis)

  while #no <= #months

    do findhm    ($hm,1,#ar_mhnvn,$newhm)
    do printmonth(&c.kvdikos,$hm,$newhm,#kat_xre,#kat_pis,$seir,$pvl,#has)

    let #pro_kat_xre = #pro_kat_xre + #kat_xre
    let #pro_kat_pis = #pro_kat_pis + #kat_pis

    do printpercent(#pro_kat_xre,#pro_kat_pis)

    let $hm = $newhm
    let #no = #no + 1

  end-while

  if  #has = 1

    next-listing

  end-if
      
from cu_kat c
where 1=1
[$SQLPVLHTHS]
[$SQLCMD]
[$SQLORDERCMD]
end-select
  
  if #has = 1
    next-listing
  end-if
  print '-'                              (,1,232) fill
  next-listing
  print 'ΓΕΝΙΚΑ ΣΥΝΟΛΑ               : '   (,12,)

!  =-=-=-  print #tot_pvl        (,+1,14)  edit {MASK14}

end-procedure

! ****************************************************************************

begin-procedure findprehm($hm,:$prehm)

begin-select
dateadd(dd,-1,$hm) &newhm () NOP
  let $prehm = &newhm
end-select

end-procedure

! ------------------------------------------------------------------------------

begin-procedure findhm($hm,#mhn,#ar_mhnvnv,:$newhm)

  let #m = #mhn * #ar_mhnvnv

begin-select
dateadd(mm,#m,$hm) &newhm () NOP
  let $newhm = &newhm
end-select

end-procedure

! ------------------------------------------------------------------------------

begin-procedure printpercent(#pro_kat_xre,#pro_kat_pis)

  let #ypp =  #pro_kat_xre - #pro_kat_pis
  let #yp  = (#pro_kat_xre - #pro_kat_pis) + #pro_kat_xre
  if #yp  != 0
    let #pc = (#pro_kat_pis * 100) / #yp
  else 
    let #pc = 0
  end-if

  print #ypp (,+1,9) edit {MASK10}
  print #pc  (,+1,5) edit {MASK15}

end-procedure

! ------------------------------------------------------------------------------

begin-procedure printmonth($kat,$apohm,$evshm,:#kat_xre,:#kat_pis,$seir,$pvl,:#has)

 if $seir <> '*'
  execute cup_getypol @pel=$kat,
                      @hm_apo=$apohm,
                      @hm_evs=$evshm,
                      @pk_flag='KS',
                      @tmhma=$seir,
                      @xrevsh  =#kat_xre output,
                      @pistvsh =#kat_pis output 

 else
  execute cup_getypol @pel=$kat,
                      @hm_apo=$apohm,
                      @hm_evs=$evshm,
                      @pk_flag='K',
                      @tmhma=$seir,
                      @xrevsh  =#kat_xre output,
                      @pistvsh =#kat_pis output 

 end-if

  let #kat_pvl = #kat_xre - #kat_pis
 
  if (#kat_xre != 0
  or  #kat_pis != 0)
      let #has  = 1
  end-if

  print #kat_xre (,+1,10) edit {MASK11}
  print #kat_pis (,+1,10) edit {MASK11}

end-procedure

! ------------------------------------------------------------------------------

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΠΩΛΗΣΕΙΣ ΚΑΤΑΣΤΗΜΑΤΩΝ ANA ΠΕΡΙΟΔΟΥΣ' (2,+1) center

 let $msg = 'ΑΠΟ : ' || $apohmpr || ' ... ΚΑΙ ΓΙΑ : ' || $mhnes || ' ΠΕΡΙΟΔΟΥΣ ... ΤΩΝ : ' || $ar_mhn || ' ΜΗΝΩΝ'
 print $msg (3,+1) center

 if  $seir = '*'
     let $s = 'ΟΛΕΣ ΤΙΣ ΣΕΙΡΕΣ'
 else 
     let $s = 'ΤΗΝ ΣΕΙΡΑ : ' || $seir
 end-if
 if  $pvl = '*'
     let $p = 'ΟΛΟΥΣ ΤΟΥΣ ΠΩΛΗΤΕΣ - ΑΝΤΙΠΡΟΣΩΠΟΥΣ'
 else 
     let $p = 'ΤΟΝ ΠΩΛΗΤΗ - ΑΝΤΙΠΡΟΣΩΠΟ : ' || $pvl
 end-if
 let $msg2 = 'ΓΙΑ ' || $s || ' ... ΚΑΙ ΓΙΑ ' || $p
 print $msg2 (4,+1) center

   print ' '                                   (6,1, 8) fill
   print ' '                                   (,+1,30) fill
   print '-------'                             (,+1,  )
   print ' Προηγουμενη Περιοδος '              (,+1,  )
   print '-------'                             (,+1,  )

   let #n = 1
   while #n < (#months + 1)
     print '-----------'                       (,+1,  )
     print #n                                  (,+1, 2) edit {MASK20}
     print 'η ΠΕΡΙΟΔΟΣ'                        (,+0, 7)
     print '------------'                      (,+1,  )
     let #n = #n + 1
   end-while

   print 'Κωδικός'                             (7,1, 8)
   print 'Επωνυμία'                            (,+1,30)

   print '    Χρεωση'                          (,+1,10)
   print '   Πιστωση'                          (,+1,10)
   print '  Υπολοιπο'                          (,+1, 9)
   print ' Εξ.%'                               (,+1, 5)

   let #n = 1
   while #n < (#months + 1)
     print '    Χρεωση'                        (,+1,10)
     print '   Πιστωση'                        (,+1,10)
     print '  Υπολοιπο'                        (,+1, 9)
     print ' Εξ.%'                             (,+1, 5)
     let #n = #n + 1
   end-while

end-Heading
