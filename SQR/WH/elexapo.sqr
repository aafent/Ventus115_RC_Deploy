!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : elexapogr.sqr                                  ****
!***** DATE        : 09/01/95                                       ****
!***** APPLICATION : warehouse                                      ****
!***** AUTHOR      : ΘΕΟΤΟΚΑΣ ΙΩΑΝΝΗΣ                               ****
!***** NOTE        : Κατάσταση Ελέγχου Καταχώρησης Απογραφής        ****
!***********************************************************************

#DEFINE   PAGE-Y    64
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  6
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   MASK1     999,999,999.99c
#DEFINE   MASK2     9,999,999.999c
#DEFINE   MASK3     999,999,999.99c
#DEFINE   DATEMASK  DD/MM/YYYY

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

  input $ixrhsh  'Xrhsh :'     
  execute app_floatcnv $ixrhsh, #xrhsh out

  input $byfpa  'ByFPA (N/O) :'
  unstring $byfpa by '"' into $filler $byfpa $filler
  
  display $byfpa     

  ! move $L_XRHSH to #xrhsh
 
 move 'F' to $new_page_flag
 move 'T' to $flag
 do main

End-Report

! *************************************************************************

begin-procedure main

begin-select -C1
xr.xrhsh_arxh        () NOP
xr.xrhsh_telos       () NOP
from ap_xrhsh xr
where xr.cid = $cid
  and xr.xrhsh = #xrhsh
end-select

begin-select -C1
  if $new_page_flag = 'T'
     do print_metafora
  end-if
a.apouhkh            ()               on-break print = never
                                               level = 1
                                               after = totapoth
  
  if $flag = 'T'
     do newapoth
  end-if
b.kfpa               ()               on-break print = never
                                               level = 2
                                               after = subtot
a.eidos              (,1,10)
b.perigrafh          (,+1,34)        
b.monada             (,+1,03)

  if &b.kfpa = 3
     move   '  -'  to $fpa_pr
  else
    if &b.kfpa = 2
       move   ' 9%'  to $fpa_pr
     else
       move   '19%'  to $fpa_pr
    end-if
  end-if
  print  $fpa_pr     (,+9,03)

a.posothta           (,+9,14)  edit {MASK1}
a.msk       &a.msk   () NOP                  
a.kostos             () NOP                  

  let #kostos = &a.kostos
  let #msk    = &a.msk
  let #kostos = round(&a.posothta * &a.msk,2)
  print #msk         (,+9,14)  edit {MASK2}
  print #kostos      (,+9,14)  edit {MASK3}

  let #sub_posot  = #sub_posot + &a.posothta
  let #sub_kostos = #sub_kostos + #kostos
  move 'F' to $new_page_flag
  next-listing


from wh_apografh a, wh_eidh b
where a.cid = $cid 
  and a.xrhsh = #xrhsh
  and a.eidos = b.kvdikos
  and a.posothta != 0
  and a.flag_print is null
[$SQLCMD]
order by a.apouhkh, b.kfpa desc, b.perigrafh
end-select

  print $L_X2           (,1,{PAGE-X}) fill
  next-listing
  print 'Γενικά Σύνολα Απογραφής'          (,06,44)
  print ''                                 (,+9,03)
  print #tot_posot                         (,+9,14) edit {MASK1}
  print ''                                 (,+9,14) 
  print #tot_kostos                        (,+9,14) edit {MASK3}
  next-listing   
  print $L_X2           (,1,{PAGE-X}) fill
  next-listing

end-procedure

! ******************************************************************

begin-procedure print_metafora
  print '*** ΑΠΟ ΜΕΤΑΦΟΡΑ'                 (,06,44)
  print ''                                 (,+9,03)
  print #sub_posot                         (,+9,14) edit {MASK1}
  print ''                                 (,+9,14) 
  print #sub_kostos                        (,+8,14) edit {MASK3}
  next-listing   

end-procedure

! ******************************************************************

begin-procedure subtot

  print $L_X2           (,1,{PAGE-X}) fill
  next-listing
  print 'Σύνολα Αποθήκης με ΦΠΑ'           (,06,44)
  print  $fpa_pr                           (,+9,03)
  print #sub_posot                         (,+9,14) edit {MASK1}
  print ''                                 (,+9,14) 
  print #sub_kostos                        (,+9,14) edit {MASK3}
  next-listing   
  print $L_X2           (,1,{PAGE-X}) fill
  next-listing

  let #ap_posot  = #ap_posot + #sub_posot
  let #ap_kostos = #ap_kostos + #sub_kostos
  move 0 to #sub_posot
  move 0 to #sub_kostos

end-procedure

! ******************************************************************

begin-procedure totapoth
  do getapou($cid, &a.apouhkh, $apouhkh)
  print $L_X2           (,1,{PAGE-X}) fill
  next-listing
  print 'Σύνολα Αποθήκης'                  (,06,15)
  print $apouhkh                           (,+1,28)
  print ''                                 (,+9,03)
  print #ap_posot                          (,+9,14) edit {MASK1}
  print ''                                 (,+9,14) 
  print #ap_kostos                         (,+9,14) edit {MASK3}
  next-listing   
  print $L_X2           (,1,{PAGE-X}) fill
  next-listing

  let #tot_posot  = #tot_posot + #ap_posot
  let #tot_kostos = #tot_kostos + #ap_kostos
  move 0 to #ap_posot
  move 0 to #ap_kostos
  move 'T' to $flag

end-procedure

! ******************************************************************

begin-procedure newapoth
 
  do getapou($cid, &a.apouhkh, $apouhkh)
  print $L_X1           (,1,{PAGE-X}) fill
  next-listing
  print 'ΑΠΟΘΗΚΗ :'                  (,01,09)
  print $apouhkh                     (,+1,35)
  next-listing   
  print $L_X1           (,1,{PAGE-X}) fill
  next-listing
  move 'F' to $flag

end-procedure

! ******************************************************************

begin-procedure getapou($cid, $apou, :$apouhkh)
begin-select
c.perigrafh
  let $apouhkh = '['||$apou||'] '||&c.perigrafh
from wh_apouhk c
where c.cid = $cid
  and c.kvdikos = $apou
end-select
end-procedure

! ******************************************************************


begin-footing {FOOT-SIZE}
  ! #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  ! #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

  move &xr.xrhsh_telos to $msg {DATEMASK}
  let $msg = '                                              ΤΗΣ '||$msg
  print $msg (2,) center
  print $L_X1                                      (4,01,{PAGE-X}) fill
  print ' '                                        (5,01,05)
  print 'Ε  ί  δ  ο  ς'                             (,+1,40)
  print 'Μ.Μ.'                                      (,+1,03)
  print 'ΦΠΑ'                                       (,+9,03)
  print '   Ποσότητα'                               (,+9,14)
  print '       Τιμή'                               (,+9,14)
  print '    Κόστος'                                (,+9,14)
  print $L_X1                                      (6,01,{PAGE-X}) fill

  move 'T' to $new_page_flag

end-heading
!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{WH	1	1	333	elexapo	rs	X	01},TITLE{Καταχώρηση Απογραφής}
!	PARCOUNT{1}
!	PAR1{Χρήση	DUMP	1	ΕΠΙΛΟΓΗ	int		X}
!REPORTHELP{F}
!</pcalib>
