!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : arelex2.sqr                                    ****
!***** DATE        : 09/06/93                                       ****
!***** APPLICATION : SYNAL                                          ****
!***** AUTHOR      : ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ                              ****
!***** NOTE        : --- ΕΛΕΧΟΥ ΑΡΘΡΟΥ ΗΜΕΡΑΣ                  ---  ****
!***********************************************************************


#DEFINE   PAGE-Y    65
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  4
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   MASK1      9999999.99
#DEFINE   DATEMASK   DD/MM/YY

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

begin-sql -C1
create table ##tmp (aa int,flag char(1))
create table ##glpar (parast varchar(10), flag_xp char(1), syntel int)
insert into ##glpar values ('X','X',1)
insert into ##glpar values ('P','P',-1)
end-sql

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $ihm_apo 'hmer_apo'
 input $ihm_evs 'hmer_evs'
 execute app_datecnv    $ihm_apo, $hm_apo output, $hm_apopr output
 execute app_datecnv    $ihm_evs, $hm_evs output, $hm_evspr output

 do findxrhsh($cid, $hm_apo, #xrhsh)
 do main
End-Report

! *************************************************************************
! *** Syntax : 
begin-procedure findxrhsh($cid,$hmer,:#xrhsh)

move 0 to #xrhsh

begin-select
xrhsh           () NOP
  move &xrhsh to #xrhsh
from ap_xrhsh
 where cid = $cid
   and $hmer between xrhsh_arxh and xrhsh_telos
end-select

end-procedure


! *************************************************************************

begin-procedure main

begin-sql -C1
insert into ##tmp (aa,flag)
select a.aa,'1'
 from sy_glkin a, ##glpar b
where a.kk = b.parast
and a.cid = $cid
and a.hmer between $hm_apo and $hm_evs
[$SQLCMD]
group by a.aa

end-sql


begin-select -C1
convert(datetime,a.hmer) &a.hmer   () NOP
a.hmerologio                       () NOP
a.parid                            () NOP
a.aa                               () on-break print=never before=printmas
                                      save=$aasave          after=printtot
a.kk                               () NOP
a.logar                            (,01,12)
b.perigrafh                        (,+1,35) 
a.poso                             () NOP
  do getposo(&a.kk,&a.poso,#xrevsh,#pistvsh)
  print #xrevsh                    (,+5,10) edit {MASK1}
  print #pistvsh                   (,+1,10) edit {MASK1}

  let #tot_xre = #tot_xre  + #xrevsh
  let #tot_pis = #tot_pis  + #pistvsh

a.fpa                              (,+4,02) edit 99
  print '.'                        ()
a.kathgoria                        (,  ,01)
a.aitia                            (,+4,44)

  next-listing

from sy_glkin a, gl_logar b, ##tmp c
where a.cid = $cid
  and b.xrhsh = #xrhsh
  and b.cid   = $cid

  and a.logar*=b.logar

  and a.aa = c.aa
  and c.flag='1'
[$SQLCMD]
order by a.aa
[$SQLORD]
end-select

next-listing
move 'T' to $time1flag

begin-select -C1

   if $time1flag = 'T'
      new-page
      next-listing
      next-listing
      print 'Τα παρακάτω παραστατικά δέν έχουν παράγει άρθρο προς τη λογιστική' () center
      next-listing
      next-listing
      next-listing
      move 'F' to $time1flag
   end-if
a0.aa              (,08,06) edit 999999
a0.parkvd          (,+4,02)
a0.seira           (,+1,03)
a0.parno           (,+1,05) edit 99999
convert(datetime,a0.hmer_e) &a0.hmer_e  (,+4,08) edit {DATEMASK}
'ΑΠ:'   &filler2   (,+4,03)
a0.apouhkh         (,+1,02)
a0.synal           (,+4,05)
c0.epvnymia        (,+1,30)
    next-listing
from sy_parmas a0, ##tmp b0, sy_synal c0
where a0.aa = b0.aa
  and a0.synal = c0.synal
  and b0.flag  = '2'
end-select

end-procedure

! ******************************************************************



begin-procedure printtot

 print '---------- ----------'    (,54)
 next-listing
 print #tot_xre                   (,54,10) edit {MASK1}
 print #tot_pis                   (,+1,10) edit {MASK1}
 let #diff = #tot_xre - #tot_pis
 if #diff <> 0
    print ' ΔΙΑΦΟΡΑ : '   ()
    print  #diff                 (,+1,10) edit {MASK1}
    move $aasave to #aasave
    do printanal(#aasave)
    do printdebug(#aasave,$SQLCMD)
 end-if
 next-listing

end-procedure

begin-procedure printmas

 next-listing

 print &a.aa                       (,01,06) edit 999999
 print &a.hmer                     (,+7,08) edit {DATEMASK}
 print &a.hmerologio               (,+4,03) 
 print &a.parid                    (,+4,10)

begin-select 
'ΑΠ:'            &filler1          (,+3,03)
a1.apouhkh                         (,+1,02)
b1.epvnymia                        (,+3,30)
   next-listing
from sy_parmas a1, sy_synal b1
where a1.aa = &a.aa
  and a1.synal = b1.synal
end-select


 next-listing

 move 0 to #tot_xre
 move 0 to #tot_pis

end-procedure

begin-procedure printanal(#aa)

next-listing
next-listing

begin-select
a.kvdikos                 (,54,02)  edit 99
b.perigrafh               (,+1,20)
a.ajia                    (,+1,10)  edit {MASK1}
a.fpa                     (,+4,02)  edit 99
a.pososto                 (,+1,05)  edit 99,99

   next-listing

from sy_partotals a, sy_partotparfk b
where a.aa = #aa
  and a.kvdikos=b.kvdikos
end-select

 next-listing
end-procedure

! ******************************************************************

begin-procedure getposo($kk,#poso,:#xrevsh,:#pistvsh)

move '' to $flag
begin-select
flag_xp          () NOP
  move &flag_xp to $flag
from ##glpar
where parast=$kk
end-select

move 0 to #xrevsh
move 0 to #pistvsh
if $flag = 'X' 
   move #poso to #xrevsh
else
   move #poso to #pistvsh
end-if

end-procedure

! ******************************************************************

begin-procedure printdebug(#aa,$SQLCMD)

 next-listing
 print 'ΣΥΝΑΛΛΑΣΟΜ'        (,5,10)
 print '    ΧΡΕΩΣΗ'        (,+1,10) 
 print '   ΠΙΣΤΩΣΗ'        (,+1,10) 
 print '   ΔΙΑΦΟΡΑ'        (,+1,10) 
 next-listing


begin-select distinct
a.synal          () NOP

 do getdebuginfo(#aa,$SQLCMD,&a.synal,#xrevsh,#pistvsh)
 print &a.synal            (,5,10)
 print #xrevsh             (,+1,10) edit 9999999999
 print #pistvsh            (,+1,10) edit 9999999999
 let #diff = #xrevsh - #pistvsh
 print #diff               (,+1,10) edit 9999999999
 next-listing

from sy_glkin a
where a.aa=#aa
[$SQLCMD]
end-select

end-procedure

begin-procedure getdebuginfo(#aa,$SQLCMD,$synal,:#xrevsh,:#pistvsh)

move 0 to #xrevsh
move 0 to #pistvsh
begin-select
ltrim(rtrim(a.kk)) &a.kk      () nop
sum(a.poso) &poso             () nop

  if &a.kk = 'X'
     move &poso   to #xrevsh
  end-if
  if &a.kk = 'P'
     move &poso   to #pistvsh
  end-if

from sy_glkin a
where a.aa=#aa
and a.synal= $synal
[$SQLCMD]
group by a.kk

end-select

end-procedure


! ******************************************************************


begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  print 'ΚΑΤΑΣΤΑΣΗ ΕΛΕΓΧΟΥ ΑΝΑΛΥΤΙΚΩΝ ΑΡΘΡΩΝ ΗΜΕΡΑΣ' (3,) Center
end-heading

!<pcalib help>
! Ειδικά για άρθρα από την μισθοδοσία η παράμετρος [συναλλασόμενος] 
! αντιπροσωπεύει τον υπάλληλο (Α.Μ.)
!</pcalib help>
