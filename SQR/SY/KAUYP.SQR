!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : ypol1.sqr                                      ****
!***** DATE        : 09/06/93                                       ****
!***** APPLICATION : synalages                                      ****
!***** AUTHOR      : ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ                              ****
!***** NOTE        : Καθυστερούμενα Υπολοιπα πελατων                ****
!***********************************************************************

#DEFINE   PAGE-Y    64
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1     9999999999
!#DEFINE   MASK3     999,999,999c
#DEFINE   MASK3     999,999,999
#DEFINE   MASK2     999999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid
 input $ihm_evs 'evs hmeromhnia       :'
 input $iyp_hm  'hmeromhnia ypoloipoy :'
 execute app_datecnv   $ihm_evs, $evshm output, $evshmpr output
 execute app_datecnv   $iyp_hm, $yp_hm output, $yp_hmpr output
 move 'T' to $first_page
 do main
End-Report

! *************************************************************************

begin-procedure main
     let #timol_col = 77
begin-select 
a.nomos                                () on-break print=never
                                                   before=printnomos
                                                   after=subtotals
a.synal                                () NOP   
a.thl                                  () NOP   
a.dieyuynsh                            () NOP
a.polh                                 () NOP
a.taxkvd                               () NOP
c.epvnymia                             () NOP

  move &a.synal to $synal
  do getoik($cid,$synal,#ypol_arx,#tzi_1,#ypoloipo,#tzi,#timol,$hte)

  if #ypoloipo > 0
     
     move 'F' to $print_flag
     do printtimol     ! ($cid,$synal,#ypoloipo,#timol_col)

  end-if

from sy_synaleidos b, sy_synaltopos a, sy_synal c
 where b.eidos = 'PVLHSEIS'
   and a.synal = b.synal
   and b.synal = a.synal
   and a.topos = 0
   and a.synal = c.synal
   and b.synal = c.synal 
[$SQLCMD]
order by a.nomos
[$SQLORD]
end-select

  next-listing
  print 'Γ Ε Ν Ι Κ Α   Σ Υ Ν Ο Λ Α  :'     (,80,25)
  print #total                             (,+1,11) edit {MASK3}

end-procedure

! ******************************************************************
begin-procedure printsynal

     !let #subtot = #subtot + #ypoloipo  
     print &a.synal                    (,01,05)

     let #epvn_col = #current-column + 1
     print &c.epvnymia                 (,+1,28)
     print &a.thl                      (,+1,16)
     print $L_X3                       (,+1,1)

     print #timol                      (,+1,10) edit {MASK1}
     print #ypol_arx                   (,+1,10) edit {MASK1}
     print 'Τελ.Εισπρ.:'               (,+1,13)
     print $hte                        (,+1,08) edit {DATEMASK}
     print 'Υπόλοιπο :'                (,+1,10)
     print #ypoloipo                   (,+1,11) edit {MASK3}

     next-listing

     let $dieyuynsh = &a.dieyuynsh||' - '||&a.polh||' TK: '||&a.taxkvd
     print $dieyuynsh                  (,#epvn_col,45)
     print $L_X3                       (,+1,1)

     print #tzi_1                      (,+1,10) edit {MASK1}
     print #tzi                        (,+1,10) edit {MASK1}

     do getpvlinf($cid, $synal, $plhr, $prakt)
     print $plhr                       (,+1,25)
     print $prakt                      (,+1,25)
     next-listing
     print $L_X2                       (,01,{PAGE-X}) fill


end-procedure
! ******************************************************************
begin-procedure subtotals
  let #total = #total + #subtot
  print 'Σ Υ Ν Ο Λ Α   Ν Ο Μ Ο Υ :'     (,80,25)
  print #subtot                         (,+1,11) edit {MASK3}
  next-listing
  print $L_X1                           (,01,{PAGE-x}) fill
  next-listing
  let #subtot = 0
end-procedure
! ******************************************************************

begin-procedure getpvlinf($cid, $synal, :$plhr, :$prakt)
  move '' to $plhr
  move '' to $prakt

  execute syp_getpvlinf @cid        = $cid,
                        @synal      = $synal,
                        @tr_plhr    = #tr_plhr   out,
                        @plhr_txt   = $plhr_txt  out,
                        @prakt      = $prakt     out,
                        @epvn_prakt = $epvn      out

  move #tr_plhr to $plhr 99
  let $plhr  = $plhr||' '||$plhr_txt
  let $prakt = $prakt||' '||$epvn
end-procedure

! ******************************************************************

begin-procedure printtimol  !  ($cid,$synal,#ypoloipo,#timol_col)

move 0 to #ajia
!move 0 to #kauyst_ajia

begin-select 
b1.parkvd                                  () NOP
b1.seira                                   () NOP
b1.parno                                   () NOP
convert(datetime,b1.hmeromhnia,3) &b1.hmer () NOP
b1.ajia                                    () NOP

  do getdiff(&b1.hmer, $yp_hm, #diff)
  let #ajia = #ajia + &b1.ajia

  display #diff
  if #diff >= 0 and $print_flag = 'F'
     do printsynal
     move 'T' to $print_flag
  end-if

  if $print_flag = 'T'
    print &b1.parkvd                       (,#timol_col,02)
    print &b1.seira                        (,+1,03)
    print &b1.parno                        (,+1,06) edit {MASK2}
    print &b1.hmer                         (,+1,8) edit {DATEMASK}
    print &b1.ajia                         (,+1,11) edit {MASK3}

    if #ajia < #ypoloipo
       print &b1.ajia                      (,+1,11) edit {MASK3}
       let #kauyst_ajia = #kauyst_ajia + &b1.ajia
       next-listing
    else
       let #ypol = &b1.ajia + #ypoloipo - #ajia
       print #ypol                         (,+1,11) edit {MASK3}
       let #kauyst_ajia = #kauyst_ajia + #ypol
       next-listing
       print 'ΚΑΘΥΣΤΕΡΟΥΜΕΝΟ ΥΠΟΛΟΙΠΟ ==>' (,81,30)
       print #kauyst_ajia                  (,+1,11) edit {MASK3}
       next-listing
    let #subtot = #subtot + #kauyst_ajia
    move 0 to #kauyst_ajia 
    end-if

  end-if

  if #ajia >= #ypoloipo
       exit-select
  end-if

from sy_kin b1
where b1.synal=$synal
  and b1.cid  =$cid
  and exists(select 1 from ap_par b2 where b1.parkvd = b2.parkvd
             and (b2.stoixeio in ('01','02','04') or b2.parkvd = 'ΧΠ'))

order by b1.hmeromhnia desc
end-select

  if $print_flag = 'T'
     print $L_X1                       (,1,{PAGE-X}) fill
     next-listing
  end-if

end-procedure

! ******************************************************************
begin-procedure getdiff($hmer1, $hmer2, :#diff) 
  move 0 to #diff
begin-select
datediff(dd, $hmer1, $hmer2) &diff
  move &diff to #diff
end-select
end-procedure

! ******************************************************************

begin-procedure getoik($cid,$synal,:#ypol_arx,:#tzi_1,:#ypoloipo,:#tzi,:#timol,:$hte)

 execute syp_getoik1 @cid=$cid, @synal=$synal, 
         @arxiko_ypoloipo = #ypol_arx  out,
         @k_tziros_1      = #tzi_1  out, 
         @ypoloipo        = #ypoloipo  out, 
         @plhuos_tim      = #timol out,
         !@miktos_tziros   = #tzi output,
         @tel_eisp        = $hte out,
         @job='RET'

! 
 execute syp_getoik1 @cid=$cid, @synal=$synal, 
         @miktos_tziros   = #tzi output,
         @job='RET'

 !
 ! unremark for debug
 ! move 1 to #ypol_arx
 ! move 2 to #tzi_1
 ! move 3 to #ypoloipo
 ! move 4 to #tzi
 ! move 5 to #timol
 ! move 'Jun 11 1994 12:24PM' to $hte 
 !
end-procedure

! ******************************************************************

begin-procedure getepvn($synal,:$epvnymia)

move '' to $epvnymia

begin-select
a.epvnymia             () NOP
  move &a.epvnymia to $epvnymia
from sy_synal a
where a.synal = $synal
end-select

end-procedure

! ******************************************************************
begin-procedure printnomos
 if &a.nomos > 0
    do printnomos_doit
  end-if
end-procedure

begin-procedure printnomos_doit

move '' to $nomos
begin-select
a1.perigrafh     () NOP
  move &a1.perigrafh to $nomos
from ap_nomos a1
where a1.kvdikos = &a.nomos
end-select

 move &a.nomos to $knomos 99
 let $nomos=$L_X3||' ΝΟΜΟΣ : ('||$knomos||') '||$nomos||' '||$L_X3
 let #sz = length($nomos)

 if $first_page = 'T'
    move 'F' to $first_page
 else
    new-page  
 end-if
 print $L_X2   (,45,#sz) fill
 next-listing 
 print $nomos  (,45) 
 next-listing 
 print $L_X2   (,45,#sz) fill
 next-listing 
 print $L_X1                         (,1,{PAGE-X}) fill
 next-listing

end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  let $msg='ΚΑΘΥΣΤΕΡΟΥΜΕΝΑ ΥΠΟΛΟΙΠΑ ΠΕΛΑΤΩΝ ΑΠΟ ΗΜΕΡΟΜΗΝΙΑ : '||$yp_hmpr
  print $msg (3,) center
  print $L_X3                                      (5,53,01)
  print 'Αρ. Τιμολ.'                               ( ,+1,10)
  print 'Αρχ. Υπολ.'                               ( ,+1,10)
  print 'Ανάλυση τιμολογίων'                       ( ,+1   )
  print 'Συναλλασόμενος / Διεύθυνση / Τηλέφωνα'    (6,01,51)
  print $L_X3                                      ( ,+1,01)
  print 'Περ.Τζίρος'                               ( ,+1,10)
  print 'Φετ.Τζίρος'                               ( ,+1,10)

end-heading
