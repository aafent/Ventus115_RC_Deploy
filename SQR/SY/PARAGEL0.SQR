!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : paragel02.sqr                                  ****
!***** DATE        : 05/08/94                                       ****
!***** APPLICATION : synalages                                      ****
!***** AUTHOR      : THEOTOKAS JOHN                                 ****
!***** NOTE        : Κατασταση παραγγελιών πωλήσεων                 ****
!***********************************************************************

#DEFINE   PAGE-Y    59
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   SMALLDATE DD/MM
#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK5     99999
#DEFINE   MASK6     999999
#DEFINE   MASK8     999,999,999c
#DEFINE   MASK4_2   9,990.99c
#DEFINE   MASK6_2   999,990.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 do main
End-Report

! *************************************************************************

begin-procedure main
begin-select 
a.aa                                () NOP 
convert(datetime,a.hmer_k) &a.hmer  (,01,08) edit {DATEMASK}
a.seira                             (,+2,02)
a.parno                             (,+1,05) edit {MASK5}
a.synal                             (,+2,05)
  do getsynal (&a.synal,0,$epvnymia,$dieyuynsh)
  print $epvnymia                   (,+1,30)
  print $dieyuynsh                  (,+1,30)
a.tropos_plhr                       () NOP   
  do getplhr (&a.tropos_plhr,$plhrvmh)
  print $plhrvmh                    (,+1,30)

  next-listing

convert(datetime,a.hm_ep_parad) &a.hm_parad    (,01,08) edit {DATEMASK}
a.topos_synal                       (,+12,05)
a.topos_parad                       () NOP   
  do getsynal (&a.topos_synal,&a.topos_parad,$epvnymia,$dieyuynsh)
  print $epvnymia                   (,+1,30)
  print $dieyuynsh                  (,+1,30)
a.prakt                             (,+1,05)
  do getsynal (&a.prakt,0,$epvnymia,$dieyuynsh)
  print $epvnymia                   (,+1,30)

  next-listing
  do getdetails(&a.aa)

  print 'ΠΑΡΑΤΗΡΗΣΕΙΣ :'            (  ,01,14)
a.note                              (  ,+1,72)
  print 'ΛΑΒΩΝ :'                   (  ,+1,07)
a.pvlhths                           () NOP 
  do getpvlhths (&a.pvlhths,$pvlhths)
  print $pvlhths                    (  ,+1,20)

  next-listing
  print $L_X2                       (,01,{PAGE-X}) fill

  next-listing

from sy_parag a
where a.cid = $cid
[$SQLCMD]
order by a.hmer_k, a.aa
end-select
end-procedure

! ******************************************************************

begin-procedure getdetails(#aa)
begin-select
b.eidos                             (,21,05) edit {MASK5}
  do geteidos (&b.eidos,$perigrafh)
  print $perigrafh                  (,+1,25)
b.posothta_parag                    (,+1,10) edit {MASK6_2}
b.timh                              (,+3,07) edit {MASK6}
  next-listing
from sy_paragdet b
where b.aa = #aa
order by seira
end-select
end-procedure

! ******************************************************************

begin-procedure geteidos(#eidos,:$perigrafh)
  move '' to $perigrafh
begin-select
perigrafh     () NOP
  move &perigrafh to $perigrafh
from wh_eidh
where kvdikos = #eidos
end-select
end-procedure

! ******************************************************************

begin-procedure getplhr(#plhr,:$perigrafh)
  move '' to $perigrafh
begin-select
perigrafh     () NOP
  move &perigrafh to $perigrafh
from sy_troposplhr
where kvdikos = #plhr
end-select
end-procedure

! ******************************************************************

begin-procedure getpvlhths(#pvlhths,:$pvlhths)
  move '' to $pvlhths
begin-select
perigrafh     () NOP
  move &perigrafh to $pvlhths
from sy_pvlhtes
where kvdikos = #pvlhths
end-select
end-procedure

! ******************************************************************

begin-procedure getsynal($synal,#topos,:$epvnymia,:$dieyuynsh)
  move '' to $epvnymia
  move '' to $dieyuynsh
  if $synal = ''       
     move '00000' to $synal
  end-if
begin-select
a.epvnymia     () NOP
  move &a.epvnymia to $epvnymia
b.dieyuynsh    () NOP
  move &b.dieyuynsh to $dieyuynsh
from sy_synal a, sy_synaltopos b
where a.synal = $synal
  and a.synal = b.synal
  and b.topos = #topos
end-select
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  let $msg='Π Α Ρ Α Γ Γ Ε Λ Ι Ε Σ   Π Ω Λ Η Σ Ε Ω Ν'
  print $msg (3,) center
  print 'Ημ/νία'               (5,01,08)
  print 'Παραγγελία'           ( ,+2,10)
  print 'Πελάτης'              ( ,+6,30)
  print 'Διεύθυνση'            ( ,+1,30)
  print 'Τρόπος Πληρωμής'      ( ,+1,20)

  print 'Παράδοση'             (6,01,08)
  print ''                     ( ,+2,10)
  print 'Τόπος Παράδοσης'      ( ,+6,30)
  print 'Διεύθυνση'            ( ,+1,30)
  print 'Τρόπος Παράδοσης'     ( ,+1,20)
end-heading
