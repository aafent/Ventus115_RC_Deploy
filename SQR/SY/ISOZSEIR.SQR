! ******************************************************************************
! **** PROLOGIC S.A. COMPUTER APPLICATIONS                                  ****
! **** -------------------------------------------------------------------- ****
! **** REPORT      : isozseir.sqr                                           ****
! **** DATE        : 15/05/95                                               ****
! **** APPLICATION : ΠΕΛΑΤΕΣ - ΠΩΛΗΣΕΙΣ                                     ****
! **** AUTHOR      : Mal Nick                                               ****
! **** NOTE        : ΙΣΟΖΥΓΙΟ ΣΕΙΡΩΝ                                        ****
! ******************************************************************************

#DEFINE   PAGE-Y     58
#DEFINE   PAGE-X    132 
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   MASK1     99,999,999,999
#DEFINE   MASK2                999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
 
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 move '1 jan 1992' to $datestart

 do main($SQLCMD,$datestart,$apohm,$evshm)

End-Report

! ******************************************************************************

begin-procedure main($SQLCMD,$datestart,$apohm,$evshm)

  move 0 to #xre
  move 0 to #pis
  move 0 to #pre_xre
  move 0 to #pre_pis
 
begin-select distinct
' '                     (,01,15)
a.seira       &seira    (,+1,10) 
' '                     (,+1,15)

!  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  --

      do get_xre(&seira,$apohm,$evshm,#xrevsh)
        add #xrevsh  to #xre
      print #xrevsh         (,+1,14)  edit {MASK1}
      

      do get_pis(&seira,$apohm,$evshm,#pistvsh)
        add #pistvsh to #pis
      print #pistvsh        (,+1,14)  edit {MASK1}


      let #diff = #xrevsh - #pistvsh
        add #diff to #diff_tot
      print #diff          (,+1,14)  edit {MASK1}

!  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  --

      do get_xre(&seira,$datestart,$evshm,#pr_xrevsh)
        add #pr_xrevsh  to #pr_xre
      print #pr_xrevsh         (,+1,14)  edit {MASK1}
      

      do get_pis(&seira,$datestart,$evshm,#pr_pistvsh)
        add #pr_pistvsh to #pr_pis
      print #pr_pistvsh        (,+1,14)  edit {MASK1}


      let #pr_diff = #pr_xrevsh - #pr_pistvsh
        add #pr_diff to #pr_diff_tot
      print #pr_diff          (,+1,14)  edit {MASK1}

!  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  ---  --
 
      next-listing

from ap_parnum a
where  1=1
[$SQLCMD]
order by a.seira
end-select
  
  print '                              '                              (,12,)
  print '-------------- -------------- -------------- --------------' (,+1,)
  print '-------------- --------------'                               (,+1,)
  next-listing
  print 'ΓΕΝΙΚΑ ΣΥΝΟΛΑ               : '                              (,12,)

  print #xre                    (,+1,14)  edit {MASK1}
  print #pis                    (,+1,14)  edit {MASK1}
  print #diff_tot               (,+1,14)  edit {MASK1}

  print #pr_xre                 (,+1,14)  edit {MASK1}
  print #pr_pis                 (,+1,14)  edit {MASK1}
  print #pr_diff_tot            (,+1,14)  edit {MASK1}

end-procedure

! ******************************************************************************

begin-procedure get_xre($seira,$apohm,$evshm,:#xrevsh)

 let #xrevsh = 0

begin-select
b.seira                      () NOP
sum(b.ajia*c.syntel_xp)  &x  () NOP

      let #xrevsh = &x

from  sy_kin b,ap_par c
where b.parkvd = c.parkvd
and   c.f_xrevsh is not null
and   b.hmeromhnia between $apohm and $evshm
and   b.seira = $seira
group by b.seira 

end-select

end-procedure

! ------------------------------------------------------------------------------

begin-procedure get_pis($seira,$apohm,$evshm,:#pistvsh)

 let #pistvsh = 0

begin-select
b.seira                      () NOP
sum(b.ajia*c.syntel_xp)  &x  () NOP

      let #pistvsh = &x

from  sy_kin b,ap_par c
where b.parkvd = c.parkvd
and   c.f_pistvsh is not null
and   b.hmeromhnia between $apohm and $evshm
and   b.seira = $seira

group by b.seira 

end-select

end-procedure

! ******************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΙΣΟΖΥΓΙΟ ΣΕΙΡΩΝ' (2,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print ' '                                                   (5,1,10)
   print ' '                                                   (,+1,30)
   print '----------------- Περιόδου ------------------'       (,+1,44)
   print '|--------------- Προοδευτικά ----------------'       (,+1,44)

   print '        '                                            (6,1,13)
   print 'Σειρά  '                                             (,+1,10)
   print '        '                                            (,+1,17)
   print '        Χρέωση'                                      (,+1,14)
   print '       Πίστωση'                                      (,+1,14)
   print '      Υπόλοιπο'                                      (,+1,14)
   print '        Χρέωση'                                      (,+1,14)
   print '       Πίστωση'                                      (,+1,14)
   print '      Υπόλοιπο'                                      (,+1,14)


end-Heading
