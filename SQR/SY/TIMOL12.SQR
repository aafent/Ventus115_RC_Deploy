!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : timol.sqr                                      ****
!***** DATE        : 16/06/94                                       ****
!***** APPLICATION : synalages                                      ****
!***** AUTHOR      : THEOTOKAS JOHNNIE                              ****
!***** NOTE        : Εκτύπωση παραστατικών                          ****
!***********************************************************************

#DEFINE   PAGE-Y    66
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  1
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  1

#DEFINE   $L_GDATA    1

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASKEIDOS 9999
#DEFINE   MASK1     9999999999
#DEFINE   MASK11    9,999,999,999c
#DEFINE   MASK2     9999
#DEFINE   MASK3     99999.99c
#DEFINE   MASK4     99999999
#DEFINE   MASK5     99

Begin-Setup
#IFNDEF debugR 
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
#ENDIF

End-Setup

Begin-Report
#IFNDEF debugR 
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid
#ENDIF
 input $inp_aa  'a/a parastatikoy   '

#IFNDEF debugR 
 execute app_floatcnv   $inp_aa, #aa output
#ELSE
 move $inp_aa to #aa
#ENDIF

 do main
End-Report

! *************************************************************************

begin-procedure main

position (2,1)
move 09  to #col1
move 69  to #col2

begin-select 
a.cid                                  () NOP   
a.tim_seira                            () NOP   
a.tim_parno                            () NOP   
  move &a.cid to $cid
  move &a.tim_parno to $p2 999999
  let $sxetda = &a.tim_seira || $p2
a.aa                                   () NOP   
a.parkvd                               () NOP   
a.seira                                () NOP
a.parno                                () NOP
  do getsxet(#aa,$sxet)
  next-listing
  print &a.parkvd                      (,#col2,2)
  move &a.seira  to $p2
  move &a.parno  to $p3 99999
  next-listing
  next-listing
  let $p1 = $p2 || ' ' || $p3
  print $p1                            (,39)
  next-listing
a.tropos_plhr                          () NOP
a.apouhkh                              () NOP
  do getstoix2($cid,&a.apouhkh, $a_perigrafh,&a.tropos_plhr,$p_perigrafh)
  print 'ΑΠΟΘΗΚΗ '                     (,39,)
  print &a.apouhkh                     ()
  print $a_perigrafh                   (,+1,30)
  next-listing
  next-listing
a.synal                                (,#col1,5)
convert(datetime,a.hmer_e) &a.hmer_e   (,#col2,8) edit {DATEMASK}
  next-listing
  do getstoix(&a.synal,0,$epvnymia,$dieyuynsh,$polh,$afm,$doy,$thlef,$epaggelma)
  print $epvnymia                      (,#col1,50)
  next-listing
  print $epaggelma                     (,#col1,40)
  next-listing
  print $dieyuynsh                     (,#col1,40)
  next-listing
  print $polh                          (,#col1,40)
  let #p = #col2-8
  print 'Σχέτ ΔΑ:'                     (,#p   ,07)
  print $sxetda                        (,#col2,10)
  next-listing
  print $afm                           (,#col1)
  print $doy                           (,+1)

  print $sxet                          (,#col2,10)

a.topos_synal                          () NOP    
a.topos                                () NOP
  do getstoix(&a.topos_synal,&a.topos,$epvnymia,$dieyuynsh,$polh,$afm,$doy,$thlef,$epaggelma)
  next-listing
  next-listing
  print $epvnymia                      (,#col1,40)
  next-listing
  print $afm                           (,#col1,40)
  next-listing
  print $dieyuynsh                     (,#col1,20)
  print $polh                          (,   +1,20)

a.skopos                               () NOP
a.prakt                                () NOP     
  do getstoix(&a.prakt,0,$epvnymia,$dieyuynsh,$polh,$afm,$doy,$thlef,$epaggelma)
  next-listing
  next-listing
  print &a.prakt                       (,#col1,05)
  next-listing
  print $epvnymia                      (,#col1,40)

  next-listing
  next-listing
  next-listing
  next-listing
  do printlines(&a.aa,#tot_posothta,$flag_fial)

  let #ln = 39 - #current-line
  display #ln
  display #current-line
  position (+#ln,1)

  print &a.tropos_plhr                 (,#col1,02) edit {MASK5}
  print $p_perigrafh                   (,+1,30) 
  print #tot_posothta                  (,51,08) edit {MASK3}

  do getypoloipo($cid,&a.synal,#neo_ypoloipo)
  do gettot(#aa,#fpa_k1,#fpa_p1,#fpa_a1,#fpa_k2,#fpa_p2,#fpa_a2,#kyd,#eisprajh,#telikh)
  let #old_ypoloipo = ( #neo_ypoloipo - #telikh + #eisprajh )

  let #fpa_m1 = #fpa_k1 + #fpa_a1
  let #fpa_m2 = #fpa_k2 + #fpa_a2

  let #tot_fpa_k = #fpa_k1 + #fpa_k2

  next-listing
  next-listing
  next-listing

  print #old_ypoloipo                  (,   07,13) edit {MASK11}
  print #neo_ypoloipo                  (,  +11,13) edit {MASK11}
  
  if #fpa_p1 != 0
       print #fpa_k1                   (   ,47,08) edit 99999999
       print #fpa_p1                   (   ,56,02) edit 99
       print '%'                       ()
       print #fpa_a1                   (   ,61,07) edit 9999999
       print #fpa_m1                   (   ,69,09) edit 999999999
  end-if

  next-listing
  next-listing

  print &a.parno                       (   ,22,07)

  move '0'    to $eis_flag
  if #eisprajh != 0
     move '2' to $eis_flag
  end-if

  if #telikh = #eisprajh and
     #telikh != 0
     move '1' to $eis_flag
  end-if

  if #fpa_p2 != 0
       print #fpa_k2                   (   ,47,08) edit 99999999
       print #fpa_p2                   (   ,56,02) edit 99
       print '%'                       ()
       print #fpa_a2                   (   ,61,07) edit 9999999
       print #fpa_m2                   (   ,69,09) edit 999999999
  end-if

  next-listing
  next-listing

  if $eis_flag = '1'
       print #eisprajh                 (,15,13) edit {MASK11}
  end-if

  next-listing
  next-listing

  if $eis_flag = '2'
       print #eisprajh                 (,15,13) edit {MASK11}
  end-if

  print #tot_fpa_k                     (   ,47,08) edit 99999999
  print #kyd                           (   ,69,09) edit 999999999

  next-listing
  next-listing

  print #telikh                        (   ,69,09) edit 999999999
a.note                                 (,5,50)

  next-listing

  do getnotice(&a.seira,$flag_fial,&a.tropos_plhr,$m1,$m2,$m3,$m4,$m5,$m6)

  print $m1                            (,1)
  next-listing
  print $m2                            (,1)
  next-listing
  print $m3                            (,1)
  next-listing
  print $m4                            (,1)
  next-listing
  print $m5                            (,1)
  next-listing
  print $m6                            (,1)
  next-listing

from sy_parmas a 
where a.aa = #aa 
end-select

end-procedure

! ******************************************************************

begin-procedure printlines (#aa,:#tot_posothta,:$flag_fial)

move 0 to #tot_posothta
move 'F' to $flag_fial

begin-select -C2
a.eidos                                    (,01,04)   edit {MASKEIDOS}
b.perigrafh                                (,+1,25)
b.monada                                   (,+1,04)
a.temaxia                                  (,+1,04)   edit {MASK2}
a.posothta/a.temaxia      &baros           (,  ,08)   edit {MASK3}
a.posothta                                 (,+3,08)   edit {MASK3}
a.timh                                     (,+1,08)   edit {MASK3}
a.ajia                                     (,+1,08)   edit {MASK4}
! a.fpa                                      (,+1,02)   edit {MASK5}

  let #tot_posothta = #tot_posothta + &a.posothta
  next-listing 
  next-listing 

  if &a.eidos = 590
     move 'T' to $flag_fial
  end-if

from sy_pardet a, wh_eidh b
where a.aa = #aa 
  and a.eidos = b.kvdikos
end-select

end-procedure

! ******************************************************************

begin-procedure gettot(#aa,:#fpa_k1,:#fpa_p1,:#fpa_a1,:#fpa_k2,:#fpa_p2,:#fpa_a2,:#kyd,:#eisprajh,:#telikh)

 execute syp_timrettot @aa       = #aa,
                       @fpa_k1   = #fpa_k1 out,
                       @fpa_p1   = #fpa_p1 out,
                       @fpa_a1   = #fpa_a1 out,
                       @fpa_k2   = #fpa_k2 out,
                       @fpa_p2   = #fpa_p2 out,
                       @fpa_a2   = #fpa_a2 out,
                       @kyd      = #kyd    out,
                       @eisprajh = #eisprajh out,
                       @telikh   = #telikh   out

end-procedure

! ******************************************************************

begin-procedure getnotice($seira,$flag_fial,#tropos,:$m1,:$m2,:$m3,:$m4,:$m5,:$m6)

move '' to $m1
move '' to $m2
move '' to $m3
move '' to $m4
move '' to $m5
move '' to $m6

 if $seira = 'ΘΑ' 
   move '* Συσκευασίες πέρα των 45 ημερών δεν επιστρέφονται           ' to $m1
 end-if

 if #tropos = 5 or
    #tropos = 3 or
    #tropos = 1 
   move '* Η εξόφληση του τιμολογίου πέραν του συμφωνηθέντος          ' to $m2
   move '  χρόνου σας επιβαρύνει με τους νόμιμους τόκους              ' to $m3
 end-if
   
 if $flag_fial='T'
   move 'ΠΡΟΣΟΧΗ : Οι φιάλες είναι ιδιοκτησία μας και δε πωλούνται,   ' to $m4
   move '========= παραχωρούνται μόνο με καταβολή εγγύησης και πρέπει ' to $m5
   move '          να επιστρέφονται κενές εντός μηνός το αργότερο.    ' to $m6
 end-if

end-procedure

! ******************************************************************

begin-procedure getsxet(#aa,:$sxet)

move '' to $sxet
begin-select
parkvd    () NOP
seira     () NOP
parno     () NOP

   move &parno to $sxet 99999
   let $sxet = &seira || ' ' || $sxet
 
from sy_parsxet
where aa=#aa
and parkvd = 'Π1'
end-select

end-procedure

! ******************************************************************

begin-procedure getstoix2($cid,$apouhkh,:$a_perigrafh,#tropos,:$p_perigrafh)
move '' to $a_perigrafh
move '' to $p_perigrafh
begin-select
a1.perigrafh () NOP
   move &a1.perigrafh to $a_perigrafh
from wh_apouhk a1
where a1.cid=$cid
  and a1.kvdikos=$apouhkh
end-select

begin-select
a2.perigrafh () NOP
   move &a2.perigrafh to $p_perigrafh
from sy_troposplhr a2
where a2.kvdikos=#tropos
end-select
end-procedure

! ******************************************************************

begin-procedure getstoix($synal,#topos,:$epvnymia,:$dieyuynsh,:$polh,:$afm,:$doy,:$thlef,:$epaggelma)

move ' ' to $epvnymia
move ' ' to $dieyuynsh
move ' ' to $epaggelma
move ' ' to $afm
move ' ' to $doy
move ' ' to $thlef

execute syp_timgetsyn @synal = $synal,
                      @topos = #topos,
                      @epvnymia = $epvnymia out,
                      @dieyuynsh = $dieyuynsh out,
                      @polh = $polh out,
                      @afm = $afm out,
                      @doy = $doy out,
                      @thlef = $thlef out,
                      @epaggelma = $epaggelma out

end-procedure

! ******************************************************************

begin-procedure getypoloipo($cid,$synal,:#ypoloipo)

 move 0 to #ypoloipo

 execute syp_getypol @synal    = $synal,
                     @cid      = $cid,
                     @ypoloipo = #ypoloipo out
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
end-footing

begin-heading {HEAD-SIZE}

end-heading
