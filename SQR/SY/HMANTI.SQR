! ***********************************************************************
! ***** PROLOGIC S.A.                                                ****
! ***** REPORT      : hmanti.sqr                                     ****
! ***** DATE        : 09/06/93                                       ****
! ***** APPLICATION : synalages                                      ****
! ***** AUTHOR      : ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ                              ****
! ***** NOTE        : Κατασταση Αντικαταβολών ημέρας                 ****
! ***********************************************************************

#DEFINE   PAGE-Y     62
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE   6
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1          99999
#DEFINE   MASK2     99,999,999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $ihm_apo  'apo hmeromhnia '
 input $ihm_evs  'evs hmeromhnia '
 execute app_datecnv    $ihm_apo, $hm_apo output, $apohmpr output
 execute app_datecnv    $ihm_evs, $hm_evs output, $evshmpr output

 do main
End-Report

! *************************************************************************

begin-procedure main
 move 0 to #total
 move 0 to #subtotal
 move 0 to #lns
 move $L_X2 to $tot_char
 move 'Σύνολο Ημέρας'  to $tot_msg          

begin-select 
convert(datetime,a.hmer_e) &a.hmer_e        ()       on-break 
                                                     print=never
                                                     after=printsubtot
convert(datetime,a.hmer_e) &a.hmer_lala     ()       NOP
  move &a.hmer_e to $hmer_e
  print $hmer_e                             (,01,08) edit {DATEMASK} 
a.parkvd                                    (,+2,02)
a.seira                                     (,+1,03)
a.parno                                     (,+1,05) edit {MASK1}
a.synal                                     (,+4,05)
  do getsynal(&a.synal,$synal_txt)
  print $synal_txt                          (,+1,25)
a.prakt                                     (,+1,05)
   do getsynal(&a.prakt,$synal_txt)
   print $synal_txt                         (,+1,25)
   let #ajia_col = #current-column + 2
b.ajia                                      ()         NOP
   move &b.ajia to #ajia 
   do make_ajia(&a.parkvd,#ajia)
   print #ajia                              (,+2,10)  edit {MASK2}

   let #total    = #total    + #ajia
   let #subtotal = #subtotal + #ajia
   let #lns      = #lns      + 1

   next-listing need=2

from sy_parmas a, sy_partotals b
where a.prakt is not null
  and a.tropos_plhr in (2, 3)
  and a.aa=b.aa
  and b.kvdikos=6
  and a.hmer_e between $hm_apo and $hm_evs
  and a.cid = $cid
[$SQLCMD]
order by 1
[$SQLORD]
end-select

 move $L_X1 to $tot_char
 move 'Γενικό Σύνολο '  to $tot_msg          
 move #total to #subtotal
 move 2      to #lns
 do printsubtot

end-procedure

! ******************************************************************

begin-procedure make_ajia($parkvd,:#ajia)

move 1 to #pros
begin-select
a.syntel_xp
  move &a.syntel_xp to #pros
from ap_par a
where a.parkvd = $parkvd
end-select

let #ajia = #ajia * #pros

end-procedure

! ----------------------------------------------------------------------

begin-procedure printsubtot
  let     #ajia_col = 89
  display #ajia_col
  if #lns > 1
     print $tot_char (,#ajia_col,10)  fill
     next-listing
     let #col_msg = #ajia_col - 15
     display #col-msg

     print $tot_msg  (,#col_msg,14)
     print #subtotal (,#ajia_col,10)  edit {MASK2}
     next-listing
  end-if

  next-listing
  move 0 to #subtotal
  move 0 to #lns

end-procedure

! -----------------------------------------------------------------*

begin-procedure getsynal($synal,:$epvnymia)
  move '' to $epvnymia
begin-select
epvnymia     () NOP
  move &epvnymia to $epvnymia
from sy_synal
where synal=$synal
end-select
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  if $apohmpr = $evshmpr
     let $msg='ΑΝΤΙΚΑΤΑΒΟΛΕΣ ΗΜΕΡΑΣ : '||$apohmpr
  else
     let $msg='ΑΝΤΙΚΑΤΑΒΟΛΕΣ ΠΕΡΙΟΔΟΥ '||$apohmpr||' ΕΩΣ '||$evshmpr
  end-if

  print $msg (3,) center

  print 'Ημ/νία'               (5,01,08)
  print 'ΠΑ'                   ( ,+2,02)
  print 'ΣΕΙ'                  ( ,+1,03)
  print 'Αριθμ'                ( ,+1,05)

  print 'Κωδ. '                ( ,+4,05)
  print 'Συναλλασόμενος'       ( ,+1,25)
  print 'Κωδ. '                ( ,+1,05)
  print 'Πρακτορείο'           ( ,+1,25)
  print 'Αξ.Τιμολογίου'        ( ,+2,10)

end-heading

