! ******************************************************************************
! **** PROLOGIC S.A. COMPUTER APPLICATIONS                                  ****
! **** -------------------------------------------------------------------- ****
! **** REPORT      : isoztmhm.sqr                                           ****
! **** DATE        : 12/05/95                                               ****
! **** APPLICATION : ΠΕΛΑΤΕΣ - ΠΩΛΗΣΕΙΣ                                     ****
! **** AUTHOR      : Mal Nick                                               ****
! **** NOTE        : ΙΣΟΖΥΓΙΟ ΤΜΗΜΑΤΩΝ                                      ****
! ******************************************************************************

#DEFINE   PAGE-Y     58
#DEFINE   PAGE-X    132 
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   DATEMASK        DD/MM/YY
#DEFINE   MASK1     99,999,999,999
#DEFINE   MASK2                999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
 
 
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output
 execute app_geteom    $evshm   , $evshm output

 move '1 jan 1992' to $datestart

 do main($SQLCMD,$datestart,$apohm,$evshm)

End-Report

! ******************************************************************************

begin-procedure main($SQLCMD,$datestart,$apohm,$evshm)

  move 0 to #xre
  move 0 to #pis
  move 0 to #pre_xre
  move 0 to #pre_pis
 
begin-select distinct
a.kvdikos          &tmhma  (,01,10) 
a.perigrafh                (,+1,30)

     
    do printrest(&tmhma,$apohm,$evshm,#xrevsh,#pistvsh)
   
    do printrest(&tmhma,$datestart,$evshm,#pre_xrevsh,#pre_pistvsh)
     
      add   #xrevsh       to  #xre
      add   #pistvsh      to  #pis
      add   #pre_xrevsh   to  #pre_xre
      add   #pre_pistvsh  to  #pre_pis

      display #prev_xre
      display #prev_pis
      display #prev_xrevsh
      display #prev_pistvsh

      print #xrevsh         (,+1,14)  edit {MASK1}
      print #pistvsh        (,+1,14)  edit {MASK1}
      
      let #ypol=#xrevsh-#pistvsh
         print #ypol        (,+1,14)  edit {MASK1}
     
      print #pre_xrevsh     (,+1,14)  edit {MASK1}
      print #pre_pistvsh    (,+1,14)  edit {MASK1}


      let #pre_ypol=#pre_xrevsh-#pre_pistvsh
         print #pre_ypol        (,+1,14)  edit {MASK1}

      next-listing

from  ap_tmhma a
where 1=1
[$SQLCMD]

end-select
  
  print '                              '                              (,12,)
  print '-------------- -------------- -------------- --------------' (,+1,)
  print '-------------- --------------'                               (,+1,)
  next-listing
  print 'ΓΕΝΙΚΑ ΣΥΝΟΛΑ               : '                              (,12,)

  print #xre            (,+1,14)  edit {MASK1}
  print #pis            (,+1,14)  edit {MASK1}
  
  let #diff=#xre-#pis
     print #diff        (,+1,14)  edit {MASK1}
  
  print #pre_xre        (,+1,14)  edit {MASK1}
  print #pre_pis        (,+1,14)  edit {MASK1}

  let #diff=#pre_xre-#pre_pis
     print #diff        (,+1,14)  edit {MASK1}

end-procedure

! ******************************************************************************

begin-procedure printrest(#tmhma,$apohm,$evshm,:#xrevsh,:#pistvsh)

  let  #xrevsh  = 0
  let  #pistvsh = 0

begin-select
sum(x_mikth)        &x  () NOP
sum(p_mikth)        &p  () NOP

  let #pistvsh   = &p
  let #xrevsh    = &x


from   sy_kint1 b
where  b.tmhma = #tmhma
and    b.hmeromhnia between $apohm and $evshm


end-select

end-procedure

! ------------------------------------------------------------------------------

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΙΣΟΖΥΓΙΟ ΤΜΗΜΑΤΩΝ' (2,+1) center

 move $evshm to $evshmpr {DATEMASK}

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (3,+1) center

   print ' '                                            (5,1,10)
   print ' '                                            (,+1,30)
   print '----------------- Περιόδου -----------------' (,+1,44)
   print '---------------- Προοδευτικά ---------------' (,+1,44)
   print '             T  M  H  M  A  '                 (6,1,41)
   print '        Χρέωση'                               (,+1,14)
   print '       Πίστωση'                               (,+1,14)
   print '      Υπόλοιπα'                               (,+1,14)
   print '        Χρέωση'                               (,+1,14)
   print '       Πίστωση'                               (,+1,14)
   print '      Υπόλοιπα'                               (,+1,14)


end-Heading
