! ***********************************************************************
! ***** PROLOGIC S.A.                                                ****
! ***** REPORT      : kartela.sqr                                    ****
! ***** DATE        : 14/09/94                                       ****
! ***** APPLICATION : ΣΥΝΑΛΛΑΣΟΜΕΝΟΙ                                 ****
! ***** AUTHOR      : ΘΕΟΤΟΚΑΣ ΙΩΑΝΝΗΣ                               ****
! ***** NOTE        : - ΚΑΤΑΣΤΑΣΗ ΚΑΡΤΕΛΑΣ ΣΥΝΑΛΛΑΣΟΜΕΝΟY -          ****
! ***********************************************************************

#DEFINE   PAGE-Y     64
#DEFINE   PAGE-X    132 
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   DATEMASK  DD/MM/YY

#DEFINE   MASK1     99,999,999,999
#DEFINE   MASK7            999,999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '
 input $ilogar    'Λογαριασμός     : '

 execute app_datecnv $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv $evshmnia, $evshm output, $evshmpr output
 unstring $ilogar by '"' into $filler $logar $filler

 let $clogar = $logar||'%'
 let #col1   =  52
 let #col2   =  68
 let #prcol1 =  84
 let #prcol2 = 100
 let #ypcol  = 116

 do main

End-Report

! ****************************************************************************

begin-procedure main

begin-select
a.synal                                    ()         on-break print=never
                                                      before=getsynal
                                                      after=printtot
a.logar                                    ()         NOP
convert(datetime,a.hmeromhnia)  &hmer      ()         NOP
  print  &hmer                             (  ,01,08) edit {DATEMASK}
a.parkvd                                   (  ,+1,02)
a.seira                                    (  ,+1,02)
a.parno                                    (  ,+1,05)
a.aitia                                    (  ,+1,30)
xrevsh                                     ()         NOP
pistvsh                                     ()         NOP

!      do getxp(&a.parkvd, &a.ajia, #xrevsh, #pistvsh)

      let #ypoloipo  = #ypoloipo  + &xrevsh - &pistvsh
      let #prxrevsh  = #prxrevsh  + &xrevsh
      let #prpistvsh = #prpistvsh + &pistvsh

      print &xrevsh                        (  ,#col1,  14) edit {MASK1}
      print &pistvsh                       (  ,#col2,  14) edit {MASK1}
      print #prxrevsh                      (  ,#prcol1,14) edit {MASK1}
      print #prpistvsh                     (  ,#prcol2,14) edit {MASK1}
      print #ypoloipo                      (  ,#ypcol, 14) edit {MASK1}

      next-listing 

from sy_kin a
where a.cid   =    $cid
  and a.logar like $clogar
  and convert(datetime,a.hmeromhnia) between $apohm and $evshm
![$SQLCMD]
order by a.synal, 
[$SQLORD]
end-select
  
end-procedure

! ****************************************************************************
begin-procedure printtot
  print 'ΥΠΟΛΟΙΠΟ ΚΑΡΤΕΛΑΣ'          ( ,30,20)
  print #ypoloipo                    ( ,#ypcol,14) edit {MASK1}
  next-listing
  print '-'                          ( ,01,{PAGE-X}) fill
  next-listing need=3
  move 0 to #ypoloipo
end-procedure

! ****************************************************************************

begin-procedure getxp($parkvd, #ajia, :#xrevsh, :#pistvsh)
  let #xrevsh  = 0
  let #pistvsh = 0
begin-select
f_xrevsh               () NOP
  move &f_xrevsh  to $f_x
f_pistvsh              () NOP
  move &f_pistvsh to $f_p
syntel_xp              () NOP
  move &syntel_xp to #syntel
from ap_par
where parkvd = $parkvd
end-select
  if $f_x != ''
     let #xrevsh = #ajia * #syntel
  end-if
  if $f_p != ''
     let #pistvsh = #ajia * #syntel
  end-if
end-procedure

! ****************************************************************************

begin-procedure getypol 
  move '' to $hmer
begin-select
dateadd(dd, -1, $apohm)   &hm
  move &hm to $hmer
end-select

execute syp_getypol  @synal   = $synal, 
                     @hmer    = $hmer,
                     @cid     = $cid,
                     @logar   = $logar,
                     @xrevsh  = #pr_xrevsh out,
                     @pistvsh = #pr_pistvsh out,
                     @ypoloipo = #ypol out

  print 'ΣΥΝΟΛΑ ΑΠΟ ΜΕΤΑΦΟΡΑ '       ( ,30,20)
  print #pr_xrevsh                   ( ,#prcol1,14) edit {MASK1}
  print #pr_pistvsh                  ( ,#prcol2,14) edit {MASK1}
  let #ypoloipo = #pr_xrevsh - #pr_pistvsh
  print #ypoloipo                    ( ,#ypcol,14) edit {MASK1}
  next-listing
end-procedure

! ****************************************************************************

begin-procedure getsynal
  move '' to $line
  move &a.synal to $synal
begin-select
epvnymia
  let $line = $synal||' '||&epvnymia
from sy_synal
where synal = $synal
end-select
   print $line                                   ( ,01)          center
   next-listing
   print '-'                                     ( ,01,{PAGE-X}) fill
   next-listing
   do getypol
end-procedure

! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 if $logar = ''
    move '30' to $logar
 end-if
 let $msg = 'ΚΑΡΤΕΛΑ ΚΙΝΗΣΗΣ ΣΥΝΑΛΛΑΣΟΜΕΝΩΝ ['||$logar||']'
 print $msg (2,+1) center
 let $msg = 'ΑΠΟ '||$apohmpr||' ΕΩΣ '||$evshmpr

   print $msg                                    (3,+1        ) center

   print '|----- Κίνηση  Περιόδου -----|'        (5,#col1,  30)
   print '|---- Προοδευτική Κίνηση ----|'        ( ,#prcol1,30)
   print '  Υπόλοιπο'                            ( ,#ypcol, 14)

   print 'Ημερ/νία'                              (6, 1,     08)  
   print 'Παραστατικό'                           ( ,+1,     11)
   print 'Αιτιολογία Κίνησης'                    ( ,+1,     40)
   print 'Χ ρ έ ω σ η  -  Π ί σ τ ω σ η '        ( ,#col1,  30)
   print 'Χ ρ έ ω σ η  -  Π ί σ τ ω σ η '        ( ,#prcol1,30)
   print '  Καρτέλας'                            ( ,#ypcol, 14)

End-Heading
