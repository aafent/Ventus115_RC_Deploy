! ***********************************************************************
! ***** REPORT      : eyrethr.sqr                                    ****
! ***** DATE        : 26/05/94                                       ****
! ***** APPLICATION : synalages                                      ****
! ***** NOTE        : Ευρετήριο Συναλλασομένων                       ****
! ***********************************************************************

#DEFINE   PAGE-Y     62
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   SMALLDATE DD/MM
#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK1             99999
#DEFINE   MASK3                99
#DEFINE   MASK2     9,999,999,999

! #DEFINE   MASK2     9,999,999,999c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid
 
     do main

End-Report

! *************************************************************************

begin-procedure main



begin-select  LOOPS = 10

isnull(b.nomos, 0)                     &nomos  () NOP
b.polh                                         ()             on-break
                                                              print=never
                                                              after=lala


a.synal                                        (  ,  1, 8) on-break 
                                                           print=change
					                   level=1
b.topos                                        (  , +1, 2)
a.epvnymia                                     (  , +2,40)
  do getnomos(&nomos,$nomos_per)
  print $nomos_per                             (  , +1,15)
b.thl                                          (  , +1,25)
isnull(a.epaggelma,'')          &epaggelma     ()          NOP
  do getepagg(&epaggelma,$epagg_per,$dras_per)
  print $epagg_per                             (  , +1,20)
a.afm                                          (  , +1,10)
b.dieyuynsh                             &dieyu        
b.polh+" "+b.taxkvd                     &dieyu2        
  print ' '                                    (+1,  1, 8)
  print ' '                                    (  , +1, 2)
  print &dieyu                                 (  , +2,40)
  print &dieyu2                                (  , +1,41)
  print ' '                                    (  , +1,20)
isnull(a.doy_perig,'NULL')        &doy_per     ()          NOP
isnull(a.doy,'')                  &doy         ()          NOP
  
  if  &doy_per = 'NULL'
      do getdoy (&doy,$doy_per)
  else
      move &doy_per to $doy_per
  end-if
  print $doy_per                               (  , +1,15)

  next-listing need = 2

from  sy_synal a,
      sy_synaltopos b
where a.synal            = b.synal 
and   b.topos            = 0
[$SQLCMD]
 !order by [$SQLORD]
end-select

end-procedure

! ******************************************************************
begin-procedure lala
 next-listing
end-procedure

begin-procedure getepagg($epagg,:$epagg_per,:$dras_per)
  move '' to $epagg_per
begin-select
isnull(a.perigrafh,'') &aper    () NOP
isnull(b.perigrafh,'') &bper    () NOP
  move &aper to $epagg_per
  move &bper to $dras_per
from ap_epaggelma a, ap_drast b
where a.kvdikos = $epagg and a.drast = b.kvdikos
end-select
end-procedure

! ******************************************************************

begin-procedure getnomos(#nomos,:$nomos_per)
  move '' to $nomos_per
begin-select
isnull(perigrafh,'') &per    () NOP
  move &per to $nomos_per
from ap_nomos
where kvdikos = #nomos
end-select
end-procedure

! ******************************************************************

begin-procedure getxvra(#xvra,:$xvra_per)
  move '' to $xvra_per
begin-select
isnull(perigrafh,'') &per    () NOP
  move &per to $xvra_per
from ap_xvra
where kvdikos = #xvra
end-select
end-procedure

! ******************************************************************

begin-procedure getdoy($doy,:$doy_per)
  move '' to $doy_per
begin-select
isnull(perigrafh,'') &per    () NOP
  move &per to $doy_per
from  ap_doy
where kvdikos = $doy
end-select
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  let $msg='Ε Υ Ρ Ε Τ Η Ρ Ι Ο    Σ Υ Ν Α Λ Λ Α Σ Ο Μ Ε Ν Ω Ν     Γ Ι Α  :  ' || $synal_eidos
  print $msg (3,) center
  print 'Κωδικός'                  (5, 1, 8)
  print ' '                        ( ,+1, 2)
  print 'Συναλλασόμενος'           ( ,+2,40)
  print 'Νομός'                    ( ,+1,15)
  print 'Τηλέφωνο'                 ( ,+1,25)
  print 'Επάγγελμα'                ( ,+1,20)
  print 'Α.Φ.Μ.'                   ( ,+1,10)
  print ' '                        (6, 1, 8)
  print ' '                        ( ,+1, 2)
  print 'Διεύθυνση - Πόλη - Τ.Κ'   ( ,+2,40)
  print ' '                        ( ,+1,15)
  print ' '                        ( ,+1,25)
  print ' '                        ( ,+1,20)
  print 'Δ.Ο.Υ.'                   ( ,+1,20)

end-heading
