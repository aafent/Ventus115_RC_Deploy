! ***********************************************************************
! ***** PROLOGIC S.A.                                                ****
! ***** REPORT      : timol.sqr     (A)                              ****
! ***** DATE        : 16/06/94                                       ****
! ***** APPLICATION : synalages                                      ****
! ***** AUTHOR      : THEOTOKAS JOHNNIE                              ****
! ***** NOTE        : Εκτύπωση παραστατικών                          ****
! ***********************************************************************

#DEFINE   PAGE-Y     40
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE   1
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   1


#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASKEIDOS          9999
#DEFINE   MASK1        9999999999
#DEFINE   MASK11    9,999,999,999
#DEFINE   MASK2              9999
#DEFINE   MASK3             99999.99
#DEFINE   MASK4          99999999
#DEFINE   MASK5                99

Begin-Setup
#IFDEF debugR
 #DEFINE $L_GDATA 1
#ENDIF

#IFNDEF debugR 
 #DEFINE $CLASS_FILE 1
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"
#ENDIF

#IFDEF debugR
 ASK $CLASS_FILE 'CLASS FILE'
#ENDIF

!  no-formfeed

End-Setup

Begin-Report
#IFNDEF debugR 
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid
#ENDIF
 input $inp_aa  'a/a parastatikoy   '

#IFNDEF debugR 
 execute -C1 app_floatcnv   $inp_aa, #aa output
#ELSE
 move $inp_aa to #aa
#ENDIF

#IFDEF debugR
 #INCLUDE "{$CLASS_FILE}"
 execute -C1 app_retdefcid  $cid output
#ENDIF

print $RESET     () code   ! reset printer
print $SETPAGE_S () code   ! set page to invoice size
print $SET10CPI  () code   ! set printer to 10 cpi
print $CONDON    () code   ! set condensend mode on
 do main
print $BELL      () code   ! set the bell
End-Report

! *************************************************************************

begin-procedure main
position (2,1)
move 09  to #col1
move 69  to #col2

 execute -C1 syp_timretmas
 @aa          =#aa,
 @cid         =$cid         out,
 @tim_seira   =$tim_seira   out,
 @tim_parno   =#tim_parno   out,
 @parkvd      =$parkvd      out,
 @seira       =$seira       out,
 @parno       =#parno       out,
 @synal       =$synal       out,
 @hmer_e      =$hmer_e      out,
 @topos_synal =$topos_synal out,
 @topos       =#topos       out,
 @skopos      =#skopos      out,
 @tropos_plhr =#tropos_plhr out,
 @prakt       =$prakt       out,
 @apouhkh     =$apouhkh     out,
 @note        =$note        out,
 @parkvd_txt  =$parkvd_txt  out,
 @stoix_txt   =$stoix_txt   out


  next-listing

  ! print $stoix_txt                   (,10,40)
  print $parkvd_txt                  (,40,40)
  print $seira                       (,79,02)
  print #parno                       (,84,06) edit 999999
  print $hmer_e                      (,96,8)  edit {DATEMASK} 

  next-listing
  next-listing

  do getstoix($synal,0,$epvnymia,$dieyuynsh,$polh,$afm,$doy,$thlef,$epaggelma)
  do getstoix2($cid,$apouhkh,$a_perigrafh,#tropos_plhr,$p_perigrafh,#skopos,$s_perigrafh)

  move $dieyuynsh to $p_dieyuynsh
  move $polh      to $p_polh  

  if $synal <> $topos_synal or
     #topos <> 0
     do getstoix($topos_synal,#topos,$filler,$p_dieyuynsh,$p_polh,$filler,$filler,$filler,$filler)
  end-if


  if #topos = 0
     let $tmp = 'Παράδοση : Η έδρα του'
  else
     let $tmp = 'Παράδοση : ' || $p_dieyuynsh || ' ' || $p_polh
  end-if
  print $tmp                          (,30,73)
  print 'Αποστολή : Απο την έδρα μάς' (,85,30)
  next-listing

  let $tmp = $synal || ' ' || $epvnymia
  print $tmp                        (,41,73)
  print $epaggelma                  (,96,30)
  next-listing

  print $dieyuynsh                  (,41,73)
  print $afm                        (,96,30)
  next-listing

  print $polh                       (,41,30)
  print $thlef				(,+1,10)
  print $doy                        (,96,30)
  next-listing

  ! next-listing
!  position (1,1)
  do printlines($cid,#aa,#tot_posothta,#tot_ajia,#tot_ekpajia)
!
  position (11,1)
  next-listing

  do gettot(#aa,#fpa_k1,#fpa_p1,#fpa_a1,#fpa_k2,#fpa_p2,#fpa_a2,#kyd,#eisprajh,#telikh)
  do gettot2(#aa,#epibar_a,#epibar_fa,#epibar_fpa)

  print    #tot_ajia            (1,114,11) edit 99999999999

  print    #fpa_p1              (3,78,2)   edit 99
  print    #fpa_p2              (3,90,2)   edit 99
  ! print    'ΕΚΠΤΩΣΗ:'           (3,105,8)   
  ! print    #tot_ekpajia         (3,114,11) edit 99999999999

  print    #fpa_k1              (4,71,9)   edit 999999999
  print    #fpa_k2              (4,83,9)   edit 999999999
  print    'ΥΠΟΚ/ΝΗ:'           (4,105,8)   
  let  #tmp = #fpa_k1 + #fpa_k2
  print    #tmp                 (4,114,11) edit 99999999999

  print    #fpa_a1              (5,71,9)   edit 999999999
  print    #fpa_a2              (5,83,9)   edit 999999999
  print    'Φ.Π.Α. :'           (5,105,8)   
  let  #tmp = #fpa_a1 + #fpa_a2
  print    #tmp                 (5,114,11) edit 99999999999

  print    'Επιβαρύνσεις'       (1,92,12)
  print    #epibar_fpa          (3,101,2)   edit 99
  print    #epibar_a            (4,94,9)   edit 999999999
  print    #epibar_fa           (5,94,9)   edit 999999999
  print    'ΕΠΙΒ/ΣΗ:'           (6,105,8)   
  let #tmp = #epibar_fa + #epibar_a
  print    #tmp                 (6,114,11) edit 99999999999

  print    'ΣΥΝΟΛΟ ΠΟΣΟΤΗΤΑΣ'   (2,45)
  print #tot_posothta           (3,50,06)  edit 999999

  let $s_perigrafh = 'Σκοπός : ' || $s_perigrafh || '    '|| $p_perigrafh
  print $s_perigrafh            (7,29,60)
  print $note                   (8,29,60)

  let #plhr = #fpa_a1 + #fpa_a2   + #tot_ajia
  let #plhr = #plhr   + #epibar_a + #epibar_fa
  print    #plhr                (8,114,11) edit 99999999999

  print $parkvd                 (10,1,2)
  print '/'                     ()
  print #aa                     (,,7)         edit 9999999
  Print '/'                     ()
  print $seira                  ()

  ! next-listing 
  ! next-listing 
  ! next-listing 
  ! next-listing 
  ! next-listing 
  ! next-listing 

end-procedure

! ******************************************************************

begin-procedure printlines ($cid,#aa,:#tot_posothta,:#tot_ajia,:#tot_ekp)

move 0 to #tot_ajia     
move 0 to #tot_posothta
move 0 to #tot_ekp
move 1 to #lns
move 'F' to $flag_new_page

begin-select -C1

  if $flag_new_page = 'T'
     new-page 10
     move 'F' to $flag_new_page 
     position (8,1)
	next-listing
	position (+1,1)
     print 'ΣΥΝΟΛΟ : '                    (,10,12)
     Print #tot_posothta                  (,22,06)   edit 999999
     print 'ΑΠΟ ΜΕΤΑΦΟΡΑ :'               (,30,22)
     print #tot_ajia                      (,114,11)  edit 99999999999
     let #lns = #lns + 1
  end-if

a.eidos                                    (+1,07,12)
a.posothta                &posot           (,21,06)   edit 999999
a.posothta                &posot1          (,28,06)   edit 999999
b.perigrafh                                (,35,45)
b.monada                                   (,86,06)
a.fpa                                      (,93,02)   edit 99
a.timh                                     (,96,06)   edit 999999
a.ekp_p                                    (,104,02)  edit 99
(a.posothta*a.timh)-a.ajia  &ekpajia       (,107,06)  edit 999999
a.ajia                      &ajia          (,114,08)  edit 99999999

  let #tot_posothta = #tot_posothta + &posot
  let #tot_ajia     = #tot_ajia     + &ajia
  let #tot_ekp      = #tot_ekp      + &ekpajia
  let #lns = #lns + 1

  if #lns = 10
     move 1 to #lns
     print 'ΣΥΝΟΛΟ : '                    (+1,10,12)
     print #tot_posothta                  (,22,06)   edit 999999
     print 'ΣΕ ΜΕΤΑΦΟΡΑ : '               (,30,22)
     print #tot_ajia                      (,114,11)  edit 99999999999
     move 'T' to $flag_new_page
!	position (+15)
  end-if

from sy_pardet a, wh_eidh b
where a.aa = #aa 
  and a.eidos = b.kvdikos
  and a.eidos not in (select c.eidos from sy_spceidh c where c.cid  =$cid
                                                     and  flag_ekt is null )
end-select
end-procedure

! ******************************************************************

begin-procedure gettot(#aa,:#fpa_k1,:#fpa_p1,:#fpa_a1,:#fpa_k2,:#fpa_p2,:#fpa_a2,:#kyd,:#eisprajh,:#telikh)

 execute -C1 syp_timrettot @aa       = #aa,
                       @fpa_k1   = #fpa_k1 out,
                       @fpa_p1   = #fpa_p1 out,
                       @fpa_a1   = #fpa_a1 out,
                       @fpa_k2   = #fpa_k2 out,
                       @fpa_p2   = #fpa_p2 out,
                       @fpa_a2   = #fpa_a2 out,
                       @kyd      = #kyd    out,
                       @eisprajh = #eisprajh out,
                       @telikh   = #telikh   out

end-procedure

! ******************************************************************

begin-procedure gettot2(#aa, :#epibar_a, :#epibar_fa, :#epibar_fpa)
  move 0 to #epibar_a
  move 0 to #epibar_fa
  move 0 to #epibar_fpa
begin-select
sum(ajia) &ajia_a () NOP
  let #epibar_a = &ajia_a
from sy_partotals 
where aa = #aa
and   kvdikos = 3
end-select
begin-select
fpa       &epibar_fpa () NOP
sum(ajia) &ajia_fa () NOP
    let #epibar_fa = &ajia_fa
    move &epibar_fpa to #epibar_fpa
from sy_partotals 
where aa = #aa
and   kvdikos = 10
end-select
end-procedure

! ******************************************************************

! begin-procedure getsxet(#aa,:$sxet)
! 
! execute -C1 syp_timretsxe @aa=#aa, @sxet = $sxet out
! 
! end-procedure

! ******************************************************************

begin-procedure getstoix2($cid,$apouhkh,:$a_perigrafh,#tropos,:$p_perigrafh,#skopos,:$s_perigrafh)

 execute -C1 syp_timrets1 
 @cid          = $cid,
 @apouhkh      = $apouhkh,
 @a_perigrafh  = $a_perigrafh out,
 @tropos       = #tropos,
 @p_perigrafh  = $p_perigrafh out,
 @skopos       = #skopos,
 @s_perigrafh  = $s_perigrafh out

end-procedure

! ******************************************************************

begin-procedure getstoix($synal,#topos,:$epvnymia,:$dieyuynsh,:$polh,:$afm,:$doy,:$thlef,:$epaggelma)

move ' ' to $epvnymia
move ' ' to $dieyuynsh
move ' ' to $epaggelma
move ' ' to $afm
move ' ' to $doy
move ' ' to $thlef

if $synal <> ''
   execute -C1 syp_timgetsyn @synal = $synal,
                      @topos = #topos,
                      @epvnymia = $epvnymia out,
                      @dieyuynsh = $dieyuynsh out,
                      @polh = $polh out,
                      @afm = $afm out,
                      @doy = $doy out,
                      @thlef = $thlef out,
                      @epaggelma = $epaggelma out
end-if

end-procedure

! ******************************************************************

begin-procedure getypoloipo($cid,$synal,:#ypoloipo)

 move 0 to #ypoloipo

 execute -C1 syp_getypol @synal    = $synal,
                     @cid      = $cid,
                     @ypoloipo = #ypoloipo out
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
end-footing

begin-heading {HEAD-SIZE}

End-heading
