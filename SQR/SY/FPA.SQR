!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : fpa.sqr                                        ****
!***** DATE        : 11/07/94                                       ****
!***** APPLICATION : ΓΕΝΙΚΗ ΛΟΓΙΣΤΙΚΗ                               ****
!***** AUTHOR      : ΘΕΟΤΟΚΑΣ ΙΩΑΝΝΗΣ                               ****
!***** NOTE        : --- ΣΥΓΚΕΝΤΡΩΣΗ ΦΠΑ ΚΑΤ Α'ΒΑΘΜΙΟ   ---         ****
!***********************************************************************

#DEFINE   PAGE-Y     62
#DEFINE   PAGE-X     96 
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  5

#DEFINE   DATEMASK  DD/MM/YY
#DEFINE   MASK2     99
#DEFINE   MASK12    999999,999.99c

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID to $cid

 input $apohmnia  'Από Ημερομηνία  : '
 input $evshmnia  'Eως Ημερομηνία  : '

 execute app_datecnv   $apohmnia, $apohm output, $apohmpr output
 execute app_datecnv   $evshmnia, $evshm output, $evshmpr output

 move $L_XRHSH to #xrhsh
 do get_logars


End-Report

! ****************************************************************************

begin-procedure main
begin-select -C2
sum(isnull(xrevsh,pistvsh))        &ajia       () nop
sum(ajia_fpa)                      &ajia_fpa   () nop
isnull(fpa_pos,0)                  &fpa_pos    () nop

    move &ajia      to #ajia
    move &ajia_fpa  to #ajia_fpa
    move &fpa_pos   to #fpa_pos
    move 'Εγγραφές' to $src
    do ektyp

from sy_eggr a
where  a.cid    = $cid
  and substring(a.logar,1,2)=&logar
and hmer_e between $apohm and $evshm
![$SQLCMD]
group by fpa_pos
order by fpa_pos
end-select

    if &logar = '20'
       move 'AGORES' to $omada
       do getfromparast
    end-if
    if &logar = '70'
       move 'PVLHSEIS' to $omada
       do getfromparast
    end-if


end-procedure

! ****************************************************************************

begin-procedure getfromparast

move 0             to #ajia
move 0             to #ajia_fpa
move -1            to #fpa_pos
move 'Παρ/τικά'    to $src
move 'F'           to $flag_ektyp

begin-select -C3
a1.fpa                       &a1.fpa     () nop
a1.kvdikos                   &a1.kvdikos () nop 
sum(a1.ajia*d1.syntel_xp)    &a1.poso    () nop

    move 'T' to $flag_ektyp

    if #fpa_pos = -1
       move &a1.fpa   to #fpa_pos
       move 0         to #ajia
       move 0         to #ajia_fpa

    end-if

    if #fpa_pos <> &a1.fpa
       do ektyp
       move &a1.fpa to #fpa_pos
       move 'F' to $flag_ektyp
    end-if

    if &a1.kvdikos = 1 ! kauarh ajia
       move &a1.poso   to #ajia
    end-if
    if &a1.kvdikos = 4 ! ajia fpa
       move &a1.poso   to #ajia_fpa
    end-if

from sy_partotals a1, sy_parmas b1,  sy_partotparfk c1, ap_par d1, ap_omadapar e1
where a1.aa=b1.aa
  and a1.kvdikos=c1.kvdikos
  and b1.parkvd=d1.parkvd
  and d1.parkvd=e1.parkvd
  and e1.omada = $omada
  and b1.hmer_e between $apohm and $evshm
  and a1.kvdikos in (1,4)
  and b1.parno is not null
group by a1.fpa,a1.kvdikos
order by a1.fpa,a1.kvdikos
end-select

 if $flag_ektyp='T'
    do ektyp
 end-if



end-procedure


! ****************************************************************************

begin-procedure ektyp

  print $src             (  ,32,10)  
  print #fpa_pos	 (  ,43,3)   edit 90
  print #ajia            (  ,+2,15)  edit {MASK12}
  print #ajia_fpa        (  ,+2,15)  edit {MASK12}

  next-listing need=3

  add 1 to #ektyp_cnt
  let #tot_ajia  = #tot_ajia  + #ajia
  let #tot_ypfpa = #tot_ypfpa + #ajia_fpa
end-procedure

! ****************************************************************************
begin-procedure get_logars
begin-select -C1 distinct
perigrafh                         () nop
substring(a.logar,1,2) &logar     () nop

          move 0 to #ektyp_cnt
	  print &logar     	  (,1,3)
	  print &perigrafh	  (,+2,20)
	  do main 
          let #ysz = {PAGE-X} - 10
	  print '. . '            (,1,#ysz)  fill
          next-listing
          if #ektyp_cnt > 1
	     print #tot_ajia      (,47,14)      edit {MASK12}
	     print #tot_ypfpa     (,+2,14)      edit {MASK12}
             next-listing
	     print '. . '	  (,1,#ysz)  fill
             next-listing
          end-if
  	  move 0 to #tot_ajia
	  move 0 to #tot_ypfpa

from gl_logar a
where  a.cid=$cid
and a.xrhsh=#xrhsh
and baumos=1
and a.logar in (select logar from gl_ypol where cid=$cid and hmeromhnia between $apohm and
				$evshm)
and
 ( a.logar like "1[012346]%" or
                a.logar like "19__[012345678]%" or
                a.logar like "20%" or
                a.logar like "3[26]%" or
                a.logar like "5400[012345678]%" or
                a.logar like "6%" or
                a.logar like "7[024568]%" or
                a.logar like "8[012]%" ) and
             (   a.logar not like "18%" and
                a.logar not like "2090%" and
                a.logar not like "2099%" and
                a.logar not like "6_90%" and
                a.logar not like "69__90%" and
                a.logar not like "7090%" and
                a.logar not like "7290%" and
                a.logar not like "7590%" and
                a.logar not like "7690%" and
                a.logar not like "7890%" and
                a.logar not like "8190%" and
                a.logar not like "8290%")
order by a.logar
end-select
end-procedure

! ****************************************************************************


! ****************************************************************************

Begin-Footing {FOOT-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
End-Footing

Begin-Heading {HEAD-SIZE}
 #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"

 print 'ΣΥΓΚΕΝΤΡΩΣΗ Φ.Π.Α. ΚΑΤΑ ΠΡΩΤΟΒΑΘΜΙΟ' (3,+1) center

 let $msg='ΠΕΡΙΟΔΟΥ '||$apohmpr||' - '||$evshmpr

 print $msg (4,+1) center

  print '   Λογαριασμός       '   (06,01,30)
  print '        Φ.Π.Α.%'         (  ,+1,15)
  print '       Αξία'             (  ,+1,15)
  print '       ΦΠΑ'              (  ,+4,15)
End-Heading

!<pcalib>
!Warning DO NOT modify this lines, are used internaly from PCA*LIBs
!	REP{SY	1	3	6043	fpa	rs	X	01},TITLE{Συγκέντρωση Φ.Π.Α. κατά πρωτοβάθμιο}
!	PARCOUNT{1}
!	PAR1{Ημερομηνία	dump	2	ΑΠΟ-ΕΩΣ	datetime		X}
!REPORTHELP{F}
!</pcalib>
