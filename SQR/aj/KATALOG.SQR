!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : lhjh.sqr                                       ****
!***** DATE        : 09/06/93                                       ****
!***** APPLICATION : Application environment                        ****
!***** AUTHOR      : ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ                              ****
!***** NOTE        : Καταλογος Αξιογραφων                           ****
!***********************************************************************

#DEFINE   PAGE-Y     58
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE   7
#DEFINE   FOOT-SIZE   1
#DEFINE   HEAD-LINE   4

#DEFINE   DATEMASK DD/MM/YY

#DEFINE   MASK1    999,999,999
#DEFINE   MASK2    99999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID  to $cid
 
 input $ieidos 'eidos ajiogr'
 unstring $ieidos by '"' into $filler $eidos $filler


 if $eidos = 'Ε' or
    $eidos = 'E'
     move '01'     to $typ_ajio
     move 'Επιτ.' to $typos
 end-if
 if $eidos = 'Σ' or
    $eidos = 'S'
     move  '1'    to $typ_ajio
     move 'Συναλ' to $typos
 end-if
 if $typ_ajio = ''
     show clear-screen (24,1) 'Λαθος Τύπος Αξιογράφου' beep
     stop
 end-if

 do main
End-Report

! *************************************************************************

begin-procedure main
begin-select 
a.cid                                           () NOP
a.aa_ajiogr                                       (, 1, 5) edit {MASK2}
c.logar_x                                        (,+1,10)
c.synal_x                                        (,+1, 5)
a.synal                                         () NOP
a.trapeza                                       () NOP

     if &a.trapeza      != ''
        do find_trapeza(&a.trapeza,$trapeza)
     end-if
 
b.epvnymia                                      (,+1,30)
  print $trapeza                                (,+1,20)
a.aa_eterias                                   (,+1,12)
convert(datetime, a.hm_ekdoshs)  &a.hm_ekd          (,+1, 8) edit {DATEMASK}
a.aitia                                        (,+1,22)

  next-listing

c.logar_p                                        (, 7,10)
c.synal_p                                        (,+1, 5)
a.ekdoths                                       (,+1,30)
a.ariumos                                       (,+1,20)
a.topos_plhr                                     (,+1,12) 
convert( datetime, a.hm_lhjhs) &a.hm_lhjh        (,+1, 8) edit {DATEMASK}
a.ajia                                          (,+1,11) edit {MASK1}

  next-listing
  next-listing

from  aj_mas a,sy_synal b, aj_kin c
where a.synal    = b.synal
and   a.eidos_ajiogr = $typ_ajio
and   a.cid      = $cid
and   c.aa_eterias = a.aa_eterias
[$SQLCMD]
order by a.eidos_ajiogr, a.aa_ajiogr
end-select

end-procedure

! ******************************************************************

begin-procedure find_trapeza($kvdtrap,:$trapeza)

begin-select
a1.epvnymia         () NOP
from sy_synal a1
where a1.synal = $kvdtrap
end-select
  move &a1.epvnymia to $trapeza
end-procedure

! ******************************************************************

begin-procedure find_prakt($prakt,:$praktoras)

begin-select
a2.epvnymia         () NOP
from sy_synal a2
where a2.synal = $prakt
end-select
  move &a2.epvnymia     to $praktoras
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  let $ep1 = 'ΚΑΤΑΛΟΓΟΣ ΑΞΙΟΓΡΑΦΩΝ (' || $typos || ')'
  print $ep1                   (3,)Center
  print 'Αριθμ'                (5, 01,5)
  print 'Χρεούμενος      '     ( ,+1,16)
  print 'Ονοματεπωνυμο Συναλλ' ( ,+1,30)
  print 'Τ ρ α π έ ζ α'        ( ,+1,20)
  print 'Αρ.Επιταγής'          ( ,+1,12)
  print '  Εκδοση'             ( ,+1,08)
  print '        '             ( ,+1,11)
  print 'Παρατηρήσεις'         ( ,+1,30)

  print  $typos                (6,01,05)
  print 'Πιστούμενος     '     ( ,+1,16)
  print '    =//=      εκδότη' ( ,+1,30)
  print 'Αριθμός Λογαριασμού'  ( ,+1,20)
  print 'Τόπος Πληρωμής'       ( ,+1,12)
  print '    Λήξη'             ( ,+1,08)
  print '    Π ο σ ό'          ( ,+1,11)
  print '     '                ( ,+1,05)
  print '        '             ( ,+1,25)
end-heading
