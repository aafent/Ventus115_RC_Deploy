!***********************************************************************
!***** PROLOGIC S.A.                                                ****
!***** REPORT      : lhjh.sqr                                       ****
!***** DATE        : 09/06/93                                       ****
!***** APPLICATION : Application environment                        ****
!***** AUTHOR      : ΑΦΕΝΤΑΚΗΣ ΑΝΔΡΕΑΣ                              ****
!***** NOTE        : Καταλογος Αξιογραφων κατα Ληξη                 ****
!***********************************************************************

#DEFINE   PAGE-Y    58
#DEFINE   PAGE-X    132
#DEFINE   HEAD-SIZE  7
#DEFINE   FOOT-SIZE  1
#DEFINE   HEAD-LINE  4

#DEFINE   DATEMASK DD/MM/YY
#DEFINE   MASK1    999,999,999c
#DEFINE   MASK2    99999

Begin-Setup
 ASK    $L_GDATA   'GDATA '
 #INCLUDE "{$L_GDATA}/sqr/SETUP01.inc"

End-Setup

Begin-Report
 #INCLUDE "{$L_GDATA}/sqr/REPORT01.inc"
 move $L_CID  to $cid
 
 input $ieidos 'eidos ajiogr'
 unstring $ieidos by '"' into $filler $eidos $filler

 input $ihm_apo  'apo hmeromhnia '
 input $ihm_evs  'evs hmeromhnia '
 execute app_datecnv    $ihm_apo, $apohm output, $apohmpr output
 execute app_datecnv    $ihm_evs, $evshm output, $evshmpr output

 input $ilogar_apo 'apo logariasmo'
 input $ilogar_evs 'evs logariasmo'
 unstring $ilogar_apo by '"' into $filler $apologar $filler
 unstring $ilogar_evs by '"' into $filler $evslogar $filler

 if $eidos = 'Ε' or
    $eidos = 'E'
     move '0'        to $typ_ajio
     move 'Επιταγών' to $typos
     ! move '339000'   to $xartofylakio
 end-if
 if $eidos = 'Σ' or
    $eidos = 'S'
     move  '1'       to $typ_ajio
     move 'Συναλλαγματικών' to $typos
     ! move '310000'   to $xartofylakio
 end-if

 if $typ_ajio = ''
    show clear-screen (24,1) 'Λάθος Είδος Αξιογράφου' beep
    stop
 end-if

 do main

End-Report

! *************************************************************************

begin-procedure main
begin-select 
convert( datetime, a.hm_lhjh) &a.hm_lhjh        ()  On-Break print = never
                                                             level = 1
                                                             save  = $day
                                                             after = subtotals
                                                             skiplines = 1
a.cid                                           () NOP
a.aa_ajio                                       (, 01,05) edit {MASK2}
a.logarx                                        (, +1,08)
a.synal                                         (, +1,05)
b.epvnymia                                      (,+01,35)
a.kvdtrap                                       (,+01,20)
a.aa_epitaghs                                   (,+01,12)
convert( datetime, a.hm_ekd)  &a.hm_ekd         (,+02,08) edit {DATEMASK}
  print &a.hm_lhjh                              (,+02,08) edit {DATEMASK}
  print ''                                      (,+01,11)
  ! a.sxolio                                        (,+01,10)

  next-listing

a.ekdoths                                       (, 22,35)
a.ar_logtrap                                    (,+01,20)
a.toposplhr                                     (,+01,12) 
  print ''                                      (,+01,19)
a.poso_ejofl                                    (,+01,11) edit {MASK1}
  let #daytot = #daytot + &a.poso_ejofl
  ! a.prakt                                         (,+01,05)
       ! if &a.prakt    != ''
          ! do find_synal(&a.prakt,$praktoras)
          ! print $praktoras                        (,+01,12)
       ! end-if
  next-listing

from aj_mas a,sy_synal b
where a.synal = b.synal
  and a.typ_ajio = $typ_ajio
  and a.cid      = $cid
  and a.hm_lhjh between $apohm and $evshm
  and a.logarx  between $apologar and $evslogar
  and a.poso_ejofl != 0
!  and a.aa_ajio <= 67
[$SQLCMD]
order by a.logarx, a.hm_lhjh, a.aa_ajio
end-select

   do totals

end-procedure

! ******************************************************************

begin-procedure subtotals
   next-listing
   print 'Σύνολο Ημέρας  ['               (,76,16)
   print $day                             (,+1,08) edit {DATEMASK}
   print ']'                              (,+1,01)
   print #daytot                          (,+9,12) edit {MASK1}
   next-listing
   let #total = #total + #daytot
   move 0 to #daytot
end-procedure

! ******************************************************************

begin-procedure totals
   next-listing
   print 'ΓΕΝΙΚΟ ΣΥΝΟΛΟ ΠΕΡΙΟΔΟΥ  ['      (,56,25)
   print $apohm                           (,+1,08) edit {DATEMASK}
   print '-'                              (,+1,01)
   print $evshm                           (,+1,08) edit {DATEMASK}
   print ']'                              (,+1,01)
   print #total                           (,+9,12) edit {MASK1}
end-procedure

! ******************************************************************

begin-procedure find_synal($synal,:$epvnymo)

begin-select
a2.epvnymia         () NOP
from sy_synal a2
where a2.synal = $synal
end-select
move &a2.epvnymia     to $epvnymo
end-procedure

! ******************************************************************

begin-footing {FOOT-SIZE}
!  #INCLUDE "{$L_GDATA}/sqr/FOOT01.inc"
end-footing

begin-heading {HEAD-SIZE}
  #INCLUDE "{$L_GDATA}/sqr/HEAD01.inc"
  let $msg = 'Λ Η Ξ Ι Α Ρ Ι Ο   Α Ξ Ι Ο Γ Ρ Α Φ Ω Ν  ['||$typos||']' 
  print $msg                   (3,) Center
  print '-'                    (4,01,{PAGE-X}) fill
  print 'Αριθμ'                (5,01,5)
  print 'Λογαρ.'               ( ,+1,08)
  print 'Ονοματεπωνυμο Συναλλ' ( ,+1,41)
  print 'Τ ρ α π έ ζ α'        ( ,+1,20)
  print 'Αρ.Επιταγής'          ( ,+1,12)
  print '  Εκδοση'             ( ,+2,08)
  print '    Λήξη'             ( ,+2,08)
  print '           '          ( ,+1,11)
  ! print 'Παρατηρήσεις'         ( ,+1,30)

  print  $typos                (6,01,05)
  print '    =//=      εκδότη' ( ,+10,41)
  print 'Αριθμός Λογαριασμού'  ( ,+1,20)
  print 'Τόπος Πληρωμής'       ( ,+1,12)
  print '        '             ( ,+2,08)
  print '        '             ( ,+2,08)
  print '    Π ο σ ό'          ( ,+1,11)
  ! print 'Πρακτ'                ( ,+1,05)
  ! print 'Επωνυμία'             ( ,+1,20)
  print '-'                    (7,01,{PAGE-X}) fill
end-heading
